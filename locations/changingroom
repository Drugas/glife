# changingroom

if $ARGS[0] = 'view_swim_list':

	if $loc = 'bass' or ($loc = 'etoexhib' and temper >= 15) or (temper >= 15 and month >= 5 and month <= 9):
		if $clothingworntype ! 'swimwear':
			if $clothingworntype = 'nude':
				if $lastwornclothingtype ! 'swimwear':
					$normalclothingworntype = $lastwornclothingtype
					normalclothingwornno = lastwornclothingnumber
				end
			else
				$normalclothingworntype = $clothingworntype
				normalclothingwornno = clothingwornnumber
			end
		end

		i = 1
		h = 0
		:loopswimwear
			if swimwear[i] = 1 and swimwearS[i] = 0 and ($clothingworntype ! 'swimwear' or clothingwornnumber ! i):h += 1 & *p '<a href="exec:gt ''changingroom'', ''view_swim_item'', ''swimwear'', <<i>>"><img src="images/pc/clothing/12swimwear/<<i>>.jpg" 	height="250" /></a>'
			i += 1
		if i <= 65:jump 'loopswimwear'

		if h = 0:'<center>You do not own any swimwear.</center>'

	else
		'<center>You cannot go swimming or sunbathing now, so there is no point in getting changed.</center>'
	end

	if $clothingworntype = 'swimwear':
		act 'Put your regular clothes back on':
			gs 'clothing', 'wear', $normalclothingworntype, normalclothingwornno
			gt 'changingroom', 'view_swim_list'
		end
	end

	act 'Leave':gt $loc, $metka

end

if $ARGS[0] = 'view_swim_item':
	!! ARGS 0 - view_clothing_item
	!! ARGS 1 - clothing type
	!! ARGS 2 - clothing index

	cla
	'<center><img src="<<FUNC(''$clothing_image'',  $ARGS[1], ARGS[2])>>"></center>'
	'<<$ARGS[1]>> no.<<ARGS[2]>>'
	FUNC('$attributes_swimwear', $ARGS[1], ARGS[2])
	FUNC('$clothing_name', $ARGS[1], ARGS[2])


	$RESULT = '(strength '
	dynamic '$RESULT += <<$ARGS[1]>>H[<<ARGS[2]>>]'
	$RESULT += ')'
	'<<$RESULT>>'


	act 'Return':gt 'changingroom', 'view_swim_list'
	!! if the clothing is worn out
	if dyneval('RESULT = <<$ARGS[1]>>H[<<ARGS[2]>>]') <= 0:
		'This item is worn and is not suitable for further wear.'
		act 'Throw it away':
			dynamic '<<$ARGS[1]>>[<<ARGS[2]>>] = 0'
			gt 'changingroom', 'view_swim_list'
		end
	elseif CloExhibit > pcs_exhibition:
		'You don''t feel confident enough to wear swimwear this revealing.'
	else
		act 'Wear':
			gs 'clothing', 'wear', $ARGS[1], ARGS[2]
			gt 'changingroom', 'view_swim_list'
		end
	end
end

--- changingroom ---------------------------------

