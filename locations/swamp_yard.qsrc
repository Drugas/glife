# swamp_yard

if $ARGS[0] = 'start' or $ARGS[0] = '':
	$loc = 'swamp_yard'
	$metka = ''
	$menu_loc = 'swamp_yard'
	$menu_arg = ''
	menu_off = 0
	$location_type = 'secluded'

	gs 'gadukino_event', 'sound'
	gs 'stat'
	!the chance of encountering hunters

	if week = 6 or week = 7:huntersrand = RAND(1,3)
	if week < 6:huntersrand = RAND(1,5)

	if huntersrand = 1:hunters = 1
	!the chance of hitting GG, if she`s a slut
	huntslutsexrand = rand(1,3)
	! to remember were cowards or not
	!if lost_girl = 0:lost_girl += rand(40,60)
	if StoryLine = 1 and lost_girl = 0 and forest_lostday > 0: lost_girl=daystart+2

	swiss_army_random_number = rand(1,10)
	
	'<center><B>The yard in front of the hut</B></center>'
	if month >= 5 and month <= 9:
		if hour >= 6 and hour < 22:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/swamp_yard.jpg"></center>'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/swamp_yard_night.jpg"></center>'
		end
	else
		if hour >= 8 and hour < 18:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/swamp_yard_winter.jpg"></center>'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/swamp_yard_winter_night.jpg"></center>'
		end
	end

	'The yard in front of the hut.'
	'Behind the hut there is a <a href="exec: gt ''swampspring'' ">spring</a>, if you continue a little further down there is a small <a href="exec: gt ''backwater'' ">creek</a> with relatively clean water.'
	if findvel = 3:'On the side of the hut is an old <a href="exec:gt ''bicycle'',''swamp''">bike</a>.'
	if hunters > 0 and huntersKnow = 0:'You spot some unfamiliar people standing in front of the hut.'
	if hunters > 0 and huntersKnow > 0 and hour >= 8 and hour < 20:'You notice the hunters standing in the front yard.'

	act 'Go into the hut':gt 'swamphouse'
	

	!Conditional acts
	if dirty_swamp_yard > 10:act 'Clean up the trash outside': gs 'hunter_favors', 'yard_cleanup'
	if hunter_collective_opinion < 10: act '<b>Escape to the swamp</b>': gs 'swamp_yard', 'swamp_escape'

	! TODO: Figure how to remove the sneaky gotos if not tied to other locations
	if hunterhelp = 1: 
		gs 'swamp_yard', 'rescued_from_swamp'
	end

	if hunterhelp = 2 and huntersKnow = 0:
		gs 'hunter_interactions', 'meet_hunters_after_rescue'
	elseif huntersKnow = 0:
		act 'Approach the men'
	end

	if hour >= 8 and hour < 20: 
		gs 'swamp_yard', 'daytime_flavor_events'
	end
end	

!Code blocks for leaf events (events not calling other  events)
if $ARGS[0] = 'swamp_escape':
	!add chance to swamped by illume
	if hunters = 1:
		
		if hour >= 6 and hour < 21:
			minut += 5
			if rand(1,10)>9:
				gt 'swamp'
			else
				gt 'gadforestswamp','2'
			end		
		else
			*clr & cla
			gs 'stat'
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/igorhanter.jpg"></center>'
			'As you start moving into the swamp Igor calls after you.'
			
			!TODO: Different line depending on opinion and slut
			'"<<$pcs_nickname>>, where are you going? You will get stuck in there!'
			
			act 'Turn back':gt 'swamp_yard'
			act 'Ignore him and keep going':
				minut += 5

				!TODO: Add skill clothing and temp check here instead of praying to RNGesus
				if rand(1,10)>8:
					gt 'swamp'
				else
					gt 'gadforestswamp','2'
				end
			end
		end
	else
		minut += 5

		!TODO: Here too, also see how to avoid duplicating. Maybe create a child event node?
		if (hour >= 6 and hour < 21) or rand(1,10)>7:
			gt 'swamp'
		else
			gt 'gadforestswamp','2'
		end
	end

	gt 'gadforestswamp','2'
end

if $ARGS[0] = 'rescued_from_swamp':
	*clr & cla
	minut += 5
	gs 'stat'

	if $clothingworntype ! 'nude':
		'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/hanterhelp.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/hanterhelp1.jpg"></center>'
	end
	
	if $clothingworntype = 'nude':
		if huntersKnow = 0:
			if pcs_apprnc >= 100:
			'"Are you... a mermaid?" You hear one of the men blurt out before blushing'
			else 
			'What the hell happened to you? Are you alright?'
			end
		end	
		if huntersKnow = 1 and huntersKnowSlut = 0:'"<<$pcs_nickname>>, what happened to you? Where are your clothes?" Andrei said concernedly.'
		if huntersKnow = 1 and huntersKnowSlut = 1:'Well isn''t this our favorite damsel in distress. And she''s even naked and ready for us so we can jump straight to the happy ending! Andrei grinned.'
	end
	
	if huntersKnow = 0:
		'The hunters help you to a dry place. For some time you just sit there, staring blankly ahead, trying to catch your breath. After a while feeling better you decide to find out where you are and who it was that saved you.'
		act 'Look':hunterhelp = 2 & gt 'swamp_yard'
	else
		'The hunters help you to a dry place. For some time you just sit there, staring blankly ahead, trying to catch your breath. After a short rest you go out to the yard.'
		act 'Further':hunterhelp = 0 & gt 'swamp_yard'
	end
end

!TODO: Move these to the interactions or favors files
!TODO: Redo the events here after working on the other locations
if $ARGS[0] = 'confronted_after_drunken_gangbang':
	if huntdanceslut = 1:
		cla
		*clr
		minut+=5
		gs 'stat'

		'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/andreihanter.jpg"></center>'
		'As soon as you walk into the yard, you immediately walk up to Andrei.'
		'"I need to talk to you, <<$pcs_nickname>>," he said. "Can you recall what happened last night?"'
		'"Well... uh... Yes, I think I can..." you reply, blushing, staring down at the ground.'
		'"I used to say that you look just like your mother," said Andrei looking at you. "After your little show yesterday, as it turns out, the resemblance doesn''t stop at just the looks."'
		'Andrei paused looking at you while you try to collect your thoughts to understand what he''s trying to imply.'
		'"To be frank, you''re as slutty as your mother was in her youth, the apple doesn''t fall far away from the tree as they say..." he chuckles. "Take it as a compliment."'
		'You look at Andrei, completely stunned by what you just heard.'
		'"And what are sluts best at?" he continues. "That''s right, to pleasure men. Don''t you agree?"'

		if pcs_dom > 0 and alko < 8:act'Refuse':huntdanceslut = 3 & gt 'hunters', 'start'
		act'Agree':huntdanceslut = 2 & gt 'hunters', 'start'
	end
end	 

!Assorted sex scenes for the yard
if $ARGS[0] = '$andrei_quick_fuck':
	cla & *clr
	!TODO: bunch of images needed here
	!if hunter_collective_opinion > 45:
	if hunter_collective_opinion < 25:
	!elseif hunter_collective_opinion > 25:

	!else
		'Andrei walks up to you without saying another word and forcefully pulls you closer by your nipples.'
		gs 'pain', 10, 'pinch', 'nipples'
		'You yelp but it is cut short when he shoves his tongue down your throat, roughly fondling you all over'
		gs 'arousal', 'foreplay', 25, 'sub'
		act 'You feel your pussy starting to get wet':
			cla & *clr
			!TODO: Yes, images
			'He definitely feels it too as he keeps shoving his fingers in and out. Satisfied, he turns you around by the shoulders and forcefully bends you over'
			'You close your eyes anticipating what is coming. He wastes no time unzipping his pants and shoves his rock hard cock all the way in your ready pussy, eliciting a loud moan from you.'
			'He starts fucking you rough and hard, not caring what you are getting out of it. You are merely a cocksleeve for him.'
			gs 'arousal', 'vaginal_finger', '15', 'sub'
			gs 'arousal', 'vaginal', '30', 'sub', 'rough'
			act '...':
				cla & *clr
				!TODO: Image here
				'After a few minutes he grabs you by the shoulders and unceremoniously shoves you on the ground on your back.'
				'You watch him furiously jerk standing over you. You realize you have been playing with yourself for some time when he starts shooting his load all over your tits and belly'
				
				gs 'arousal', 'masturbate', 15
				gs 'arousal', 'end'
				gs 'cum_call', 'breasts', 'A172', 1
				gs 'cum_call', 'stomach', 'A172', 1
				gs 'stat'
				
				act 'Further': gt 'swamp_yard'
			end 
		end
	end
end

if $ARGS[0] = 'igor_demonstration': 
	!TODO: Image here
	'Seeing Igor staring at your naked body like a deer in the headlights gives you an idea.'
	act 'Show him it is no big deal':
		cla & *clr
		hunter_sexual_comfort += 3
		hunter_collective_opinion += 7
							
		!TODO: Mp4 here 
		'You smile at Igor as you start swaying slowly, getting into something resembling a rhythm'
		'"The clothes just get in the way when I do this. Why not join me for a dance?" you say.'
		'He starts awkwardly swaying to match you as you two get into it, with you acting like you are not dancing completely naked in front of the men.'
		'As he starts getting into the rythm, the other men take notice and join in too. Sergei puts on some music as you all spend some time dancing like you are at a disco.'
							
		gs 'arousal', 'flash', 25, 'dom', 'exhibitionism'
		gs 'arousal', 'end'
		gs 'stat'

		act 'Further': gt 'swamp_yard'   
	end

	act 'Give him a hands on demonstration':
		cla & *clr
		hunter_sexual_comfort += 5
		hunter_collective_opinion += 1

		!TODO: Gif and images here as well as the rest of the scene
		'You smile at Igor as you approach him and grab his hand'
		'"It is no big deal to me, why are you the one that''s embarrassed?" you say as you put his hand on your breasts'
		'He starts blushing to his ears as you lead him to play with your tits. He starts kneading your breasts lightly after a minute but does not go further.'
		'After a minute he stops and starts going away but not before you notice the satisfied smirk on his face.'

		gs 'arousal', 'breasts', 5, 'dom', 'exhibitionism'
		gs 'arousal', 'end'
		gs 'stat'

		act 'Further': gt 'swamp_yard'
	end
end

if $ARGS[0] = 'skirt_breeze':
	if CloSkirtShortness > 1 and $pantyworntype = 'none': 
		*clr & cla
		hunter_sexual_comfort += 2
		

		'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/no_tanga.jpg"></center>'
		'You walk across the yard, when suddenly the wind lifts you skirt.'
		'"I forgot to put on panties...," you realize this seeing your bare pussy.'
		'You nervously started looking around if anyone caught a glimpse of your little wardrobe malfunction...'
		'You hoped that the men missed the opportunity seeing you exposed, however, Igor''s face had turned red and you see Andrei grinning, you realize quite quickly that your little show did not pass unappreciated.'
		gs 'arousal', 'flashlite', 1
		gs 'arousal', 'end'
		gs 'stat'
		
		act 'Further':gt 'swamp_yard'
	end
end

!TODO: This will be revamped. Temporary to keep the dialogue for now
if $ARGS[0] = 'hunter_group_sex':
	! sex if GG whore
	if hunters > 0 and huntersRape = 5 and huntslutsexrand = 1 and huntslutsex = 0:
		*clr & cla
		minut += 5
		temphunt = rand(1,3)
		! the choice of who will fuck GG
		slutgosex = 1
		gs 'stat'

		if temphunt = 1:'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/andreihanter.jpg"></center>'
		if temphunt = 2:'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/sergeihanter.jpg"></center>'
		if temphunt = 3:'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/igorhanter.jpg"></center>'
		if temphunt = 1:'Andrei steps out of the hut, heading straight towards you. "<<$pcs_nickname>>, walk with me..." without uttering another word he grabs you by the arm and leads you to the nearest tree...'
		if temphunt = 2:'Sergei steps out of the hut, heading straight towards you. "<<$pcs_nickname>>, let''s go a bit aside you need to help me out with my itch," he said and lead you in the direction of the nearest thicket.'
		if temphunt = 3:'Igor steps out of the hut, heading straight towards you. "<<$pcs_nickname>>, I need you to help me with this bulge," he says pointing at his groin, leading you away from the yard...'
		if pcs_horny < 40:' You obediently follow along, well knowing the reason he wanted you to tag along.'
		if pcs_horny >= 40 and pcs_horny < 70:'"I need you to take me..." is all you manage to say while he''s leading you away.'
		if pcs_horny >= 70:'"I was hoping this would happened," you think to yourself, feeling your wet pussy pulsating with desire. You jump onto him, taking charge, leaving him stunned about who wants it more, you or him.'

		act 'Go':gt 'hunters', 'start'
	end
		
	! trio if GG whore
	if hunters > 0 and huntersRape = 5 and huntslutsexrand = 2 and huntslutsex = 0:
		*clr & cla
		minut += 5
		temphunt = rand(1,3)
		slutgosex = 2
		gs 'stat'

		'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/hanterswork1.2.jpg"></center>'
		if temphunt = 1:
			'You see Andrei talking with Sergei in the yard. They notice you, and start moving towards you. "Come with us <<$pcs_nickname>>," without uttering another word Andrei grabs you by the arm and leads you to the nearest tree.... Sergei follows after.'
		elseif temphunt = 2:
			'Andrei and Igor notice you enter the yard, they call you over. "Come with us <<$pcs_nickname>>, you need to help us out with our itch," said Andrei pulling you in the direction of the nearest thicket. Igor follows behind.'
		elseif temphunt = 3:
			'You notice Igor and Sergei standing in the yard whispering something to each other. Igor approaches you and says "<<$pcs_nickname>>, "<<$pcs_nickname>>, I need you to help me with this bulge," he says pointing at his groin. He calls Sergei over and they start leading you away from the yard...'
		end
		if pcs_horny < 40: 
			'You obediently follow along, well knowing the reason they wanted you to tag along.'
		elseif pcs_horny >= 40 and pcs_horny < 70:
			'"I need you to take me..." is all you manage to say while they''re leading you away.'
		elseif pcs_horny >= 70:
			'"I was hoping this would happened," you think to yourself, feeling your wet pussy pulsating with desire. You jump onto one of them, taking charge, leaving the other one stunned about who wants it more, they or you.'
		end

		act 'Go with the guys':
			*clr & cla
			minut += 5
			pcs_horny += 5

			gs 'stat'

			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/doublehanterlove.jpg"></center>'
			'You go along with the men holding their hands, knowing where they''re about to lead you. You don''t mind you want them as bad as they wants you...'
			act 'Further':gt 'huntersex', 'sluthomesex'
		end
	end
end

!Code blocks for branch events (events calling other  events)

if $ARGS[0] = 'daytime_flavor_events': 
	if rand(1,20) = 1: gs 'swamp_yard', 'skirt_breeze'
	elseif rand(1,12) = 1 : gs 'hunter_favors', 'housekeeping_request'
	elseif rand(1,20) = 1: gs 'hunter_ambient' , 'yard_convo'
	elseif rand(1,10) = 1: gs 'hunter_interactions', 'naked_encounter'
	elseif rand(1,20) = 1: gs 'hunter_ambient', 'bring_food'
end

if $ARGS[0] = 'nighttime_flavor_events':
	!TODO: Add stuff here
end
--- swamp_yard ---------------------------------

