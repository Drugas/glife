# outdoors

weatherGrM[1] = -15
weatherGrM[2] = -10
weatherGrM[3] = -5
weatherGrM[4] = 10
weatherGrM[5] = 15
weatherGrM[6] = 20
weatherGrM[7] = 25
weatherGrM[8] = 25
weatherGrM[9] = 20
weatherGrM[10]= 10
weatherGrM[11]= -5
weatherGrM[12]= -10

weatherGrDelta = 5

weatherFallM[1] = 60
weatherFallM[2] = 20
weatherFallM[3] = 20
weatherFallM[4] = 30
weatherFallM[5] = 30
weatherFallM[6] = 20
weatherFallM[7] = 15
weatherFallM[8] = 15
weatherFallM[9] = 30
weatherFallM[10]= 50
weatherFallM[11]= 60
weatherFallM[12]= 30

weatherFallDelta = 20

DayLengthMin = 177 &! Minutes from dawn to noon
DayLengthMax = 563

if $ARGS[0] = 'weather':
	if weatherDay ! DayInYear:
		weatherTemper = rand(weatherGrM[month]-weatherGrDelta, weatherGrM[month]+weatherGrDelta)
		weatherFall = rand(weatherFallM[month]-weatherFallDelta, weatherFallM[month]+weatherFallDelta)
	end
	if (weatherHour ! hour) or (weatherDay ! daystart):
		weatherDay = DayInYear
		weatherHour = hour

		temper = hour
		if temper < 2 : temper = 24 + temper
		temper -= 14
		if temper > 0 : temper = -temper
		temper += 6
		temper = weatherTemper + (weatherGrDelta*100/6*temper)/100
		
		if chWeather = 0:
			sunWeather = IIF( rand(0,100)<weatherFall, 0, 1)
		else
			killvar 'chWeather'
		end

		if temper < -5 :
			if weatherFall < 30 :
				if sunWeather = 1 :
					$osadki = 'Clear and cloudless.'
				else
					$osadki = 'Clear sky, but light snow falls.'
				end
			elseif weatherFall < 60 :
				if sunWeather = 1 :
					$osadki = 'Partly cloudy and light breeze.'
				else
					$osadki = 'Clouds and it''s snowing.'
				end
			else
				if sunWeather = 1 :
					$osadki = 'Clouds all over the sky and a sharp cold wind.'
				else
					$osadki = 'Strong snow with wind.'
				end
			end
		elseif temper < 5 :
			if weatherFall < 30 :
				if sunWeather = 1 :
					$osadki = 'Clear and cloudless.'
				else
					$osadki = 'Slightly wet snow.'
				end
			elseif weatherFall < 60 :
				if sunWeather = 1 :
					$osadki = 'Partly cloudy and light breeze.'
				else
					$osadki = 'Clouds and wet snow.'
				end
			else
				if sunWeather = 1 :
					$osadki = 'Clouds all over the sky and a sharp wind.'
				else
					$osadki = 'Heavy snow, sticking wet snow.'
				end
			end
		else
			if weatherFall < 30 :
				if sunWeather = 1 :
					$osadki = 'Clear and cloudless.'
				else
					$osadki = 'Light mushroom rain.'
				end
			elseif weatherFall < 60 :
				if sunWeather = 1 :
					$osadki = 'Partly cloudy skies.'
				else
					$osadki = 'It''s raining.'
				end
			else
				if sunWeather = 1 :
					$osadki = 'Clouds all over the sky and a sharp wind.'
				else
					$osadki = 'Heavy rain and wind.'
				end
			end
		end

		gs 'outdoors', 'temp_set'

		jump 'WTHR_<<month>>'
		:WTHR_1
			$weather = '<b>On the street there is snow, the temperature is <<$temperature>> degrees Celsius.<br>  <<$osadki>></b>'
			jump 'WTHR_x'
		:WTHR_2
			$weather = '<b>On the street there is snow, the temperature is <<$temperature>> degrees Celsius.<br>  <<$osadki>></b>'
			jump 'WTHR_x'
		:WTHR_3
			$weather = '<b>On the street there is snow, the temperature is <<$temperature>> degrees Celsius.<br>  <<$osadki>></b>'
			jump 'WTHR_x'
		:WTHR_4
			$weather = '<b>On the street the snow melts, the temperature is <<$temperature>> degrees Celsius.<br>  <<$osadki>></b>'
			jump 'WTHR_x'
		:WTHR_5
			$weather = '<b>Puddles in the street left by snow, winter has covered everything in dirt and debris, some places where new grass grows are pleasantly green, the temperature is <<$temperature>> degrees Celsius.<br>  <<$osadki>></b>'
			jump 'WTHR_x'
		:WTHR_6
			$weather = '<b>Green grass lines the streets, the temperature is <<$temperature>> degrees Celsius.<br>  <<$osadki>></b>'
			jump 'WTHR_x'
		:WTHR_7
			$weather = '<b>Green grass lines the streets, the temperature is <<$temperature>> degrees Celsius.<br>  <<$osadki>></b>'
			jump 'WTHR_x'
		:WTHR_8
			$weather = '<b>The grass is green, in the trees the leaves are already turning yellow, the temperature is <<$temperature>> degrees Celsius.<br>  <<$osadki>></b>'
			jump 'WTHR_x'
		:WTHR_9
			$weather = '<b>Streets lined with green grass, visible yellow leaves, the temperature is <<$temperature>> degrees Celsius.<br>  <<$osadki>></b>'
			jump 'WTHR_x'
		:WTHR_10
			$weather = '<b>The grass is fading and patchy, yellow and brown leaves fall from the trees, mud and puddles dot the street, temperature is <<$temperature>> degrees Celsius.<br>  <<$osadki>></b>'
			jump 'WTHR_x'
		:WTHR_11
			$weather = '<b>The street is dirty with a thin snow cover, temperature is <<$temperature>> degrees Celsius.<br>  <<$osadki>></b>'
			jump 'WTHR_x'
		:WTHR_12
			$weather = '<b>The street is covered in snow, the temperature is <<$temperature>> degrees Celsius.<br>  <<$osadki>></b>'
			jump 'WTHR_x'
		:WTHR_x
	end	
end

if $ARGS[0] = 'temp_set':
	if temper < 0:
		$temperature = '<font color=blue><<temper>></font>'
	elseif temper < 10:
		$temperature = '<font color=green><<temper>></font>'
	elseif temper < 20:
		$temperature = '<font color=orange><<temper>></font>'
	else
		$temperature = '<font color=red><<temper>></font>'
	end
end

if $ARGS[0] = 'main':

	DayInYear=0
	if month > 1:DayInYear += 31
	if month > 2:
		if ((year mod 4 = 0) and (year mod 100 ! 0)) or (year mod 400 = 0):
			DayInYear += 29
		else
			DayInYear += 28
		end
	end
	if month > 3:DayInYear += 31
	if month > 4:DayInYear += 30
	if month > 5:DayInYear += 31
	if month > 6:DayInYear += 30
	if month > 7:DayInYear += 31
	if month > 8:DayInYear += 31
	if month > 9:DayInYear += 30
	if month > 10:DayInYear += 31
	if month > 11:DayInYear += 30
	if month > 12:DayInYear += 31
	DayInYear += day

	if DayInYear >= 171 :
		DayInYear2 = DayInYear-171
	else
		DayInYear2 = 171-DayInYear
	end
	DayLength = DayLengthMax-((DayLengthMax-DayLengthMin)*100/182)*DayInYear2/100
	! length/2 in minutes current day

	CurTimeSun = hour*60 + minut
	! midday in 13:00
	! CurTimeSun (minuts) <0 - before midday; >0 - after midday
	if CurTimeSun >= 780 :
		CurTimeSun = CurTimeSun-780
	else
		CurTimeSun = -(780-CurTimeSun)
	end

	DayStage=5
	BColorBase=0
	! 1 - dawn
	! 2 - midday
	! 3 - sunset
	! 4 - the beginning of the night
	! 5 - night
	! 6 - the end of the night
	if CurTimeSun >= -(DayLength-60) and CurTimeSun <= (DayLength-60) :
		DayStage=2
		BColorBase=255
	elseif CurTimeSun >= -DayLength and CurTimeSun <= -(DayLength-60) :
		DayStage=1
		BColorBase=128+(DayLength+CurTimeSun)*2 &! CurTimeSun - negative, result = 128+ [0-60]*2
	elseif CurTimeSun >= (DayLength-60) and CurTimeSun <= DayLength :
		DayStage=3
		BColorBase=256+((DayLength-60)-CurTimeSun)*2 &! CurTimeSun - positive, result =128+ [0-60]*2
	elseif CurTimeSun >= DayLength and CurTimeSun <= DayLength+60 :
		DayStage=4
		BColorBase=128-(CurTimeSun-DayLength)*2  &! CurTimeSun - positive, result =128- [0-60]*2
	elseif CurTimeSun >= -(DayLength+60) and CurTimeSun <= -DayLength :
		DayStage=6
		BColorBase=128+(CurTimeSun+DayLength)*2  &! CurTimeSun - negative, result = 128- [0-60]*2
	end

!	*pl '    DayInYear=<<DayInYear>> DayLength=<<DayLength>>  CurTimeSun=<<CurTimeSun>>  DayStage=<<DayStage>>'

	$weatherImage = '<img src="images/system/weather/WTHR'
	$weatherImage = $weatherImage + '<<DayStage>>'
	if temper < -10 : 
		$weatherImage += '1'
	elseif temper <= 5 :
		$weatherImage += '2'
	elseif temper <= 19 :
		$weatherImage += '3'
	else
		$weatherImage += '4'
	end
	if weatherFall < 30 :
		$weatherImage += '1'
	elseif weatherFall < 60 :
		$weatherImage += '2'
	else
		$weatherImage += '3'
	end
	if sunWeather = 1 :
		$weatherImage += '1'
	else
		$weatherImage += '2'
	end
	$weatherImage += '.jpg">'

!	clr
!	pl $weatherImage
!	pl
!	pl $weather
!	pl $osadki

	if night_mode = 1:
		fcolor = rgb(255, 255, 255)
		bcolor = rgb(0, 0, 0)
	else
		if (DayStage = 1) or (DayStage = 6) :
			bcolor = rgb(BColorBase/2+BColorBase/4, BColorBase/2+BColorBase/4, BColorBase)
		elseif (DayStage = 3) or (DayStage = 4) :
			bcolor = rgb(BColorBase, BColorBase/2+BColorBase/4, BColorBase/2+BColorBase/4)
		else
			bcolor = rgb(BColorBase, BColorBase, BColorBase)
		end
		
		if BColorBase <= 160 :
			fcolor = rgb(255, 255, 255)
		else
			fcolor = rgb(0, 0, 0)
		end
		lcolor = rgb(106, 90, 205)
	end

	if $location_type = 'public_outdoors' or $location_type = 'secluded' or $location_type = 'event_outdoors':
		if sunWeather = 0 and umbrella = 0:
			*NL 'It is raining and you do not have an umbrella.'
			if pcs_makeup > 1:
				pcs_makeup = 0
				*p ' The rain has ruined your makeup.'
			end

			if pcs_hairbsh = 1:
				pcs_hairbsh = 0

				if curly > 0:curly -= 1

				*p ' Your hair is soaked.'
			end
			*nl
		end

		if temper <= 0:
			if (totminut - Time15Minute) >= 15:
				!reset Time15Minute
				if frost = 0:Time15Minute = totminut
				! begin Loop
				:Time15MinuteLoop

				if defaultcoat = 5:bonusZ = -3
				if defaultcoat = 0:bonusZ = 0
				if defaultcoat = 3 or defaultcoat = 9 or defaultcoat = 11 or defaultcoat = 12 or defaultcoat = 15 or defaultcoat = 18:bonusZ = 3

				if temper < -10 and defaultcoat < 4 and defaultcoat ! 0:
					if temper < -30:
						frost += 2
					else
						frost += 1
					end
					if pcs_energy < 20:frost += 1
					if tanga = 0:frost += 1
					if pcs_sweat > 20:frost += 1
				elseif temper < -20 and (defaultcoat = 4 or defaultcoat = 5 or defaultcoat = 8 or defaultcoat = 10 or defaultcoat = 12 or defaultcoat = 13 or defaultcoat = 14 or defaultcoat = 16 or defaultcoat = 17 or defaultcoat = 18 or defaultcoat = 19):
					frost += 1
					if pcs_energy < 20:frost += 1
					if pcs_sweat > 20:frost += 1
				elseif temper < -30 and (defaultcoat = 6 or defaultcoat = 7 or defaultcoat = 9 or defaultcoat = 11 or defaultcoat = 15):
					frost += 1
				end

				if defaultcoat = 0:
					if temper < -10:frost += 1
					if temper < -20:frost += 2
					if temper < -30:frost += 5
				end

				if frost >= 12:sick += 1
				if sick > 0 and sickstage = 0:sickstage = 1

				Time15Minute += 15

				if (totminut - Time15Minute) >= 15:jump 'Time15MinuteLoop'
			end
		end

!!15 minute check to stop refreshes causing early gameover and cheating at exhibitionism
		if outdoors_checks + 15 < totminut:
			outdoors_checks = totminut
			if isprok = 0 and isprokp = 0 and mesec > 0 and preg = 0:
				'<b><font color="red">You have the blood flowing down your legs and staining your clothes.</font></b>'

				if clrbelo > 0 and tanga = 1:clrbelo -= 1 & tanga = 0 & '<b><font color="red">Your panties have been ruined and you discreetly remove and dispose of them.</font></b>'

				pcs_sweat += 10
				pcs_mood -= 3
				pcs_willpwr -= 10
				dynamic '<<$clothingworntype>>H[<<clothingwornnumber>>] -= 1'
			end

!!Exhibitionism

			if CloExhibit >= pcs_exhibition - 10:
				exhibition_outdoors += 1
				if pcs_exhibition < 45 and PCloPanties = 1: exhibition_outdoors += 1
			end

			if tanga = 0:
				if PCloSkirt = 3:
					exhibition_outdoors += 1
				elseif PCloSkirt = 4:
					exhibition_outdoors += 2
				elseif PCloSkirt = 5:
					exhibition_outdoors += 3
				elseif PCloSkirt = 6:
					exhibition_outdoors += 4
				end
			end
		end
	else
		if night_mode = 1:
			fcolor = rgb(255, 255, 255)
			bcolor = rgb(0, 0, 0)
			lcolor = rgb(106, 90, 205)
		else
			fcolor = rgb(0, 0, 0)
			bcolor = rgb(255, 255, 255)
			lcolor = rgb(106, 90, 205)
		end
		gs 'indoors'
	end
end
	
--- outdoors ---------------------------------

