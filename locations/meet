# meet

if $ARGS[0] = 'start':
	minut += 15
	dayA += 1
	svidanieA = 0
	giftNum = giftNumA
	fcolor = rgb(0, 0, 0)
	bcolor = rgb(255, 255, 255)
	lcolor = rgb(106, 90, 205)
	gs 'boyStat', $boyA
	$boybody = $boybodyA
	$boybod = $boybodA
	$boyface = $boyfaceA
	!! sexual force of 0 prior_to 2
	silaVag = silaVagA
	!!security, is responsible for the money and requirements 0 beggar, 1 middle_peasant 2 rich
	finance = financeA
	!!check in appearance, a parameter pcs_apprnc
	vneshBoy = vneshBoyA
	$boyClo = $boyCloA
	!!0 - ass less 80, 1 back from 80 prior_to 100, 2 ass over 100
	figurBoy = figurBoyA
	!!0 - chest less 3, 1 breast 3-4, 2 breasts bigger 4
	titBoy = titBoyA
	!!favorite hair color 0 - brunette, 1 - haired, 2 - Redhead, 3 - blonde
	haerBoy = haerBoyA
	!!character 0 - soft, 1 -norm 2 - aggressor
	harakBoy = harakBoyA
	!!perversity
	izvrat = izvratA
	husb_birth_year = year - boyage
	husbAge = boyage
	
	!!new preferences
	
	!!This is for clothing type preference
	!!0 - none, 1 - cheap, 2 - average, 3 - formal, 4 - naughty.
	clotTypePref = clotTypePrefA
	
	!!This is for the quality of clothing
	!! 0 - no preference, 1 - 1 to 4 units cheap to average (Locked if formal chosen), 2 - 5 to 7 units expensive to ballroom quality (Locked if naughty is chosen)
	clotQualPref = clotQualPrefA
	
	!!Need to add modifiers based on breast size, where larger breasts end up forcing the player to show more cleavage than those with smaller breasts.
	!! 0 - no preference, 1 - 1 to 2 or neckline to moderate, 2 - 3 to 4 or heavy to severe, 3 - 5 to 7 or buldging to outpouring 
	!! The breast size must be adjusted either when the action is being calculeted or we can base it on their ideal breast size :titBoy + 0/3 depending on the players breast size
	
	!! How much they want to see your chest USE THIS FOR NOW
	!! 0 - no preference, 1 - 1 neckline , 2 - 2 or moderate, 3 - 3 to 4 or heavy to severe
	!! It doesn''t matter if your poor or rich, everyone loves tits.
	clotTopPref = clotTopPrefA
	
	!! This is for pants and skirt shortness and will be grouped together as such.
	!! 0 - no preference, 1 - 1 to 2 units or ankle to calf length, 2 - 3 to 4 units or knee to miniskirt, 3 - 5 units to 6 or slut to hoe.
	bottShorPref = bottShorPrefA
	
	!! This is for clothes thinness.
	!! 0 - no preference, 1 - 0 to 2 (Conservative), 2 - 3 to 4 (Moderate), 3 - 4 to 6 (really thin)
	!! I see the moderate to really thin overlapping. Its intentional.
	clotThinPref = clotThinPrefA
	
	!!This is to find out if they want you to wear bimbo clothing
	!! 0 - no preference, 1 - Prefers bimbo clothing.
	bimbPref = bimbPrefA
	
	!!This is to find out if they want you to wear piercings.
	!! 0 - no preference, 1 - 0 to 2 piercings, 2 - 3 to 5 piercings, 3 - 6 or more piercings.	
	pierPref = pierPrefA
	
	!!This is to find out if they want you to have tattoos.
	!! 0 - no preference, 1 - 0 to 2 tattoos, 2 - 3 to 5 tattoos, 3 - 6 or more tattoos.
	tattPref = tattPrefA
	
	!!This is to find out how big they want your lips.
	!! 0 - no preference, 1 - 2 to 3 units (average) , 2 - 4 units (THIC lips)
	lipsPref = lipsPrefA
	
	!!This is to find out how big they want YOU.
	!! 0 - no preference, 1 - 1 to 3 units (Skinny) , 2 - 3 to 6 units (Average), 3 - 6 + units (THIC)
	!! Notice how this one overlaps. I believe that it should because changing ones weight is very hard.
	bodyPref = bodyPrefA
	
	!!This is to find out how much makeup they want you to wear.
	!! 0 - no preference, 1 - 1 (light makeup) , 2 - 2 (moderate makeup), 3 - 3(heavy makeup)
	makePref = makePrefA
	
	
	

	if love = 3:
		'<<$boydescA>> has hired a wedding limousine and it''s outside waiting for you. This is it, you are getting married today.'

		act 'Sit in the limousine':
			cla
			'You get into the limo, and it drives off. Soon you arrive at the wedding, <<$boydescA>> is waiting for you as you walk down the aisle.'

			act 'Marry husband':
				cla
				minut += 60
				love = 0
				bfA = 0
				husband = 100
				husbanday = 0
				$husName = $boyA
				$husHair = $boyfaceA
				$husBody = $boybodyA
				$husFat = $boybodA
				husDick = dickA
				harakHusb = harakBoyA
				izvratH = izvratA
				silavagH = silavagA
				husbFin = financeA
				husbandMark = 1
				'The wedding reception takes place at a cafe. There aren''t many guests since neither you nor the groom have that many friends or family members. There is a lot of congratulations, toasts, merrymaking and crying. By the evening, everybody is drunk and you and your husband stagger into a taxi, which takes you home.'

				act 'Wedding night':gt 'husbsex', 'room'
			end
		end

		exit
	elseif love = 2:
		'<<$boydescA>> looks very excited and suddenly pulls a ring out of a box.'
		'<<$boydescA>> says "<<$pcs_nickname>>. I offer you my hand and heart. Would you marry me?"'

		act 'No (turn him down)':
			minut += 60
			bfA = 0
			pregtalk = 0
			gt 'street'
		end

		act 'Yes':
			cla
			'You agree and put the ring on your finger. <<$boydescA>> rejoices and happily hugs and kisses you.'
			'<<$boydescA>> says "Now you need to find a wedding dress."'

			act 'Go looking for the dress':
				cla
				minut += 60
				love = 3
				'You get into a taxi with your soon to be husband and go to the store to choose a dress. You choose an expensive and luxurious dress. <<$boydescA>> pays for the purchase.'
				'After a long day you feel tired and ask <<$boydescA>> to drive you home, warning him that the next time you''ll meet you will get married.'

				act 'Leave':gt 'street'
			end
		end

		exit
	end

	if pcs_makeup > 1 and ($clothingworntype = 'expensive' or $clothingworntype = 'formal') and curly > 0 and love = 1 and harakBoyA = 0:
		'<<$boydescA>> looks very excited. He decides that you are now ready to meet his mother.'

		act 'Meet the parents':gt 'love', 'mother'

		exit
	elseif pcs_makeup > 1 and ($clothingworntype = 'expensive' or $clothingworntype = 'formal') and curly > 0 and love = 1 and harakBoyA = 1 and tanga = 1:
		'<<$boydescA>> examins you and decides that you are ready to meet his friends.'

		act 'Meet his friends':gt 'love', 'frends'

		exit
	end

	if pcs_skin < 20:
		bfa -= 2 & $meetskin = 'looking at his feet'
	elseif pcs_skin < 40:
		bfa -= 1 & $meetskin = 'giving you a quick glance'
	elseif pcs_skin < 60:
		$meetskin = 'looking at you'
	elseif pcs_skin < 80:
		bfa += 1 & $meetskin = 'smiling at you'
	else
		bfa += 2 & $meetskin = 'smiling at you'
	end

	if harakBoyA = 0:
		if pcs_makeup <= 2 and pcs_makeup > 0:bfA += 1 & $meetmop = 'You look very beautiful today.'
		if pcs_makeup = 4:bfA -= 1 & $meetmop = 'Great weather we are having today.'
	elseif harakBoyA = 1:
		if pcs_makeup <= 3 and pcs_makeup > 0:bfA += 1 & $meetmop = 'You look very beautiful today.'
		if pcs_makeup = 4:bfA -= 1 & $meetmop = 'Your makeup looks like you''re going to a Halloween party.'
	elseif harakBoyA = 2:
		if pcs_makeup = 4:bfA += 1 & $meetmop = 'You look very beautiful today.'
		if pcs_makeup <= 2 and pcs_makeup > 0:bfA -= 1 & $meetmop = 'Great weather we are having today.'
	end

	if pcs_makeup = 0:bfA -= 1 & $meetmop = 'Um, your makeup is running.'
	if pcs_hairbsh = 1 and curly > 0:bfA += 2 & $meethair = 'Your hair looks very beautiful.'
	if pcs_hairbsh = 1 and curly = 0:bfA += 1 & $meethair = 'You have pretty hair today.'
	if pcs_hairbsh = 0:bfA -= 1 & $meethair = 'You look like you were in a hurry.'
	if harakBoyA = 0:$meet = '"Hello darling, I am glad to see you, <<$meetmop>> <<$meethair>>"'
	if harakBoyA = 1:$meet = '"Hello <<$pcs_nickname>>, I am glad to see you, <<$meetmop>> <<$meethair>>"'
	if harakBoyA = 2:$meet = '"Hi <<$pcs_nickname>>! Glad to see you, <<$meetmop>> <<$meethair>>"'

	$meetcol = ''

	if dayA > 10 and harakBoy = 2:
		if haerBoyA ! pcs_haircol:
			bfA = bfA - 5

			if haerBoyA = 0:$meetcol = '<<$boydescA>> tells you that he prefers black hair.'
			if haerBoyA = 1:$meetcol = '<<$boydescA>> tells you that he prefers blonde hair.'
			if haerBoyA = 2:$meetcol = '<<$boydescA>> tells you that he prefers red hair.'
			if haerBoyA = 3:$meetcol = '<<$boydescA>> tells you that he prefers light hair colors.'
		elseif haerBoyA = pcs_haircol:
			bfA += 1
			'<<$boydescA>> notes that you have a wonderful hair color.'
		end
	end

	if dayA > 20 and harakBoy = 1:
		if haerBoyA ! pcs_haircol:
			bfA = bfA - 5

			if haerBoyA = 0:$meetcol = '<<$boydescA>> tells you that he prefers black hair.'
			if haerBoyA = 1:$meetcol = '<<$boydescA>> tells you that he prefers blonde hair.'
			if haerBoyA = 2:$meetcol = '<<$boydescA>> tells you that he prefers red hair.'
			if haerBoyA = 3:$meetcol = '<<$boydescA>> tells you that he prefers light hair colors.'
		elseif haerBoyA = pcs_haircol:
			bfA += 1
			'<<$boydescA>> notes that you have a wonderful hair color.'
		end
	elseif dayA > 30 and harakBoy = 0:
		if haerBoyA ! pcs_haircol:
			bfA = bfA - 5

			if haerBoyA = 0:$meetcol = '<<$boydescA>> tells you that he prefers black hair.'
			if haerBoyA = 1:$meetcol = '<<$boydescA>> tells you that he prefers blonde.'
			if haerBoyA = 2:$meetcol = '<<$boydescA>> tells you that he prefers red hair.'
			if haerBoyA = 3:$meetcol = '<<$boydescA>> tells you that he prefers light hair colors.'
		elseif haerBoyA = pcs_haircol:
			bfA += 1
			'<<$boydescA>> notes that you have a wonderful hair color.'
		end
	end

	if pcs_lip = 0:$meetlip = 'thin lips'
	if pcs_lip = 1:$meetlip = 'soft lips'
	if pcs_lip = 2:$meetlip = 'plump lips'
	if pcs_lip = 3:$meetlip = 'large, plump lips'
	if pcs_lip = 4:$meetlip = 'thick and huge lips'

	'At the entrance <<$meetskin>> stands <<$boydescA>>. He comes over to you and says. <<$meet>> <<$meetcol>>'

	if financeA = 0:$clom = 'dressed in a tracksuit.'
	if financeA = 1:$clom = 'dressed in jeans and a sweater.'
	if financeA = 2:$clom = 'dressed in a respectable and expensive jacket.'
	if harakBoyA = 0:'<<$boydescA>> looks like a timid and not very confident young man <<$clom>>'
	if harakBoyA = 1:'<<$boydescA>> looks like a calm and confident young man <<$clom>>'
	if harakBoyA = 2:'<<$boydescA>> looks like a cocky and brash young man <<$clom>>'

	! WD: Uninitialised ~ 'giftDay' us  'giftDayA'
	if dayA >= 2 and financeA = 2 and giftDayA < (dayA - giftNum):
		giftrand = rand(0, 2)

		if giftrand <= financeA:
			giftDayA = dayA
			giftNum += 1
			gift = rand(bfa * 20, bfa * 30)

			if gift > 2500:gift = 2500

			giftNumA = giftNum
			money += gift
			'<<$boydescA>> reaches into his pocket and pulls out a gift for you. He gives you <<gift>> <b>₽</b>.'
		end

	! WD: Uninitialised ~ 'giftDay' us  'giftDayA'
	elseif dayA >= 5 and financeA = 1 and giftDayA < (dayA - giftNum):
		giftrand = rand(0, 2)

		if giftrand <= financeA:
			giftDayA = dayA
			giftNum += 1
			gift = rand(bfa * 10, bfa * 20)

			if gift > 1500:gift = 1500

			giftNumA = giftNum
			money += gift
			'<<$boydescA>> reaches into his pocket and pulls out a gift for you. He gives you <<gift>> <b>₽</b>.'
		end

	! WD: Uninitialised ~ 'giftDay' us  'giftDayA'
	elseif dayA >= 10 and financeA = 0 and giftDayA < (dayA - giftNum):
		giftrand = rand(0, 2)

		if giftrand <= financeA:
			giftDayA = dayA
			giftNum += 1
			gift = rand(bfa * 5, bfa * 10)

			if gift > 500:gift = 500

			giftNumA = giftNum
			money += gift
			'<<$boydescA>> reaches into his pocket and pulls out a gift for you. He gives you <<gift>> <b>₽</b>.'
		end
	end

	'<<$boybody>> <<$boybod>> <<$boyface>> walks up to you and wants to kiss you.'

	if venera < 4 and spdirtyv = 0:
		!!cumlip
		act 'Kiss with tongue':
			cla

			if spdirtyb = 0:
				'<center><img src="images/etogame/kiss.jpg"></center>'

				if harakBoyA = 0:bfa -= 2 & 'You press your <<$meetlip>> onto his and thrust your tongue into his mouth as soon as he opens it. <<$boydescA>> blushes and pulls away.'
				if harakBoyA = 1:bfa -= 1 & 'You press your <<$meetlip>> onto his and thrust your tongue into his mouth as soon as he opens it. <<$boydescA>> gently returns the kiss.'
				if harakBoyA = 2:bfa += 2 & 'You press your <<$meetlip>> onto his and thrust your tongue into his mouth as soon as he opens it. <<$boydescA>> aggressively pushes his tongue into your mouth and almost starts to fuck it with his tongue.'

				act 'Finish the kiss':xgt 'meet', 'a'

				exit
			elseif spdirtyb > 0:
				'<center><img src="images/etogame/cumlip.jpg"></center>'
				'<<$boydescA>> kisses you on your <<$meetlip>> that still smell like cum. He pulls away quickly. "What is this?" he asks, "Your mouth smells like sperm."'

				if izvrat = 1:
					bfa += 5
					pcs_dom += 1
					'<<$boydescA>> blushes and lowers his eyes. You can see the growing bulge in his pants.'

					act 'Finish the kiss':xgt 'meet', 'a'

					exit
				else
					bfa = 0
					'<<$boydescA>> angrily spats at you, "Oh, you dirty whore! Get lost, I don''t want to see you anymore. I''m done with you!"'

					act 'Leave':gt 'street'

					exit
				end
			end
		end

		act 'Kiss on the lips':
			cla

			if spdirtyb = 0:
				'<center><img src="images/etogame/kiss1.jpg"></center>'

				if harakBoyA = 0:'You move in for a kiss and <<$boydescA>> a little embarrassed kisses you back.'
				if harakBoyA = 1:bfa += 2 & 'You move in for a kiss on his lips. <<$boydescA>> quickly kisses you.'
				if harakBoyA = 2:bfa -= 1 & 'You move in for a kiss and <<$boydescA>> replies with a juicy kiss himself, pressing his lips to your firm <<$meetlip>>.'

				act 'Finish the kiss':xgt 'meet', 'a'

				exit
			elseif spdirtyb > 0:
				'<center><img src="images/etogame/cumlip.jpg"></center>'
				'<<$boydescA>> kisses you on your <<$meetlip>> that still smell like cum. He pulls away quickly. "What is this?" he asks, "Your mouth smells like sperm."'

				if izvrat = 1:
					bfa += 5
					pcs_dom += 1
					'<<$boydescA>> blushes and lowers his eyes. You can see the growing bulge in his pants.'

					act 'Finish the kiss':xgt 'meet', 'a'

					exit
				else
					boya = 0
					'<<$boydescA>> angrily spats at you, "Oh, you dirty whore! Get lost, I don''t want to see you anymore. I''m done with you!"'

					act 'Leave':gt 'street'

					exit
				end
			end
		end

		act 'Kiss on the cheek':
			cla
			pcs_dom += 1
			'<center><img src="images/etogame/kiss2.jpg"></center>'

			if harakBoyA = 0:bfa += 1 & 'You offer your cheek and <<$boydescA>> timidly kisses you.'
			if harakBoyA = 1:'You offer your cheek and <<$boydescA>> kisses you.'
			if harakBoyA = 2:bfa -= 2 & 'You offer your cheek and <<$boydescA>>, looking dissapointed, kisses you.'

			if giftDayA = dayA:
				bfa -= 3

				if harakBoyA = 0:'<<$boydescA>> looks upset.'
				if harakBoyA = 1:'<<$boydescA>> looks offended.'
				if harakBoyA = 2:'<<$boydescA>> looks angry.'
			end

			act 'Finish the kiss':xgt 'meet', 'a'

			exit
		end

		act 'Do not kiss':
			cla
			pcs_dom += 2

			if harakBoyA = 0:bfa -= 1 & 'You nod and say hello. <<$boydescA>> gets confused and blushes slightly.'
			if harakBoyA = 1:bfa -= 2 & 'You nod and say hello. <<$boydescA>> frowns slightly.'
			if harakBoyA = 2:bfa -= 3 & 'You nod and say hello. <<$boydescA>> chuckles and grins.'

			if giftDayA = dayA:
				bfa -= 3

				if harakBoyA = 0:'<<$boydescA>> looks upset.'
				if harakBoyA = 1:'<<$boydescA>> looks offended.'
				if harakBoyA = 2:'<<$boydescA>> looks angry.'
			end

			act 'Next':xgt 'meet', 'a'

			exit
		end
	elseif venera >= 4:
		bfa = 0
		'<center><img src="images/etogame/venera.jpg"></center>'
		'<<$boydescA>> takes a closer look at you and says, "What is wrong with your lips? They are very sore. Is that genital herpes? I better go, I do not want to get infected. Goodbye."'

		act 'Leave':gt 'street'

		exit
	elseif spdirtyv > 0:
		'<center><img src="images/etogame/cumface.jpg"></center>'
		'<<$boydescA>> takes a closer look at you and says, "What is that in your hair? Is that sperm?"'

		if izvrat = 1:
			bfa += 5
			pcs_dom += 1
			'<<$boydescA>> blushes and lowers his eyes. You can see the growing bulge in his pants.'

			act 'Yes it is the sperm. Any other questions?':
				cla
				gs 'stat'
				'(You) - Yes, it is sperm. Any other questions?'
				'<<$boydescA>> shakes his head.'

				xgt 'meet', 'a'
			end

			exit
		else
			boya = 0
			'<<$boydescA>> angrily spats at you, "Oh, you dirty whore! Get lost, I don''t want to see you anymore. I''m done with you!"'

			act 'Leave':gt 'street'

			exit
		end
	end
end

if $ARGS[0] = 'a':
	if spdirtyc > 0:
		'"Hey, what is this?" asks <<$boydescA>> looking at your ass. "It''s sperm!"'

		if izvrat = 1:
			bfa += 5
			pcs_dom += 1
			'<<$boydescA>> blushes and lowers his eyes. You can see the growing bulge in his pants.'
		else
			bfa = 0
			'<<$boydescA>> angrily spats at you, "Oh, you dirty whore! Get lost, I don''t want to see you anymore. I''m done with you!"'

			act 'Leave':gt 'street'

			exit
		end
	end

	if financeA = 0:
		if ($clothingworntype = 'expensive' or $clothingworntype = 'formal') and PCloSkirt > 0:
			bfa -= (harakBoyA + 1)
			'"And you dress smartly, " remarks a skeptical <<$boydescA>> looking at your expensive dress.'
		end

		if PCloSkirtShortness > 0 or $clothingworntype = 'exercise':
			bfa += (harakBoyA + 1)
			'"A classy outfit, " says <<$boydescA>>'
		end

		mesorand = rand(0, 1)

		if mesorand = 0:
			'<<$boydescA>> offers you a beer in the stairwell.'

			act 'Sorry, but I need to go':
				cla
				bfa -= (harakBoyA + 1)
				gt 'street'
			end

			act 'Go with him':gt 'podezdM', 'start'

			exit
		elseif mesorand = 1:
			'<<$boydescA>> suggests that you take a walk in the park.'

			act 'Sorry, but I need to go':
				cla
				bfa -= (harakBoyA + 1)
				gt 'street'
			end

			act 'Go with him':gt 'parkM', 'start'

			exit
		end
	elseif financeA = 1:
		if ($clothingworntype = 'expensive' or $clothingworntype = 'formal'):
			boyA += (harakBoyA + 1)
			'"Great clothes, " <<$boydescA>> says admiringly.'
		end

		if $clothingworntype = 'exercise':
			bfa -= (harakBoyA + 1)
			'"And you''re an athletic girl" <<$boydescA>> notices skeptically, looking at your clothes.'
		end

		mesorand = rand(0, 2)

		if mesorand = 0:
			'<<$boydescA>> suggests that you go to the movies.'

			act 'Sorry, but I need to go.':
				cla
				bfa -= (harakBoyA + 1)
				gt 'street'
			end

			act 'Go with him':gt 'kinoM', 'start'

			exit
		elseif mesorand = 1:
			'<<$boydescA>> suggest that you take a walk in the park'

			act 'Sorry, but I need to go.':
				cla
				bfa -= (harakBoyA + 1)
				gt 'street'
			end

			act 'Go with him':gt 'parkM', 'start'

			exit
		elseif mesorand = 2:
			'<<$boydescA>> suggests that you go to a cafe.'

			act 'Sorry, but I need to go.':
				cla
				bfa -= (harakBoyA + 1)
				gt 'street'
			end

			act 'Go with him':gt 'kafeM', 'start'

			exit
		end
	elseif financeA = 2:
		if ($clothingworntype = 'expensive' or $clothingworntype = 'formal') and PCloSkirt > 0:
			bfa += (harakBoyA + 1)
			'"You''ve got great taste in clothes." <<$boydescA>> says admiringly, looking at your expensive dress.'
		elseif $clothingworntype ! 'expensive' and $clothingworntype ! 'formal':
			bfa -= harakBoyA + 1
			'"You have an interesting taste in clothes." <<$boydescA>> remarks skepitcally, examining your unpretentious clothes.'
		end

		mesorand = rand(0, 1)

		if mesorand = 0:
			'<<$boydescA>> suggests that you go to a cafe.'

			act 'Sorry, but I need to leave':
				cla
				bfa -= (harakBoyA + 1)
				gt 'street'
			end

			act 'Go with him':gt 'kafeM', 'start'

			exit
		elseif mesorand = 1:
			'<<$boydescA>> suggests that you go to a restaurant.'

			act 'Sorry, but I need to leave':
				cla
				bfa -= (harakBoyA + 1)
				gt 'street'
			end

			act 'Go with him':gt 'restoranM', 'start'

			exit
		end
	end
end

--- meet ---------------------------------

