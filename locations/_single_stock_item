# $single_stock_item
!! generic function to be used to stock single purchase items in stores
!! see # shop, specifically the household appliances section for an example of use

!! ARGS 0 is the value identifying when the item is owned
!! it is assumed that anything >= the value is acceptable, this allows for upgrades like regular tv to plasma
!! ARGS 1 is the item description
!! ARGS 2 is the price
!! ARGS 3 is the variable name for the item
!! ARGS 4 is the location to go back to
!! ARGS 5 > 0 to  force-disable buy link

!! This will check to see if the item is already owned
dynamic "$RESULT = iif(<<$ARGS[3]>> >= <<ARGS[0]>>, '', 'not owned')"
if $RESULT = '':
	exit
end

$RESULT = '<tr>' + _
'<td align="center"><<FUNC(''$stock_item_info'', $ARGS[3])>></td>' + _
'<td align="center"><<$ARGS[1]>></td>' + _
'<td></td>' + _
'<td align="right">'
if money >= ARGS[2] and ARGS[5] < 1:
	$RESULT += '<a href="exec: gs ''$buy_single_item'', <<ARGS[0]>>, ''<<$ARGS[1]>>'', <<ARGS[2]>>, ''<<$ARGS[3]>>'', ''<<$ARGS[4]>>''">buy</a>'
end
$RESULT += '</td><td align="left">for <<ARGS[2]>> <b>â‚½</b></td></tr>'

--- $single_stock_item ---------------------------------

