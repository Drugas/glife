# MagEncounterFairy
$din_fairy_chat_no1 ={
	cls
	gs 'stat'
	knowsfairy = 1
	'You take a closer look at the floating glowing orb. What you mistook for a large firefly is actually a small woman with translucent wings on her back: A fairy!'
	'"Hello, biggie!", she says in a high pitched voice. "You can see me, right?'
	'"It''s so nice to meet you! Finally someone who can see me!", she goes on before you have a chance to answer. "Finally someone to chat with!!! You have no idea how annoying it is to be here and nobody recognizes you. It''s sooo BORING!!!"'
	act 'Chat with the fairy':
		cls
		minut += 30
		fairychat = 1
		manna += 5 * magik * 5
		gs 'stat'
		'You chat with the fairy. At first it feels strange to talk with a creature, you thought was a myth from children''s books. More so, since the small beautiful woman flutters in front of your face the whole time. But the fairy is so amicably that before you know it half an hour has passed.'
		if vnesh < 30 and skin < 4:
			skinvan += 10
			horny += 10
			gs 'stat'
			'The fairy bids you farewell. Just as she flies off, she returns fluttering in front of your face.'
			''
			wait 725
			'"You know, someone as nice as you should look just as lovely.", she says. "Hold still and don''t breath!"'
			'Before you can do anything, the fairy blows some sparkling dust into your face. Surprised you hold your breath, albeit barely. You feel the powdery substance soak into your skin and your face starts tingling. It is not uncomfortable, rather like the brush of a feather.'
			' ''That must have been MAGICAL FAIRY DUST!'', it hits you as the fairy flies off.'
		else
			'The fairy waves you goodbye and flies away.'
		end
		act 'Wave goodbye':gt 'gskver'
	end
}

$din_fairy_shoo ={
	cls
	minut += 1
	if knowsfairy < 1:
		knowsfairy = 1
		fairyshoo = 1
		horny = 90
		gs 'stat'
		'Not paying much attention you wave your hands to chase away the (very large!?) firefly. She easily dodges your halfhearted moves.'
		'"You are a big meanie!", suddenly squeals a high pitched voice.'
		'Stunned by surprise you take a closer look at what you thought was a firefly. In the glow you can see a small woman with fluttering wings on her back. It''s a fairy! Taking advantage of your bewilderment she blows something sparkling in your face. Flabbergasted as you are you inhale the substance.'
		'Instantly you become completely horny and your face flushes with arousal.'
		'"Maybe this will teach you." she says with a pout and flies off.'
		act 'Move along':gt 'gskver'
	else
		fairyshoo += 1
		horny = 90
		fairycurse = daystart + (fairyshoo - 1) * 2
		gs 'stat'
		'"Still a meanie, I see!", squeals the fairy "Well, have it your way."'
		act 'Get away from the fairy': gt 'gskver'
	end
}

$din_fairy_chat ={
	cls
	gs 'stat'
	'You take the few steps over to the glowing orb that apparently only you can recognize as what it truely is: a fairy.'
	act 'Chat with the fairy':
		cls
		minut += 30
		if fairyshoo = 1 and rand (1, 3) = 2:
			fairyshoo = 0
		elseif fairyshoo > 1 and (fairyshoo * 3 - fairychat) <= 0:
			fairyshoo -= 1
		end
		fairychat += 1
		manna += 5 * magik * 5
		'You sit down to chat with the fairy. She being so amicably before you know it half an hour has passed.'
		if vnesh < 30 and skin < 4 and fairyday < daystart and fairyshoo = 0:
			skinvan += 10
			fairyday = daystart
			gs 'stat'
			'The fairy bids you farewell.'
			''
			wait 725
			'Just as she flies off, she returns fluttering in front of your face.'
			'"You know, you could still look lovlier.", she says. "Don''t move."'
			'Remembering the last time you hold your breath and close your eyes. Just a moment later you feel your skin start tingling, again, as if brushed by a feather.'
		else
			'You two say your goodbyes and the fairy flies off.'
		end
		act 'Go on your way': gt 'gskver'
	end
}

if knowsfairy = 1:
	if fairyshoo = 0:
		act 'Look for the fairy to chat': dynamic $din_fairy_chat
		act 'Just be on your way': gt 'gskver'
	else
		if manna >= ((intel * magik + magik * 100 + vital * 10 + rikudo) * 50 / 100): act 'Be nice and chat with the fairy': dynamic $din_fairy_chat
		if manna >= ((intel * magik + magik * 100 + vital * 10 + rikudo) * 25 / 100): act 'Just ignore the fairy': gt 'gskver'
		act 'Shoo the pesky fairy away': dynamic $din_fairy_shoo
	end
elseif magik + rand (1,10) >= 8:
	act 'Investigate the strange glow': dynamic $din_fairy_chat_no1
	act 'Shoo the pesky firefly away': dynamic $din_fairy_shoo
	act 'Ignore the firefly': gt 'gskver'
else
	gs 'stat'
	'You have the strange feeling of being watched, but looking around you don''t see anybody.'
	act 'Shake off the feeling and move on': gt 'gskver'
end
--- MagEncounterFairy ---------------------------------

