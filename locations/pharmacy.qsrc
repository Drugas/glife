# pharmacy

if $ARGS[0] = 'start':
	*clr & cla
	menu_off = 1
	minut += 5
	gs 'stat' & ! Update the information about the past tense
	!a time limit of work pharmacies in Pavlovo
	people = rand(1,15) & !We define the size of the queue in the pharmacy
	if people <= 5:
		*pl '<center><b><font color="maroon">Pharmacy</font></b></center>'
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/pharmacy/aptekabusy<<people>>.jpg"></center>'
		'When you go up to check out their is <<people>> ' + iif(people < 2 or people > 4, 'people' , 'man' ) + ' in line before you.'
		act 'Leave': minut += 5 & gt $loc, $metka
		act 'Wait for your turn' :
			minut += rand(1,4) * people
			people = 0
			gs 'stat'
			gt 'pharmacy', 'shop'
		end
	else
		gt 'pharmacy', 'shop'
	end
end

if $ARGS[0] = 'shop' or $ARGS[0] = '':
	*clr & cla
	$location_type = 'public_indoors'
	$menu_loc = 'pharmacy'
	$menu_arg = 'shop'
	menu_off = 0
	gs 'stat'
	*pl '<center><b><font color="maroon">Pharmacy</font></b></center>'
	!*pl '<center><img <<$set_imgh>> src="images/unused/pharmacy.jpg"></center>'
	!there are 3 pic from russian 1.2.5, Let make it 3 shift
	if hour>22 or hour < 7:
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/pharmacy/apteka_worker_2.jpg"></center>'
	elseif hour<17:
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/pharmacy/apteka_worker_3.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/pavlovsk/pharmacy/apteka_worker_4.jpg"></center>'
	end
	if $message ! '':
		$message
		killvar '$message'
		'<center>"Do you need anything else?"</center>'
	else
		'<center>Their is a young woman behind the counter in a white coat,as you approach she asks. "Hello, what can I help you with?"</center>'
	end

	!! output for purchased item
	iif($ARGS[1] = '', '', '<center><<$ARGS[1]>></center>')

	*pl '<center><table align="center">'
	if preziktype = 0: *P FUNC ('$stock_item', 5, 'condoms', 500, 'prezik', $CURLOC, $ARGS[0])
	if preziktype = 1 or preziktype = 2: *P FUNC ('$stock_item', 5, 'condoms', 500, 'prezikcount', $CURLOC, $ARGS[0])
	*P FUNC ('$stock_item', 1, 'pregnancy test', 530, 'pregtest', $CURLOC, $ARGS[0])
	*P FUNC ('$stock_item', 1, 'birth control pills', 1870, 'tabletki', $CURLOC, $ARGS[0])
	*P FUNC ('$stock_item', 10, 'antibiotics', 580, 'lekarstvo', $CURLOC, $ARGS[0])
	*P FUNC ('$stock_item', 30, 'vitamins', 130, 'vitamin', $CURLOC, $ARGS[0])
	*P FUNC ('$stock_item', 50, 'painkillers', 435, 'painkiller', $CURLOC, $ARGS[0])
	*P FUNC ('$stock_item', 10, 'ointments for chafing', 372, 'mosolmaz', $CURLOC, $ARGS[0])
	*P FUNC ('$stock_item', 1, 'lubricant', 145, 'lubri', $CURLOC, $ARGS[0])
	!!tube of 5 uses as far as I can tell, price is set to that
	*P FUNC ('$stock_item', 30, 'lip balm', 127, 'lipbalm', $CURLOC, $ARGS[0])
	*P FUNC ('$stock_item', 12, 'weight loss pill', 248, 'fatdel', $CURLOC, $ARGS[0])
	*P FUNC ('$stock_item', 20, 'Tampons', 200, 'tampon', $CURLOC, $ARGS[0])
	*P FUNC ('$stock_item', 20, 'Sanitary napkins', 100, 'sanpad', $CURLOC, $ARGS[0])
	*P FUNC ('$single_stock_item', 1, 'enema kit', 1250, 'klisma', $CURLOC, $ARGS[0])
	*P FUNC ('$single_stock_item', 1, 'breast pump', 2200, 'breastpump', $CURLOC, $ARGS[0])
	if pcs_knowsmilkbottles > 0:
		*P FUNC ('$stock_item', 3, 'Breastmilk Bottle S', 930, 'bpbottles', $CURLOC, $ARGS[0])
		*P FUNC ('$stock_item', 2, 'Breastmilk Bottle M', 930, 'bpbottlem', $CURLOC, $ARGS[0])
	end
	if pcs_knows_nipplecream > 0:
		*P FUNC ('$stock_item', 20, 'Nipplebalm', 600, 'bpnipplebalm', $CURLOC, $ARGS[0])
	end
!!added this from the pav pharmacy as it makes sense they sell this as well, but not sure the code is right, as it was not done the same way.
	*P '</table></center>'
	*nl
	if KandidNapr = 1:'Cure for yeast infection - 1,050 <b>₽</b>'
	*nl
	act 'Leave': minut += 5 & gt $loc, $metka
	if KandidNapr = 1:
		act 'Buy tablets for your yeast infection (1,050 <b>₽</b>)':
			*clr & cla
			money -= 1050
			Kandidoz = 0
			'<center><img <<$set_imgh>> src="images/locations/shared/store/kassa.jpg"></center>'
			'You put the money on the counter and immediately open the package and swallow the tablet. A while later, you feel better.'

			act 'Move away from the counter':gt 'pharmacy', 'start'
		end
	end	
end

--- pharmacy ---------------------------------

