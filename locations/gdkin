# gdkin

if $ARGS[0] = '':
	gdkin_in = daystart
	$metka = ''
	$loc = 'gdkin'
	$location_type = 'public_indoors'
	boynumBlock = 0
	gs 'stat'
	fcolor = rgb(0, 0, 0)
	bcolor = rgb(255, 255, 255)
	lcolor = rgb(106, 90, 205)

	pl ''

	if music_off = 0:
		inited = 1
		music_on = 1
		volume = 100
		$music_theme = 'gdis'
		gs 'init_music'
		pl '<a href="exec: music_off = 1 & gt $loc">Turn off the music</a>'
	else
		music_on = 0
		volume = 100
		gs 'init_music'
		pl '<a href="exec: music_off = 0 & gt $loc">Play music</a>'
	end

	'<center><b><font color="maroon">Disco</font></b></center>'
	'<center><img src="images/etogame/club_yaho.jpg"></center>'

	if hour >= 20 and hour <= 23:
		if StoryLine = 1 and SchoolAtestat = 0:'The club and you see a few <a href="exec:gt ''SgrupDisco''">of people you know</a>.'
		if soniaslut < 5:soniaslut += 1

		if grupTipe[25] = 3 and StoryLine = 1 and BeInSchool > 0 and soniaslut >= 5 and hour < 23:
			'You notice Sonia from your class. She is talking to Victor about something.'

			act 'View Sonya':gt 'soniaDisco'

			exit
		end

		act 'Dance one':
			cls
			'<center><img src="images/etogame/club_dance.jpg"></center>'
			minut += 5
			!!fat -= 1

			if dance < 50 and agil >= dance:dance += 1

			manna += 10
			willpower += 10 & ! WD: Should be willpower as is mood
			frost = 0
			gs 'stat'

			if spdirtyc > 0 or spdirtyv > 0:
				if GorSlut < 2:GorSlut = 2

				manna -= 100
				'It suits you and the guy starts to stick, suddenly he sees you cum. The guy jumps on you "thanked, vaflersha. Lads estimate, this chick all in Concha peremazanna." To you guys still see fit. "Yeah sure. Hey nipple unsuccessfully vysmarkalas what?" You cheeks blush with shame and you''re heading for the exit.'

				act 'Leave':gt 'gdk'

				exit
			end
			if rand(0,7)=1 and katja_disco_end=1 and (alko > 1 or horny => 80) and KatjaHorny > 60:
				*CLR
				'<center><img src="images/qwest/alter/katja/katja_dance.jpg"></center>'
				'While you are dancing you notice Katya out on the dance floor as well.'
				Act 'Dance with Katya':gt 'gdkin', 'Katja_dance'
			end

			if alko >= 4:
				'You''re to drunk to try and dance, you are barely able to stay on your feet and mostly just drunkenly bump into other people as you try and dance.'
				if rand (0,1) = 0:gt 'gdkin', 'too_drunk_dance'
			elseif alko > 0:
				if tanga = 0 and PCloSkirt > 2:
					*clr
					'<center><img src="images/etogame/club_dance2.gif"></center>'
					'You''re drunk and you forget you don''t have any panties on, as you dance around moving your body, your skirt rides up. Exposing your pussy to the observant people on the dance floor with you.'
				else
					*clr
					'<center><img src="images/etogame/club_dance7.jpg"></center>'
					'You''re drunk, but you''re having a lot of fun out on the dance floor anyways, you''re not even paying attention to others or even the music.'
				end
			else
				'The music is quite old, but catchy. You have fun dancing, not paying attention to anyone.'
			end

			if alko < 4:
				verprigrand = rand(0, 100)

				if verprigrand <= dance:
					gs 'gdkin', 'dance_partner'
					gs 'stat'
					'<<$din_disc_dance2>>'

					act 'Dance with a guy':gt 'gdkin', 'dance_vrp'

					act 'Move away':
						cls
						gt 'gdkin'
					end

					exit
				end
			end

			gs 'stat'

			act 'Further':gt 'gdkin'
		end

		act 'Stand at the wall':
			cls
			minut += 5
			!!fat -= 1
			manna -= 5
			frost = 0
			gs 'stat'
			'<center><img src="images/etogame/club_yaho.jpg"></center>'

			if spdirtyc > 0 or spdirtyv > 0:
				if GorSlut < 2:GorSlut = 2

				manna -= 100
				'While standing by the wall a guys notices cum on your clothes and he points and shouts. "This chick is covered in cum, wait did you fail to swallow slut?" He says to you laughing. Your cheeks blush with shame and you head for the exit.'

				act 'Leave':gt 'gdk'

				exit
			end
		
			if alko >= 6 and rand (1, 100) < 3:
				gt 'gevent', '6'
			end

			if alko >= 4:
				'You''re drunk to dance and you lean against the wall for support to keep from falling down.'
				if rand (0,1) = 0:gt 'gdkin', 'too_drunk_dance'

			elseif alko > 0:
				'You''re drunk, and standing against the wall watching the others dance.'
			else
				'You are standing watching dancing.'
			end

			if alko < 4:
				verodance = 0

				if hapri = 1:verodance += 1
				if PCloSkirt > 2 or PCloPants > 4:verodance += 1
				if $clothingworntype = 'expensive':verodance += 2
				if mop = 2:verodance += 1
				if mop = 3:verodance += 2
				if mop = 4:verodance += 3

				verodance += alko
				verodance += 5
				verprigrand = rand(0, 20)

				if verprigrand <= verodance:
					gs 'gdkin', 'dance_partner'
					gs 'stat'
					'<<$din_disc_dance>>'

					act 'Dance':gt 'gdkin', 'dance_vrp'

					act 'Refuse':
						cls
						gt 'gdkin'
					end

					exit
				end
			end

			gs 'stat'

			act 'Further':gt 'gdkin'
		end
	elseif hour < 20:
		'Club closes. The main lights go on, the music fades and everyone beings to leave.'
	end

	act 'Go outside':gt 'gdk'
	
	act 'Go to the toilets': gt 'gdktoilet', 'start'

end

if $ARGS[0] = 'Katja_dance':	
	minut += 15
	gs'stat'
	*clr & cla 
	dancerand=rand(1,5)
	if tanga=0: 
		'<center><img src="images/qwest/alter/katja/katja_dance1_2.jpg"></center>'  
		'You dance with Katja attracting views of men from across the room. And when you consider that you not wearing panties, and your occasional spiraling of your skirt shows that fact off, it helps explain the attention.'
	else
		'<center><img src="images/qwest/alter/katja/katja_dance1_1.jpg"></center>'
		'You dance with Katja attracting views of men from across the room.'
	end
	act 'Expose Katja':
		horny += 20 
		KatjaHorny += 20
		*clr & cla 
		if dancerand = 1: 
			'<center><img src="images/qwest/alter/katja/katja_dance2_2.jpg"></center>'
			'During the dance you have decided to fool around and embraced Katja, pulling aside her top a bit to show off a nipple. Which cause a approving roar from all around and a embarrassed squeak from Katja.'
			act 'Dance on':
				*clr & cla
				'<center><img src="images/qwest/alter/katja/katja_dance2_1.jpg"></center>'
				'One of the guys moved in close dancing with the two of you, unable to stand watching the two of you dancing and touching each other like that, he slides his hand under her skirt.'
				act 'Stick out tongue':
					*clr & cla
					'<center><img src="images/qwest/alter/katja/katja_dance1_3.jpg"></center>'
					'Rebuffing guy you and Katja, pushed him away and turned to the crowd of guys around you. Katja hugs you and sticks out her tongue at the guys. The rebuffed guys looks around and thinks better of it and gets lost among dancers.'
					act 'to get lost':  gt 'gdkin'
				end
			end	
		elseif dancerand = 2:	
			'<center><img src="images/qwest/alter/katja/katja_dance3.jpg"></center>'
			'Dancing with Katja you can''t control yourself, you pull Katja close to you, putting your hands on her boobs and burying your face in her cleavage. Katja moans slightly at your actions, while the boys gathered around shout encouragements to the two of you.'
		elseif dancerand = 3:
			'<center><img src="images/qwest/alter/katja/katja_dance4.jpg"></center>'
			'Dancing with Katya and moving quite actively, you do not notice how your breasts fell out from clothes, exposing your nipples. But the guys sure noticed that where around you.'
		elseif dancerand = 4:
			'<center><img src="images/qwest/alter/katja/katja_dance5.jpg"></center>'
			'During the dance you turn to face Katja, the two of your dance close together, faces near each other. You lean in and give her a passionate kiss, you can hear the cheering of the guys. You barely notice the guys, more of a faint back ground noise, as you lose yourself in the moment.'
		elseif dancerand = 5:
			'<center><img src="images/qwest/alter/katja/katja_dance6.gif"></center>'
			'You dance with Katya paying little attention to what is happening around you, when one of the guys around you reaches out and pulls down your top. Baring your breasts for everyone to see. Without ceasing to dance you pull your top back up to the disgruntled cries of the guys around you.'
		end
		act 'Stick out tongue':
			*clr & cla
			'<center><img src="images/qwest/alter/katja/katja_dance1_3.jpg"></center>'
			'Looking at the crowd of guys around you, you hug Katja and stick out your tongue at them, letting them know they can look at they like but that''s all their going to get.'
			act 'to get lost':  gt 'gdkin'
		end
	end
end

if $ARGS[0] = 'kotov_love':
	cls
	gs 'stat'
	'<center><b><font color="maroon"><<$npcName[9]>> <<$npcSurName[9]>></font></b></center>'
	'<center><img src="images/qwest/alter/npc/9.jpg"></center>'
	'Vitek walks up to you and the guy. "What is this garbage? Who''s this guy?" Vitek shoves the guy and angrily says "Well who the fuck are you? This is my girl, you want me to bust you up?"'

	if DPgrup = 1:
		fightboyrand = rand(0, 10)

		if fightboyrand <= 5:
			'The frightened man begins to speak. "Come on, I didn''t know, I''m leaving." Vitek pushes the guy. "Get out of here and don''t come back."'
		elseif fightboyrand > 5 and fightboyrand < 10:
			'The guy shoves Victor back. "Get your hands off me asshole!" Vitek suddenly kicks the guy with a fast and powerful blow.'
            'The guy falls to the floor in pain, Vitek steps over and looms over the guy, grabbing him by this shirt he yanks him off the floor and slams him into the wall.'
            '"Well, motherfucker? You think you can take whats mine? How about I take whats yours." The guy curled up in pain and frighteningly replies. "You can have anything, anything I got!"'
		elseif fightboyrand >= 10:
			'The guy punches Vitek in the face, Vitek recoils from the hit and lands a powerful kick of his own. Then they start fighting, fists and kicks landing from both sides. Finally a very powerful hit by Vitek, sends the guy flying into the wall, where he slumps to the floor. Vitek runs to him and starts punching him in the face. The guy stops resisting, apparently he lost consciousness.'
		end
	elseif DPgrup = 2:
		'The guy with a look calls over his friends, who all come over to crowd around Vic. "Real tough guy huh?" Vitek''s friends come over to back him up, after a bit of tough talk and a bit of shoving the two groups break apart peacefully, going their own ways.'
	elseif DPgrup = 3:
		'The guy throws up his hands "Sorry young man, I didn''t know she was with you." Vitek pushes the guy. "Go back to the city and stay there."'
	end

	act 'Go to Vitek':numnpc = 9 & gt 'Snpc'

	exit
end

if $ARGS[0] = 'reakt_dance':
	if alko > 0:
		horny += 10
		manna += 5
		'You feel yourself getting aroused by the guys actions.'
	else
		if horny > 0:horny -= 5

		manna -= 5
		'You feel disgusted by the guys actions.'
	end

	gs 'stat'
end

if $ARGS[0] = 'reverse_reakt_dance':
	if alko <= 0:
		manna += 20
		horny += 1
		'You enjoy dancing with him a great deal.'
	elseif alko > 0:
		if horny > 0:horny -= 5

		manna -= 5
		'You find the guy boring and uninteresting.'
	end

	gs 'stat'
end

if $ARGS[0] = 'dance_vrp':
	cls
	minut += 5
	if dance < 50 and agil >=dance:dance += 1
	gs 'stat'
	'<center><img src="images/etogame/club_dance5.jpg"></center>'
	'You accepted the offer, the guy took you by the hand and brought you out onto the dance floor, where you started dancing together.'

	if DPgrup = 1:
		if DPtipe = 1:
			'The guy is dancing with you holding your waist with his hands.'
			gs 'gdkin', 'reverse_reakt_dance'
		elseif DPtipe = 2:
			*clr &'<center><img src="images/etogame/club_dance6.jpg"></center>'
			'The drunk guy pushes into you, grinding his body up against yours. You feel your soft breasts literally flatten against the mans chest.'
			gs 'gdkin', 'reakt_dance'
		end
	elseif DPgrup = 2:
		if DPtipe = 1:
			'The guy slides his hands down your side to your hips.'
			gs 'gdkin', 'reakt_dance'
		elseif DPtipe = 2:
			*clr &'<center><img src="images/etogame/club_dance8.jpg"></center>'
			'A guy grabs your ass and squeezes it, he pulls you into a hug and kisses your passionately.'
			gs 'gdkin', 'reakt_dance'
		end
	elseif DPgrup = 3:
		if DPtipe = 1:
			'A very confident and elegant man leads you in a dance, holding you by your waist with his strong hands.'
			gs 'gdkin', 'reverse_reakt_dance'
		elseif DPtipe = 2:
			'A tipsy man hugs your around your waist and his hands go down to grab your ass.'
			gs 'gdkin', 'reakt_dance'
		end
	end

		if dance >= 50 and alko > 0 and horny >= 50:
		act 'Sexy dance':
			cls
			'You are taking advantage of your ability to dance, sexy moves cause your partner to stare at you. But he seems stunned by how great you look or maybe by how well you move, either way he suddenly moves away and disappears into the crowd.'
			'<center><img src="images/etogame/club_dance3.jpg"></center>'
			if tanga=0 and PCloSkirt > 2: *clr & '<center><img src="images/etogame/club_dance4.jpg"></center>' & 
			'Trying to move your body as erotically as possible, you lean forward while doing a complex dance move, completely forgetting that you''re not wearing panties. Now your dance partner knows as well and it seems he will likely never forget.'
			dance += 1
			gs 'stat'
			act 'Further':gt'gdkin'

		end
	end

	act 'Further':
		cls
		gs 'stat'
		'<center><img src="images/etogame/club_dance.jpg"></center>'
		if tanga = 0 and alko > 0 and PCloSkirt > 2: *clr &'<center><img src="images/etogame/club_dance9.jpg"></center>'
		if alko > 0: *clr &'<center><img src="images/etogame/club_dance10.jpg"></center>'

		if DPgrup = 1:
			if DPtipe = 1:
				'The guy told you during the dance "My name is <<$bname>>. What''s your name?" You answered "<<$name>>." Once the song ends, he guy asks you to sit with him and chat.'
			elseif DPtipe = 2:
				'The guy told you during the dance "My name is <<$bname>>. What''s your name?" You answered "<<$name>>." During the whole dance the drunk guy humped your body and now his face flushed with excitement. He ask you to leave the club and go get some fresh air, socialize, outside.'
			end

			act 'Go with a guy':gt 'EvBoyBuh'
		elseif DPgrup = 2:
			if DPtipe = 1:
				'For the entire dance the guy was grabbing your ass, breasts and kissing you, he was clearly excited. When you would press up against him, you could feel something hard in his pants. Finally the song ended and the guy offers to take you outside for a drink.'

				act 'Go drink':gt 'EvgopBuh'
			elseif DPtipe = 2:
				'For the entire dance the guy was grabbing your ass, breasts and kissing you, he was clearly excited. When you would press up against him, you could feel something hard in his pants. Finally the song ended and the guy offers to take you outside for a drink.'

				act 'Go drink':gt 'EvgopBuh'
			end
		elseif DPgrup = 3:
			if DPtipe = 1:
				'The man danced with confidence and elegance, finally the song was over and the man offers to go out for some fresh air and talk.'

				act 'Go with a man':
					cls
					gs 'stat'
					'<center><img src="images/etogame/dk_night.jpg"></center>'
					'You went outside with the man. As you walked he put his arm around your waist. "My name is <<$bname>>. What''s your name?" You answered "<<$name>>." The man continued, "<<$name>>, I''m in town on a business trip. I am staying staying at a nearby hotel, I live in the city itself. There is virtually nothing to do in this town you know. Come back to my hotel with me and we can drink whiskey, while we site and have a nice chat."'

					act 'Go to the hotel':gt 'EvOtelMan'

					if alko <= 2 and horny <= 75:
						act 'Refuse':
							if will <= 30: will += rand(0, 1)
							gt 'gdkin'
						end
					end
				end
			elseif DPtipe = 2:
				'For the entire dance the drunk man molested you, when the dance ended. He whispered in your ear. "My name is <<$bname>>. What''s your name?" You answered "<<$name>>." The man continued, "<<$name>>, I''m not a local, I''m staying in a nearby hotel, let''s take a taxi and go to my room."'

				act 'Go to the hotel':gt 'EvOtelMan'
			end
		end

		gs 'stat'
		if alko <= 2 and horny <= 75:
			act 'Refuse':
				if will <= 30: will += rand(0, 1)
				gt 'gdkin'
			end
		end
		if kotovLoveQW > 0: gt 'gdkin', 'kotov_love'
	end

	act 'Stop dancing':
		cls
		gt 'gdkin'
	end
end

if $ARGS[0] = 'dance_partner':
	!!the creation of a partner to dance
	sozboyrand = rand(1, 300)
	if sozboyrand > boynum:
		gs 'BnameGen'
		DaPaTipeRand = rand(0, 100)

		if DaPaTipeRand < 70:
			!!ordinary local boy
			bmtimeTipe = 0
			DrunkDPTrand = rand(0, 100)

			if DrunkDPTrand < 70:
				DPtipe = 1
				DPgrup = 1
				$din_disc_dance = 'A guy dressed in jeans and a t-shirt, asks you to dance.'
				$din_disc_dance2 = 'Dancing next to you is a guy dressed in jeans and a T-shirt.'
			elseif DrunkDPTrand >= 70:
				DPtipe = 2
				DPgrup = 1
				$din_disc_dance = 'A drunk guy dressed  in jeans and a t-shirt, asks you to dance.'
				$din_disc_dance2 = 'Dancing next to you is a drunk guy dressed in jeans and a T-shirt.'
			end
		elseif DaPaTipeRand >= 70 and DaPaTipeRand < 90:
			!!local gopar
			bmtimeTipe = 1
			DrunkDPTrand = rand(0, 100)

			if DrunkDPTrand < 50:
				DPtipe = 1
				DPgrup = 2
				$din_disc_dance = 'A guy dressed in sweatpants and a sweatshirt asks you to dance.'
				$din_disc_dance2 = 'Dancing next to you is a guy dressed in sweatpants and sweatshirts.'
			elseif DrunkDPTrand >= 50:
				DPtipe = 2
				DPgrup = 2
				$din_disc_dance = 'A drunk guy dressed in sweatpants and a sweatshirt, puts his arms around your waist and asks you to dance.'
				$din_disc_dance2 = 'Dancing next to you is a drunk guy dressed in sweatpants and sweatshirts.'
			end
		elseif DaPaTipeRand >= 90:
			!!traveling_expenses
			bmtimeTipe = 2
			DrunkDPTrand = rand(0, 100)

			if DrunkDPTrand < 90:
				DPtipe = 1
				DPgrup = 3
				$din_disc_dance = 'A man in a nice suit and expensive shoes, asks you to dance.'
				$din_disc_dance2 = 'Dancing next to you is a man in a nice suit and expensive shoes.'
			elseif DrunkDPTrand >= 90:
				DPtipe = 2
				DPgrup = 3
				$din_disc_dance = 'A drunk man in a nice suit and expensive shoes, puts his arms around your waist and asks you to dance.'
				$din_disc_dance2 = 'Dancing next to you is a drunk man in a nice suit and expensive shoes.'
			end
		end
		gs 'boymass'
	elseif sozboyrand <= boynum:
		gt 'gdkin', 'familiar'
	end
end

if $ARGS[0] = 'familiar':	
	cls
	gs 'stat'

	Tboynum = sozboyrand

	if bmTip[Tboynum] = 0:
		$niknameboy = 'Guy'
		rudeboy = 1
		'<center><img src="images/etogame/club_dance_gay.jpg"></center>'
		'A man dressed in jeans and T-shirt, invites you to dance. You don''t remember ever seeing him but he seems familiar.'
	elseif bmTip[Tboynum] = 1:
		$niknameboy = 'Gopnik'
		rudeboy = 2
		'<center><img src="images/etogame/club_dance_gop.jpg"></center>'
		'A man dressed in sweatpants and sweatshirts, invites you to dance. You don''t remember ever seeing him but he seems familiar.'
	elseif bmTip[Tboynum] = 2:
		$niknameboy = 'Man'
		rudeboy = 0
		'<center><img src="images/etogame/club_dance_man.jpg"></center>'
		'A man dressed in a nice suit and expensive shoes, invites you to dance. You don''t remember ever seeing him but he seems familiar.'
	end

	gs 'BMloc'

end

if $ARGS[0] = 'too_drunk_dance':
	if StoryLine = 1 and SchoolAtestat = 0:
		cls
		gs 'stat'
		'<center><img src="images/etogame/club_dance_alco1.jpg"></center>'
		'Katja Meynold approached you. "<<$name>>, are you to drunk? Let''s go home, I''ll take you."'

		act 'Go with Katja':
			cls
			minut += 15
			grupNPC[14] = 100

			if meynoldQW = 0:meynoldQW = 1

			gs 'stat'
			'<center><img src="images/etogame/club_dance.jpg"></center>'
			'Katja took you by the arm and helped you get home.'

			act 'Go home':gt 'korrPar'
		end

		exit
	else
		gt 'gdkin'
	end
end

--- gdkin ---------------------------------

