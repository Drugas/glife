# vokzalGin

if $ARGS[0] = '':
	$metka = ''
	$loc = 'vokzalGin'
	$location_type = 'public_indoors'
	CLOSE ALL
	if sound = 0:PLAY 'sound/vokzal2.mp3',30
	clr
	frost = 0
	poezddalsled = 0
	gs 'vokdin'
	gs 'vokdinvera'
	gs 'stat'
	fcolor = rgb(0, 0, 0)
	bcolor = rgb(255, 255, 255)
	lcolor = rgb(106, 90, 205)


	'<center><h4>Pavlovsk train station interior</h4></center>'
	'<center><img src="images/qwest/alter/vokzal.jpg"></center>'
	'The center of the station building is a large waiting room, where passengers can wait until their train arrives. Off to one side, the <a href="exec:gt ''vokzalGin'', ''kassi''">ticket office</a> is situated next to the departures board. Opposite to that you can find the <a href="exec:dynamic $din_vokzalg_kabinet">station manager''s office</a>, and the <a href="exec:minut += 1 & gt ''vokmantoiev'', ''start''">toilets</a> on the other end of the building.'
	'You can go to the train <a href="exec:GT ''vokzalperG'',''start''">platforms</a> through the station building.'

	if katalkinSexOnce = 1:'There is a small <a href="exec:minut += 5 & gt ''katalkin''">police station</a> inside the station building.'

	if StoryLine = 1 and SchoolAtestat = 0:
		if (sunWeather = 0 or temper <= 0) and hour >= 18 and hour < 20 and week < 6:
			'You see your classmates <a href="exec:minut += 3 & gt ''gopskver''">Vitek, Dan and Vasily</a> hanging around here.'
		end
	end

	if podbros = 0:
		podrand = rand(0, 1)

		if money >= 1000 and hour >= 8 and hour <= 20 and podrand = 0:'As you walk through the station building, you notice a man''s thick <a href="exec:dynamic $din_vokzalg_podbros">wallet</a> dropping from his pocket, not far away from you.'
	end

	act 'Leave the station building':minut += 3 & gt 'vokzalG'

	if vokzalVork >= 1:
		if cleZalDay ! daystart:
			act 'Mop the waiting room floor (1:00)':
				cls
				cleZalDay = daystart
				minut += 60
				pcs_sweat += 10

				if pcs_vital < 15:pcs_vital += rand(0, 1) + (steroid_dose - rand(0,steroid_dose)) & gs 'obj_din', 'steroids'
				if pcs_stren < 15:pcs_stren += rand(0, 1) + (steroid_dose - rand(0,steroid_dose)) & gs 'obj_din', 'steroids'

				gs 'stat'
				'<center><img src="images/etogame/clener1.jpg"></center>'
				'You take some cleaning supplies from the maintenance closet, pour some water and soap into a bucket and wash the floor in the waiting room.'

				act 'Continue':minut += 1 & gt 'vokzalGin'
			end
		end

		if cleMTDay ! daystart:
			act 'Clean the men''s restroom (0:30)':
				cls
				cleMTDay = daystart
				minut += 30
				pcs_sweat += 10

				if pcs_vital < 15:pcs_vital += rand(0, 1) + (steroid_dose - rand(0,steroid_dose)) & gs 'obj_din', 'steroids'
				if pcs_stren < 15:pcs_stren += rand(0, 1) + (steroid_dose - rand(0,steroid_dose)) & gs 'obj_din', 'steroids'

				gs 'stat'
				'<center><img src="images/etogame/clener1.jpg"></center>'
				'You take some cleaning supplies from the maintenance closet, pour some water and soap into a bucket and go to the men''s room.'
				gs 'vokmantoiev', 'toilet_event'
				!!'You began to wash the floor in the men''s room, and then began to scrub toilets.'

				act 'Leave':minut += 1 & gt 'vokzalGin'
			end
		end

		if cleWTDay ! daystart:
			act 'Clean the women''s restroom (0:30)':
				cls
				cleWTDay = daystart
				minut += 30
				pcs_sweat += 10

				if pcs_vital < 15:pcs_vital += rand(0, 1) + (steroid_dose - rand(0,steroid_dose)) & gs 'obj_din', 'steroids'
				if pcs_stren < 15:pcs_stren += rand(0, 1) + (steroid_dose - rand(0,steroid_dose)) & gs 'obj_din', 'steroids'

				gs 'stat'
				'<center><img src="images/etogame/clener1.jpg"></center>'
				'You take some cleaning supplies from the maintenance closet, pour some water and soap into a bucket and scrub the toilets in the ladies'' room before mopping the floor.'
				dynamic $d_vokzal_vera_talk

				act 'Leave':minut += 1 & gt 'vokzalGin'
			end
		end

		if cleMTDay = daystart and cleWTDay = daystart and cleZalDay = daystart and cleZarplatDay ! daystart:
			act 'Collect your salary':
				cls
				cleZarplatDay = daystart
				vokzalVork += 1
				money += 250
				minut += 3
				gs 'stat'
				'When you are done cleaning all the places you were told to clean, you go to the cashier and collect your well-earned salary.'

				act 'Continue':minut += 1 & gt 'vokzalGin'
			end
		end
	end

	if hour = 11 and minut = 0:
		poezddalsled = 1
		'The 11:30 long-distance train is currently approaching the platform.'
	elseif hour = 11 and minut > 0 and minut < 30:
		poezddalsled = 1
		'The 11:30 long-distance train is currently waiting at the platform.'
	elseif hour = 11 and minut = 30:
		poezddalsled = 1
		'The 11:30 long-distance train is currently leaving the station.'
	elseif hour = 18 and minut = 0:
		poezddalsled = 1
		'The 18:30 long-distance train is currently approaching the platform.'
	elseif hour = 18 and minut > 0 and minut < 30:
		poezddalsled = 1
		'The 18:30 long-distance train is currently waiting at the platform.'
	elseif hour = 18 and minut = 30:
		poezddalsled = 1
		'The 18:30 long-distance train is currently leaving the station.'
	end

	if poezddalsled = 1:
		'The platform is bustling with passengers that are entering or leaving the currently waiting train, carrying their heavy bags with them. Several police officers are patrolling the platforms while the train is here, keeping an eye out for any unruly activities.'

		if tovarL > 0:
			act 'Sell your goods to passengers':gt 'vokzaltorg'
		end
	end
end

if $ARGS[0] = 'kassi':
	cls
!! This is a lie, but a useful one don''t change the $loc and $metka
	$loc = 'vokzalGin'
	$metka = 'oldtown'
	gs 'stat'
	'<center><h4>Ticket office</h4></center>'
	'<center><img src="images/qwest/alter/kassa.jpg"></center>'
	'The long-distance trains are at the station platforms between 11:00-11:30 and 18:00-18:30.'

	if money >= 50:
		act 'Buy a ticket to the old town (50 <b>₽</b>)':
			train_event = 0
			money -= 50
			train_dest = 5
			gt 'vokzalGin', 'oldtown'
		end
	end

	if money >= 75:
		act 'Buy a ticket to the village of Gadyukino (75 <b>₽</b>)':
			train_event = 0
			money -= 75
			train_dir = 1
			train_dest = 4
			gt 'vokzalGin', 'oldtown'
		end
	end

	if money >= 100:
		act 'Buy a ticket to the the communal village (100 <b>₽</b>)':
			train_event = 0
			money -= 100
			train_dir = 1
			train_dest = 3
			gt 'vokzalGin', 'oldtown'
		end
	end

	if money >= 125:
		act 'Buy a ticket to city industrial area (125 <b>₽</b>)':
			train_event = 0
			money -= 125
			train_dir = 1
			train_dest = 2
			if rand(1, 100) < 15: gt 'train', 'private'
			gt 'vokzalGin', 'oldtown'
		end
	end

	if money >= 150:
		act 'Buy a ticket to city center (150 <b>₽</b>)':
			train_event = 0
			money -= 150
			train_dir = 1
			train_dest = 1
			if rand(1, 100) < 15: gt 'train', 'private'
			gt 'vokzalGin', 'oldtown'
		end
	end

	act 'Leave the ticket office':minut += 1 & gt 'vokzalGin'
end


!!For travel north from Pav to old town
if $ARGS[0] = 'oldtown':
	close all
	if rand(1,100) < 10 and train_event = 0: gt 'train', 'weed'
	cls
	minut += 5
	gs 'stat'
	'<center><img src="images/etogame/electri.jpg"></center>'
	'You pay for the ticket and board the next train going to St. Petersburg.'
	'After 5 minutes, you see the familiar old buildings and the train stops at the station near the old town.'
	act 'Get off the train at this station':gt 'train', 'oldtown'
	if train_dest < 3 and rand(0, 100) < 15: gt 'train', 'private'
	if train_dest < 5:
		act 'Continue your journey':
			gt 'train', 'oldtownN'
		end
	end
end

--- vokzalGin ---------------------------------

