# nichUtil

!! Used variables-
!! Prefix- nich
!! nichWork- 0= does not work for nicholas and never has, 1=has agreed to work for Nicholas but has not introduced herself yet,  2= currently works for nicholas, 3= quit job in good faith and could get hired again, 4= quit job in bad faith or got thrown out and nicholas is blocked
!! nichWorkState- 0= sample days, 1= fully employed
!! nichPerformance- ranging from 0 (fired) to 100 (best)
!! nichHired- the day Sveta was hired
!! nichVacationFrom- the first free day if Sveta gets some time off 
!! nichVactionTil- the last free day if Sveta gets some time off (=nichVacationFrom if she has one free day)
!! nichLastWorkDay- the last day Sveta was in the appartment... needed because otherwise the player could avoid work performance checks by just not showing up here
!! nichMissedWorkdays- the number of days Sveta has missed work... hopefully she has a good excuse
!! nichEvaluationLast- the day Sveta had her last evaluation (may be today)
!! nichEvaluationLastReq- the last day Sveta was required to get an evaluation (may be today)
!! nichLastInspepection- the day Sveta last inspected the appartment (if it is today she has a list of chores she still has to do)
!! nichMaidBathG- how many days the guest bathroom has been used since it has been last cleaned. will not get dirty automatically, only when guests were present
!! nichMaidLaundry- how many days the laundry hasnt been done... doing it every 4 days is best
!! nichMaidDishes- 
!! nichMaidGroceries-
!! nichMaidHallway- how many days the hallway hasnt been cleaned... doing it every 2 days is best
!! nichMaidLivingDust
!! nichMaidLivingFloor
!! nichMaidLivingItems
!! nichMaidLivingWindows
!! nichOutfitState- 0=not wearing an appropriate outfit, 1=wearing an appropriate outfit

!! Chore IDs:
!! 0: hallway
!! 1: guest bathroom
!! 2: servant bedroom
!! 3: servant bathroom
!! 4: laundry
!! 5: tanya bedroom
!! 6: master bathroom
!! 7: master bedroom
!! 8: living room
!! 9: kitchen

!! nichGalaOpinion: 0- Gala has no opinion about PC, 1- Gala thinks PC is a slut

if $ARGS[0] = '' or $ARGS[0] = 'start':
	'This page should never appear. Please report this bug including a description of how you got here.'
end

if $ARGS[0] = 'startWorkday':
	nichMissedWorkdays = daystart - nichLastWorkDay - 1
		
	if nickHired + 1 = daystart:
		!! this is the first workday ever
		nichMissedWorkdays = 0
	elseif nichVactionTil + 1 = daystart:
		!! this is the first day Sveta has to work after a vacation... maybe work piled up?		
		nichMissedWorkdays = nichMissedWorkdays - nichVactionTil + nichVacationFrom -1
	elseif nichVactionTil >= daystart and nichVacationFrom <= daystart:
		!! Sveta does not have to work today
		nichMissedWorkdays = nichVacationFrom - nichLastWorkDay - 1
	end
	
	if nichMissedWorkdays > 0:
		!!Sveta has missed a workday... very bad
		nichPerformance -= 40 * nichMissedWorkdays
		if nichPerformance <= 0:
			'You see a note hanging at the door to your room.'
			'"<<$pcs_nichname>>, you missed work. This is unacceptable. Take your stuff and leave. You are fired. -Nicholas"'
			'You see all your belongings standing in front of the door prepared for your departure.'
			gt 'nichUtil', 'fired'
		else
			'You see a note hanging at the door to your room.'
			'"<<$pcs_nichname>>, you missed work. This is unacceptable. I expect this not to happen again. -Nicholas"'
		end
	elseif daystart > nichEvaluationLastReq and nichEvaluationLast < nichEvaluationLastReq:
		!! Sveta has missed her last evaluation
		nichPerformance -= 20
		if nichPerformance <= 0:
			'You see a note hanging at the door to your room.'
			'"<<$pcs_nichname>>, you missed your last evaluation. This is unacceptable. Take your stuff and leave. You are fired. -Nicholas"'
			'You see all your belongings standing in front of the door prepared for your departure.'
			gt 'nichUtil', 'fired'
		else
			'You see a note hanging at the door to your room.'
			'"<<$pcs_nichname>>, you missed your last evaluation. This is unacceptable. I expect this not to happen again. -Nicholas"'
		end
	end
	
	nichLastWorkDay = daystart
	nichEvaluationLastReq = daystart
	
	
	!! update the vars for all the chores
	nichTemp = 0
	:choreLoopStart
	nichChoreState[nichTemp] += 5
	if nichTemp < ARRSIZE('nichChoreState') -1: nichTemp += 1 & jump 'choreLoopStart'
	
	
end

!! get rid of all variables concerning Nicholas except for nichWork
if $ARGS[0] = 'clearVars':
	killvar 'nichPerformance'
	killvar 'nickHired'
	killvar 'nichVacationFrom'
	killvar 'nichVactionTil'
	killvar 'nichLastWorkDay'
	killvar 'nichMissedWorkdays'
	killvar 'nichEvaluationLast'
	killvar 'nichEvaluationLastReq'
	killvar 'nichMaidBathG'
	killvar 'nichMaidLaundry'
	killvar 'nichMaidDishes'
	killvar 'nichMaidGroceries'
	killvar 'nichMaidHallway'
	killvar 'nichMaidLivingDust'
	killvar 'nichMaidLivingFloor'
	killvar 'nichMaidLivingItems'
	killvar 'nichMaidLivingWindows'
	killvar 'nichLastInspepection'
	killvar 'nichOutfitState'
end

if $ARGS[0] = 'fired':
	'It takes you a few moments to process what just happened:'
	'You have just been fired. And kind of lost your home, too.'
	
	nichWork = 2
	
	gs 'nichUtil', 'clearVars'
	
	act 'Take your belongings and leave':gt 'down'
end


!! initializes variables when hired, might mess everything up if called at any other time
if $ARGS[0] = 'hired':
	nichWork = 2
	nichWorkState = 0
	nichPerformance = 20
	nickHired = daystart
	!!Todo: check the following
	nichVacationFrom = daystart
	nichVactionTil = daystart
	nichLastWorkDay = daystart
	nichEvaluationLast = daystart
	nichEvaluationLastReq = daystart
	nichMaidBathG = 0
	nichMaidLaundry = 0
	nichMaidDishes = 0
	nichMaidGroceries = 0
	nichMaidHallway = 0
	nichMaidLivingDust = 0
	nichMaidLivingFloor = 0
	nichMaidLivingItems = 0
	nichMaidLivingWindows = 0
	nichLastInspepection = -1
	
	nichChoreState[0] = 0
end

!! convert the old vars to the new system
if $ARGS[0] = 'convert':
	if tanwork = 1:
		gs 'nichUtil', 'hired'
		nichPerformance = tan_work_rep * 5
	end
	killvar 'MaidWorkDay'
	killvar 'MaidWork'
	killvar 'TanHallClean'
	killvar 'TanRoomClean'
	killvar 'TanBathClean'
	killvar 'TanMasterClean'
	killvar 'TanLivingClean'
	killvar 'TanMaidRoomClean'
	killvar 'TanKitchenClean'
	killvar 'TanMadeBreakfast'
	killvar 'tanwork'
	killvar 'tan_work_rep'
	killvar 'tan_work_evaluation'
end


if $ARGS[0] = 'checkOutfit':
	if $clothingworntype = 'uniform':
		nichOutfitState = 1
	else
		nichOutfitState = 0
	end
end

if $ARGS[0] = 'cleanOptions':
	$nichTempPic = func('nichUtil','cleanPic',$nichChoreType,$nichChoreLoc)
	
	if nichOutfitState = 0:
		'<b><font color = red>You have to change into an appropriate outfit before cleaning up.</font></b>'
	elseif nichChoreState[nichChoreID] > 0:
		act '(<<nichtTimeQuick>> minutes) Clean quickly':
			*clr & cla
			'<center><img <<$set_imgh>> src="<<$nichTempPic>>"></center>'
			minut += nichtTimeQuick
			nichChoreState[nichChoreID] = 0
			cleaning_exp += rand (0,1)
			gs'stat'
			act 'Finish':
				gt $loc
			end
		end
		
		act '(<<nichTimeNormal>> minutes) Clean normally':
			*clr & cla
			'<center><img <<$set_imgh>> src="<<$nichTempPic>>"></center>'
			minut += nichTimeNormal
			nichChoreState[nichChoreID] = 0
			cleaning_exp += rand (1,3)
			act 'Finish':
				gt $loc
			end
		end
		
		act '(<<nichTimeDiligently>> minutes) Clean diligently':
			*clr & cla
			'<center><img <<$set_imgh>> src="<<$nichTempPic>>"></center>'
			minut += nichTimeDiligently
			nichChoreState[nichChoreID] = 0
			cleaning_exp += rand (1,5)
			act 'Finish':
				gt $loc
			end
		end
	end
	
	act 'Don''t clean':
		gt $loc
	end
end

!! Returns the path to a picture of a maid cleaning something. If there are more than one pictures matching the citeria one of them is chosen at random.
!! Arguments:
!! 1 (required): What to clean/do (bed, floor, furniture)
!! 2 (optional): Where (servant (=bedroom servant), living, kitchen, hallway)
!! 3 (optional): How to clean (quickly, normally, diligently)
if $ARGS[0] = 'cleanPic':
	if $ARGS[1] = 'bed':
		$nichTempPic[ARRSIZE('$nichTempPic')] = 'maid(7).jpg'
		$nichTempPic[ARRSIZE('$nichTempPic')] = 'maid(21).jpg'
		$nichTempPic[ARRSIZE('$nichTempPic')] = 'maid(80).jpg'
		if $ARGS[2] = 'servant' or $ARGS[2] = 'tanya':
			$nichTempPic[ARRSIZE('$nichTempPic')] = 'maid(27).jpg'
			$nichTempPic[ARRSIZE('$nichTempPic')] = 'maid(34).jpg'
			$nichTempPic[ARRSIZE('$nichTempPic')] = 'maid(71).jpg'
		elseif $ARGS[2] = 'master':
			$nichTempPic[ARRSIZE('$nichTempPic')] = 'maid(79).jpg'
		end
	elseif $ARGS[1] = 'floor':
		$nichTempPic[ARRSIZE('$nichTempPic')] = 'maid(9).jpg'
	elseif $ARGS[1] = 'bath':
		$nichTempPic[ARRSIZE('$nichTempPic')] = 'maid(35).jpg'
	end
	
	if ARRSIZE('$nichTempPic') = 0:
		!!generic fallback in case no matching picture was found
		$RESULT = 'images/characters/city/nicholas/maid/maid(0).jpg'
	else
		$RESULT = 'images/characters/city/nicholas/maid/'+$nichTempPic[rand(0,ARRSIZE('$nichTempPic')-1)]
	end
	
	!!killvar 'nichTemp'
	killvar '$nichTempPic'
end

!! Returns the path to a picture of Tanya.
!! Arguments:
!! 1 (required): General activity ("idle" for something not sex-related, "sexL" for lesbian sex, "sexH" for sex with a man)
!! 2 (required for sex): Specific activity (prefix a: Tanya act, prefix p: Tanya get acted upon; "")
!! 3 (optional): Partner for sex-options (0 or unset: PC, 1: PC and ignore PC appearance)
if $ARGS[0] = 'tanyaPic':
	gs 'nichUtil','updatePcHair'

	if $ARGS[1] = 'sexL' or $ARGS[1] = 'sexH':
		!! Pictures which work for both lesbian and heterosexual sex
		if $ARGS[2] = 'after':
			!! pictures of Tanya relaxing after sex
			$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sex/tanya_sex(0).jpg'
			$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sex/tanya_sex(1).jpg'
			$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sex/tanya_sex(2).jpg'
			$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sex/tanya_sex(3).jpg'
			$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sex/tanya_sex(4).jpg'
			$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sex/tanya_sex(5).jpg'
		elseif $ARGS[2] = 'seduce':
			
		end
	end
	if $ARGS[1] = 'sexL':
		if $ARGS[2] = 'kiss':
			if ARGS[3] = 1 or (ARGS[3] = 0 and nichPCHair = 0):
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(9).jpg'
			end
			if $ARGS[3] = 1 or ($ARGS[3] = 0 and nichPCHair = 1):
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(1).jpg'
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(2).jpg'
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(4).jpg'
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(16).jpg'
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(17).jpg'
			end
			
		elseif $ARGS[2] = 'aeatout':
			$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(3).jpg'
			$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(5).jpg'
			$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(7).jpg'
			if $ARGS[3] = 1 or ($ARGS[3] = 0 and nichPCHair = 0):
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(10).jpg'
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(11).jpg'
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(24).jpg'
			end
			if $ARGS[3] = 1 or ($ARGS[3] = 0 and nichPCHair = 1):
			
			end
		elseif $ARGS[2] = 'peatout':
			
			$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(14).jpg'
			if ARGS[3] = 1 or (ARGS[3] = 0 and nichPCHair = 0):
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(13).jpg'
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(22).jpg'
			end
			if ARGS[3] = 1 or (ARGS[3] = 0 and nichPCHair = 1):
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(6).jpg'
			end
		elseif $ARGS[2] = 'alickbreast':
			if ARGS[3] = 1 or (ARGS[3] = 0 and nichPCHair = 0):
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(8).jpg'
			end
			if ARGS[3] = 1 or (ARGS[3] = 0 and nichPCHair = 1):
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(19).jpg'
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(25).jpg'
			end
		elseif $ARGS[2] = 'plickbreast':
			if ARGS[3] = 1 or (ARGS[3] = 0 and nichPCHair = 0):
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(23).jpg'
			end
		elseif $ARGS[2] = 'alickbutt':
			if ARGS[3] = 1 or (ARGS[3] = 0 and nichPCHair = 1):
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(18).jpg'
			end
		elseif $ARGS[2] = 'aride':
			if ARGS[3] = 1 or (ARGS[3] = 0 and nichPCHair = 1):
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(12).jpg'
			end
		elseif $ARGS[2] = 'arimjob':
			$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(15).jpg'
			if ARGS[3] = 1 or (ARGS[3] = 0 and nichPCHair = 0):
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(27).jpg'
			end
		elseif $ARGS[2] = 'avibrator':
			if ARGS[3] = 1 or (ARGS[3] = 0 and nichPCHair = 0):
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(20).jpg'
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(28).jpg'
			end
		elseif $ARGS[2] = 'pvibrator':
			if ARGS[3] = 1 or (ARGS[3] = 0 and nichPCHair = 0):
				$nichTempPicArr[ARRSIZE('$nichTempPicArr')] = 'sexL/tanya_sex(26).jpg'
			end
		end
	end
	!!TODO: 20,
	
	
	if ARRSIZE('$nichTempPicArr') = 0:
		!!TODO: better default
		$RESULT = 'images/characters/city/nicholas/tanya/01.jpg'
	else
		$RESULT = 'images/characters/city/nicholas/tanya/'+$nichTempPicArr[rand(0,ARRSIZE('$nichTempPicArr')-1)]
	end
	
	
	killvar '$nichTempPicArr'
end

!! sets nichPCHair to:
!! 0 if PC has brown, dark red or black hair (any natural color which is not blonde)
!! 1 if PC is blonde
!! 2 if PC has any other hair color
if $ARGS[0] = 'updatePcHair':
	if pcs_haircol = 3:
		nichPCHair = 1
	elseif pcs_haircol > 3:
		nichPCHair = 2
	else
		nichPCHair = 0
	end
end

if $ARGS[0] = 'npcActivityAdd':
	$nichNpcActArr[ARRSIZE('$nichNpcActArr')] = $ARGS[1]
	nichNpcActTime[ARRSIZE('nichNpcActTime')] = ARGS[2]*60 + ARGS[3]
end

if $ARGS[0] = 'npcActivity':
	if $ARGS[1] = 'nicholas':
		!!Morning routine
		if week <= 5:
			gs 'nichUtil', 'npcActivityAdd', 'sleep', 6 , 45
			gs 'nichUtil', 'npcActivityAdd', 'bathMorning', 7 , 15
			gs 'nichUtil', 'npcActivityAdd', 'study', 7 , 45
			gs 'nichUtil', 'npcActivityAdd', 'breakfast', 8 , 30
		else
			gs 'nichUtil', 'npcActivityAdd', 'sleep', 7 , 45 
			gs 'nichUtil', 'npcActivityAdd', 'bathMorning', 8 , 15
			gs 'nichUtil', 'npcActivityAdd', 'study', 8 , 45
			gs 'nichUtil', 'npcActivityAdd', 'breakfast', 9 , 30
		end
		
		!!day avtivity
		if week <= 4:
			gs 'nichUtil', 'npcActivityAdd', 'leaveWork', 8 , 35
			gs 'nichUtil', 'npcActivityAdd', 'work', 18 , 0
			gs 'nichUtil', 'npcActivityAdd', 'returnWork', 18 , 05
		elseif week = 5:
			gs 'nichUtil', 'npcActivityAdd', 'leaveWork', 8 , 35
			gs 'nichUtil', 'npcActivityAdd', 'work', 16 , 0
			gs 'nichUtil', 'npcActivityAdd', 'returnWork', 16 , 05
		elseif week = 6:
			gs 'nichUtil', 'npcActivityAdd', 'leaveShopping', 9 , 35
			gs 'nichUtil', 'npcActivityAdd', 'shopping', 16 , 0
			gs 'nichUtil', 'npcActivityAdd', 'returnShopping', 16 , 05
		else
			gs 'nichUtil', 'npcActivityAdd', 'living', 16 , 0
		end
		
		!!evening
		if week <= 4:
			gs 'nichUtil', 'npcActivityAdd', 'dinner', 18 , 30
			gs 'nichUtil', 'npcActivityAdd', 'study', 20 , 30
			if week = 1:
				gs 'nichUtil', 'npcActivityAdd', 'study', 22 , 0
			elseif week = 2 or week = 4:
				gs 'nichUtil', 'npcActivityAdd', 'tanya', 22 , 0
			elseif week = 3:
				gs 'nichUtil', 'npcActivityAdd', 'living', 22 , 0
			end
			gs 'nichUtil', 'npcActivityAdd', 'bathEvening', 22 , 15
			gs 'nichUtil', 'npcActivityAdd', 'sleep', 24 , 0
		elseif week = 5 or week = 6:
			gs 'nichUtil', 'npcActivityAdd', 'study', 18 , 0
			gs 'nichUtil', 'npcActivityAdd', 'bathClub', 18 , 30
			gs 'nichUtil', 'npcActivityAdd', 'leaveClub', 18 , 35
			gs 'nichUtil', 'npcActivityAdd', 'club', 23 , 45
			gs 'nichUtil', 'npcActivityAdd', 'returnClub', 23 , 50
			gs 'nichUtil', 'npcActivityAdd', 'sleep', 24 , 0
		else
			gs 'nichUtil', 'npcActivityAdd', 'study', 18 , 0
			gs 'nichUtil', 'npcActivityAdd', 'dinner', 18 , 30
			gs 'nichUtil', 'npcActivityAdd', 'gala', 21 , 30
			gs 'nichUtil', 'npcActivityAdd', 'bathEvening', 21 , 45
			gs 'nichUtil', 'npcActivityAdd', 'sleep', 24 , 0
		end
	elseif $ARGS[1] = 'gala':
		!!Morning routine
		if week <= 5:
			gs 'nichUtil', 'npcActivityAdd', 'sleep', 6 , 45
			!! gala will wake up the same time Nicholas does (->alarm clock) but she will wait for him to be finished in bath before she goes there
			gs 'nichUtil', 'npcActivityAdd', 'snooze', 7 , 15
			!! Gala is in bath while Nicholas is in his study
			gs 'nichUtil', 'npcActivityAdd', 'bathMorning', 7 , 45
			!! note: breakfast time should be the same for all three characters
			gs 'nichUtil', 'npcActivityAdd', 'breakfast', 8 , 30
		else
			gs 'nichUtil', 'npcActivityAdd', 'sleep', 7 , 45
			gs 'nichUtil', 'npcActivityAdd', 'snooze', 8 , 15
			gs 'nichUtil', 'npcActivityAdd', 'bathMorning', 8 , 45
			gs 'nichUtil', 'npcActivityAdd', 'breakfast', 9 , 30
		end
		!! daily activity
		if week = 1 or week = 3:
			gs 'nichUtil', 'npcActivityAdd', 'living', 18 , 05
		elseif week = 2:
			!! Gala goes to spa on Tuesdays
			gs 'nichUtil', 'npcActivityAdd', 'living', 9 , 0
			gs 'nichUtil', 'npcActivityAdd', 'leaveSpa', 9 , 5
			gs 'nichUtil', 'npcActivityAdd', 'spa', 15 , 30
			gs 'nichUtil', 'npcActivityAdd', 'returnSpa', 15 , 35
			gs 'nichUtil', 'npcActivityAdd', 'living', 18 , 05
		elseif week = 4:
			!! Gala visits a friend on Thursdays
			gs 'nichUtil', 'npcActivityAdd', 'living', 9 , 0
			gs 'nichUtil', 'npcActivityAdd', 'leaveFriend', 9 , 5
			gs 'nichUtil', 'npcActivityAdd', 'friend', 17 , 30
			gs 'nichUtil', 'npcActivityAdd', 'returnFriend', 17 , 35
			gs 'nichUtil', 'npcActivityAdd', 'living', 18 , 05
		elseif week = 5:
			!! TODO: Friday plans
			gs 'nichUtil', 'npcActivityAdd', 'living', 18 , 05
		elseif week = 6:
			!! TODO: Saturday plans
			gs 'nichUtil', 'npcActivityAdd', 'living', 18 , 05
		else
			!! TODO: Sunday plans
			gs 'nichUtil', 'npcActivityAdd', 'living', 18 , 0
		end
		!!evening
		if week <= 4:
			!! dinner has to be the same time for all three characters
			gs 'nichUtil', 'npcActivityAdd', 'dinner', 18 , 30
			gs 'nichUtil', 'npcActivityAdd', 'study', 20 , 30
			gs 'nichUtil', 'npcActivityAdd', 'bathEvening', 22 , 15
			gs 'nichUtil', 'npcActivityAdd', 'sleep', 24 , 0
		elseif week = 5 or week = 6:
			!! Gala will prepare herself for the club in the bedroom
			!! she will accompany Nicholas there, so their times have to be the same
			gs 'nichUtil', 'npcActivityAdd', 'prepareClub', 18 , 30
			gs 'nichUtil', 'npcActivityAdd', 'leaveClub', 18 , 35
			gs 'nichUtil', 'npcActivityAdd', 'club', 23 , 45
			gs 'nichUtil', 'npcActivityAdd', 'returnClub', 23 , 50
			gs 'nichUtil', 'npcActivityAdd', 'sleep', 24 , 0
		else
			gs 'nichUtil', 'npcActivityAdd', 'dinner', 18 , 30
			!! Nicholas will do her at this time, so she must have the corresponding entry
			gs 'nichUtil', 'npcActivityAdd', 'nicholas', 21 , 30
			gs 'nichUtil', 'npcActivityAdd', 'bathEvening', 21 , 45
			gs 'nichUtil', 'npcActivityAdd', 'sleep', 24 , 0
		end
	end
	
	$RESULT = $nichNpcActArr[0]
	
	nichNpcActC = 0
	
	:nichNpcActivityLoop
	if nichNpcActTime[nichNpcActC] <= hour * 60 + minut:
		nichNpcActC += 1
		if nichNpcActC < ARRSIZE('nichNpcActTime'): jump 'nichNpcActivityLoop'
	end
	
	$RESULT = $nichNpcActArr[nichNpcActC]
	
	killvar '$nichNpcActArr'
	killvar 'nichNpcActTime'
	killvar '$nichNpcActC'
end

if $ARGS[0] = 'whereIs':	
	$RESULT = 'nowhere'
	if $ARGS[1] = 'nicholas':
		$nichTemp = func('nichUtil','npcActivity','nicholas')
		if $nichTemp = 'sleep' or $nichTemp = 'gala':
			$RESULT = 'masterBedroom'
		elseif $nichTemp = 'bathMorning' or $nichTemp = 'bathClub' or $nichTemp = 'bathEvening':
			$RESULT = 'masterBathroom'
		elseif $nichTemp = 'breakfast' or $nichTemp = 'living' or $nichTemp = 'dinner':
			$RESULT = 'living'
		elseif $nichTemp = 'leaveWork' or $nichTemp = 'returnWork' or $nichTemp = 'leaveShopping' or $nichTemp = 'returnShopping' or $nichTemp = 'leaveClub' or $nichTemp = 'returnClub':
			$RESULT = 'hallway'
		elseif $nichTemp = 'work':
			$RESULT = 'work'
		elseif $nichTemp = 'study':
			$RESULT = 'study'
		elseif $nichTemp = 'tanya':
			$RESULT = 'bedroomTanya'
		elseif $nichTemp = 'club':
			$RESULT = 'club'
		elseif $nichTemp = 'shopping':
			$RESULT = 'shopping'
		end
	elseif $ARGS[1] = 'cook':
		$RESULT = 'kitchen'
	end
end

if $ARGS[0] = 'isHome':
	$nichTemp = func('nichUtil','whereIs',$ARGS[1])
	RESULT = 0
	if $nichTemp = 'masterBedroom' or $nichTemp = 'masterBathroom' or $nichTemp = 'bedroomTanya' or $nichTemp = 'bedroomServant'  or $nichTemp = 'bathroomServant'  or $nichTemp = 'bedroomGuest'  or $nichTemp = 'study'  or $nichTemp = 'living'  or $nichTemp = 'kitchen':
		RESULT = 1
	end
end

if $ARGS[0] = 'isPresent':
	$npcLocation = FUNC('nichUtil','whereIs',$ARGS[1])
	if $npcLocation = $ARGS[2]:
		RESULT = 1
	else
		RESULT = 0
	end
	
end
--- nichUtil ---------------------------------