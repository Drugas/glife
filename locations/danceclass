# danceclass
clr
minut += 5
razdtumbler = 0
gs 'stat'
if night_mode = 1:
	fcolor = rgb(255, 255, 255)
	bcolor = rgb(0, 0, 0)
	lcolor = rgb(106, 90, 205)
else
	fcolor = rgb(0, 0, 0)
	bcolor = rgb(255, 255, 255)
	lcolor = rgb(106, 90, 205)
end
'<center><b><font color="maroon">Dance Studio</font></b></center>'
'<center><img src="images/locations/city/citycenter/gym/dance/danceclass.jpg"></center>'
'A big, open room with good lighting and huge mirrors, perfect for developing dance skills.'

act 'Modern dance class (0:30)':
	cla
	*clr
	timemult = 2
	loopcount = 1
	:timeloop
	gs 'exercise', 'tier2', 0, 'agil_exp', 'stren_exp', 'danc_exp'
	if loopcount < timemult: loopcount += 1 $ jump 'timeloop'
	loopcount = 0
	timemult = 0
	abonement -= 1
	'<center><img src="images/locations/shared/gym/fit1.jpg"></center>'

	if danc_lvl < 100:'You studied modern dance, which is very popular in clubs.'
	if danc_lvl >= 100:'Though you worked hard, you realize there is nothing more worth learning from this class.'

	act 'Leave':gt 'fit', 'dressing room'
end

act 'Striptease class (0:30)':
	cla
	*clr
	gs 'exercise', 'tier2', 0, 'agil_exp', 'stren_exp', 'danc_exp', 'dancero_exp'
	abonement -= 1
	'<center><img src="images/locations/city/citycenter/gym/dance/stripdance.jpg"></center>'

	if danc_lvl >= 50:
		gs 'exercise', 'tier2', 0, 'dancero_exp'
		'You studied the art of burlesque, imaging yourself stripping to the music.'

		if pcs_dancero >= 100:'But there is nothing more worth learning, since you have already mastered the art.'
	else
		gs 'exercise', 'tier2', 0, 'danc_exp'
		'This isn''t just taking clothes off, it''s a difficult dance style and your basics aren''t upto the task. The teacher instead helps you work on your modern dancing.'
	end

	act 'Leave':gt 'fit', 'dressing room'
end

if pcs_stren >=40 or needstrength = 0:
	act 'Pole dancing class (0:30)':
		cla
		*clr
		gs 'exercise', 'tier2', 0, 'agil_exp', 'stren_exp', 'dancpol_exp'
		abonement -= 1
		'<center><img src="images/locations/city/citycenter/gym/dance/poledance.jpg"></center>'

		if dancero_lvl >= 50 and pcs_stren >= 40:
			gs 'exercise', 'tier2', 0, 'dancpol_exp'
			'You studied various acrobatics on the pole.'

			if dancpol_lvl >= 100:'But you could not learn anything new, you''re already a master of the art.'
		else
			'It''s not pretty and you bang your head on the pole. This is far harder than it looks. It will help if you improve your modern dancing or striptease but you did learn a little.'

			if pcs_stren < 40:'The instructor tries to be patient, but it is clear that you are too weak for this sort of dance.' & needstrength = 1
		end

		act 'Leave':gt 'fit', 'dressing room'
	end
end


--- danceclass ---------------------------------

