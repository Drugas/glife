# nichBathServant

CLOSE ALL

$metka = ''
$loc = 'nichBathServant'
$locM = $loc
$location_type = 'bathroom'
$menu_loc = 'nichBathServant'
$menu_arg = ''
menu_off = 0

*clr
gs 'stat'
gs 'themes', 'indoors'
!! Argument return: PC has been in the room before, dont perform actions meant to be executed before or as she enters the room.
if $ARGS[0] = '' or $ARGS[0] = 'start' or $ARGS[0] = 'return':
	'<center><b><font color="maroon">Servant Bathroom</font></b></center>'
	'<center><img <<$set_imgh>> src="images/characters/city/nicholas/apartment/bathroomServant.jpg"></center>'
	'This is the servants bathroom. It includes a shower, a toilet and a sink. There is a washing machine standing in one corner of the room.' 



	if nichWork = 2:
		if shampoo = 0: 
			'Since you are allowed to use some of the families shampoo for yourself you open up a new bottle and place it inside your shower.'
			shampoo = 15
		end
	
		act 'Go to your bedroom':gt 'nichBedroomServant'

		act 'Inspect room':
			*clr & cla
			nichChoreID = 3
			nichChoreTimeF = 1
			$nichChoreType = 'bath'
			$nichChoreLoc = 'bathServant'
			if nichChoreState[nichChoreID] <= 5:
				'The bathroom is not perfectly clean but still clean enough. You don''t have to clean it now.'
			else
				'Some of your hair is lieing on the ground, the shower could use some cleaning and there is some dust and dried spots of soap on the sink.'
			end
			
			nichtTimeQuick = 10* nichChoreTimeF
			nichTimeNormal = 20* nichChoreTimeF
			nichTimeDiligently = 30* nichChoreTimeF
			
			gs 'nichUtil', 'cleanOptions'
			
		end
		act 'Inspect laundry':
			*clr & cla
			nichChoreID = 4
			nichChoreTimeF = 1
			$nichChoreType = 'laundry'
			$nichChoreLoc = 'bathServant'
			
			if nichChoreState[nichChoreID] = 0:
				'There is no laundry to do right now.'
				act 'Back':
					gt $loc, 'return'
				end
				exit
			elseif nichChoreState[nichChoreID] <= 15:
				'A small pile of laundry lies in front of the washing machine. It is hardly enough to fill the machine halfway.'
			elseif nichChoreState[nichChoreID] <= 25:
				'A pile of laundry lies in front of the washing machine waiting to be washed.'
			elseif nichChoreState[nichChoreID] <= 35:
				'A big pile of laundry lies in front of the washing machine. You will have to fill the machine several times to get it all done.'
			else
				'The pile of laundry in front of the washing machine is enormous. It appears as if the majority of Nicholas'' clothes is here instead of his wardrobe now.'
			end
						
			$nichTempPic = func('nichUtil','cleanPic',$nichChoreType,$nichChoreLoc)
						
			act 'Do laundry quickly (60 minutes)':
				minut += 60
				cleaning_exp += rand (0,1)
				'<center><img <<$set_imgh>> src="<<$nichTempPic>>"></center>'
				'You think about seperating the clothes before you wash them, but then you decide to put them into the washing machine all at once.'
				'While the washing machine washes them you wash the delicates by hand by putting all of them into the sink at the same time.'
				'After the washing washine is done you place its load in the dryer.'
				'Afterwards you iron the clothes quickly.'
				if pcs_cleaning < 80:
					nichRand = rand(1,10)
					if nichRand = 1:
						'Once you are done you realize that one of the shirts of Nicholas has some visible spots of color on it. There is no way you could fix that. Some other clothes are also affected, but you might get them clean by washing them again.'
						nichChoreModLaundry1 = 1
						nichChoreState[nichChoreID] = max(nichChoreState[nichChoreID] - 5,0)
					elseif nichRand <= 3:
						'Taking a look at the laundry you realize that you didn''t manage to clean it properly. You will have to do it all over again.'
					else
						nichChoreState[nichChoreID] = max(nichChoreState[nichChoreID] - 15,0)
					end
				else
					nichChoreState[nichChoreID] = max(nichChoreState[nichChoreID] - 15,0)
				end
				cla
				act 'Finish':
					gt $loc, 'return'
				end
			end
			
			act 'Do laundry (120 minutes)':
				minut += 120
				
				cleaning_exp += rand (1,3)
				'<center><img <<$set_imgh>> src="<<$nichTempPic>>"></center>'
				'You start by seperating the laundry by color.'
				'While the washing machine washes the dark and then the light clothes you wash the delicates by hand.'
				'Whenever the washing washine is done you place its load in the dryer.'
				'Everything that come from the dryer you iron.'
				if pcs_cleaning < 60 and rand(1,10) <= 2:
					'Just before you are about to finish you notice that some of the clothes you just ironed have some visible spots of dirt on them. It seems as if you have to clean some of them all over again.'
					nichChoreState[nichChoreID] = max(nichChoreState[nichChoreID] - 20,nichChoreState[nichChoreID]/2)
				else
					nichChoreState[nichChoreID] = max(nichChoreState[nichChoreID] - 20,0)
				end
				cla
				act 'Finish':
					gt $loc, 'return'
				end
			end
			
			act 'Do laundry diligently (240 minutes)':
				minut += 240
				nichChoreState[nichChoreID] = max(nichChoreState[nichChoreID] - 30,0)
				cleaning_exp += rand (1,5)
				'<center><img <<$set_imgh>> src="<<$nichTempPic>>"></center>'
				'You start by seperating the laundry by color and fabric.'
				'One after one you have the washing machine wash the dark cotton, white cotton, dark cloth and light cloth.'
				'Meanwhile you wash the delicates by hand.'
				'Whenever the washing washine is done you place its load in the dryer.'
				'Everything that come from the dryer you iron, making sure you don''t miss a single crease.'
				cla
				act 'Finish':
					gt $loc, 'return'
				end
			end
			act 'Don''t do laundry':
				gt $loc, 'return'
			end
		end
	else
		act 'Go to the servants bedroom':gt 'nichBedroomServant'
	end

	gs 'din_van', 'private'

end
--- nichBathServant ---------------------------------