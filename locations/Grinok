# Grinok
!{change log
March 31, 2016
Added rice wine quest
April 20, 2016
Fixed Rice wine event
}
$location_type = 'public_outdoors'
if $ARGS[0] = '':
	$metka = $ARGS[0]
	$loc = $CURLOC
	CLOSE ALL
	if sound = 0:
		if month >=11 and month <= 12 or month >=1 and month <=3:
			if hour >= 8 and hour <= 23: PLAY 'sound/rynok.mp3',30 & PLAY 'sound/zima.mp3',30
			if hour >= 0 and hour <= 7: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/zima.mp3',30
		else
			if hour >= 8 and hour <= 23: PLAY 'sound/rynok.mp3',30 & PLAY 'sound/street_leto.mp3',30
			if hour >= 0 and hour <= 7:PLAY 'sound/street_night.mp3',30
		end
	end

	clr
	gs 'stat'
	'<center><h4>Market</h4></center>'
	'<center><img src="images/locations/pavlovsk/market/grinok.jpg"></center>'
	'This small market is location near the station.'

	if arturQW = 1 and (week = 6 or week = 7) and hour < 12:
		act 'Wait for Arthur to arrive':hour = 12 & gt 'grinok'
	end

	if hour >= 8 and hour <= 18:
		'In the market the sellers of goods are beckoning shoppers.'
		if gorslut > 2 and rand(0,9) = 0:
			'An Armenian man comes up to you and in broken Russian says, "You girl who like men, no? Come meet real men have good time."'
			act 'Go with him':gt 'gevent', '3'
			act 'Reject him':
				'You pretend not to understand what he is saying.'
				act 'Return':gt 'Grinok'
			end
			
			exit
		end
		act 'Wander through the market': gt 'Grinok','events'
		!expand market events from 1.2.4
		
		if pornmarkonce = 1:
			act 'Go to the stall selling porn':gt 'Gpornmark'
		end

		if arturKnow = 1 and arturQW = 0:
			act 'Look for Arthur':gt 'arturRinok', 'start'
		elseif arturQW = 1 and (week = 6 or week = 7):
			if hour = 12 or hour = 13:
				act 'Look for Arthur':gt 'arturRinok', 'work_1'
			end
		elseif arturQW = 2:
			if hour = 16 and minut <= 30:
				act 'Look for Arthur':gt 'arturRinok', 'work_2'
			elseif hour < 16:
				act 'Wait for Arthur to arrive':hour = 16 & minut = 0 & gt 'Grinok'
			end
		end

		act 'Go to the book stall':
			cls
			minut += 15
			gs 'stat'
			'On the shelves are very entertaining fantasy books, which costs only 150 <b>₽</b> each and there are also women''s romance paperback novels for 300 <b>₽</b> each.'

			if money >= 150:
				act 'Buy a fantasy book (150 <b>₽</b>)':
					cls
					minut += 15
					money -= 150
					f_book += 1
					gs 'stat'
					'You bought a fantasy book for 150 <b>₽</b>.'

					act 'Leave':gt 'Grinok'
				end
			end

			if money >= 300:
				act 'Buy a romance novel (300 <b>₽</b>)':
					cls
					minut += 15
					money -= 300
					w_book += 1
					gs 'stat'
					'You bought a romance novel for 300 <b>₽</b>.'

					act 'Leave':gt 'Grinok'
				end
			end

			act 'Leave':gt $curloc
		end

		
		if gobelen > 0 and gobQW = 0:
			act'Sell ​​tapestry':
				cls
				gobQW = 1
				minut += 15
				gs'stat'
				'You stand quietly by the entrance to the market, waiting for customers and displaying your tapestries. Shortly buyers start to gather, but a large, muscular man approaches you and says, "It is illegal to trade in the market without a trader''s licence, but I may be interesting in buying your tapestries, 500 <b>₽</b> apiece. Come and see me at my stall."'
				'Not wanting to get into trouble, you pack your stuff away. The man was already gone, but you saw which direction he went and he should be easy to find.'
				act 'Leave':gt 'Grinok'
			end
		end

		if gobelen > 0 and gobQW >= 1:
			act'Sell ​​tapestry':
				cls
				minut += 15
				gs'stat'
				'You go to the man who offered to buy your tapestries for 500 <b>₽</b>. You find him by his stall, smiling.'

				if gobQW = 2:
					'Roman welcomes you. He is interested and businesslike as he asks what you have for sale.'
				end

				if gobQW = 1:
					gobQW = 2
					minute + 5
					'The man greets you and introduces himself as Roman, you tell him that your name is <<$pcs_nickname>>. After a brief chat Roman says that he is interested in seeing what you have for sale.'
				end

				act'Show your tapestry':
					cls
					gobelen -= 1
					money += 500
					gs'stat'
					'You show your tapestry to Roman and he gives you five hundred for it. You take the money and give him the tapestry.'
					act 'Leave':gt 'Grinok'
				end

				if gobelen > 1:
					act'Sell all of your tapestries':
						cls
						kolgob = gobelen
						gobelen = 0
						moneyGob = kolgob*500
						money += moneyGob
						gs'stat'
						'You show Roman your tapestries and he gives you <<moneyGob>> <b>₽</b> for them. You take the money and give him the tapestries.'
						act 'Leave':gt 'Grinok'
					end
				end

				act 'Leave':gt 'Grinok'
			end
		end
		if ShowerIvan = 5 and porndeal = 0 and rand(1,100) <= 10 and lookwine ! daystart:
			cls
			cla
			gs 'stat'
			minut += 20
			lookwine = daystart
			'<center><img src="images/locations/pavlovsk/market/chinese_stall01.jpg"></center>'
			'You were looking through the market when you came upon a stall selling Chinese foods. On a shelf behind the old woman minding the stall there are a number of bottles.'
			act 'Ask about huangjiu':
				cla
				'You asked the old woman if she has any rice wine to sell. She smiles at you and points to a basket filled with rice.'
				'"Onry 2 kopec, hundred glam." she said in badly accented Russian, "You buy?"'
				'"No," you replied, "I need rice wine, rice wine."'
				'She looked at you in confusion for a moment then suddenly brightened, "Ah!" she exclaimed, "Come, come!"'
				'The old woman hurried to the other side of the stall and you followed her to where a number of smoked piglets hung in a row.'
				'"Here," the old woman crowed handing you one of r the pigs, "Nice swine, vely nice swine."'
				'"No, not nice swine," you cried in dispair, "Rice. Wine."'
				'"Yes, yes," the woman countered, "Nice swine, only 5 lubles."'
				'Just as you were about to give up, a young Chinese boy about your brother''s age arrived.'
				'"I am sorry, my grandmother does not speak much Russian." he said as he stepped up beside the old woman who started to speak rapidly to him in what you assumed was Chinese.'
				'"My grandmother asks why you don''t like the piglets." he translated as the old woman stopped speaking.'
				'"I don''t want a pig," you replied in relief as his Russian is quite good, "I am looking for rice wine, wang-goo."'
				'"Ah, huangjiu!" he said in understanding then spoke to his grandmother in Chinese.'
				'The old woman glared at you then hung the smoked pig back on its hook. She shuffled over to the shelf with the bottles and peered short-sightedly at the labels. She finally picked up one of them and brought it over to you.'
				'"Why you not say wanted lice wine." she asked in aggrieved tones, "2000 lubles."'
				if money >= 2000:
					act 'Buy the wine':
						cla
						money -= 2000
						ricewine = 1
						'You hand the woman the money and she gives you the wine.'
						act 'Leave': gt $curloc
					end
				else
					'You are astonished by the price and realize that you cannot afford the wine. You apologize and turn away.'
					act 'Leave the market': gt $curloc
					act 'Continue browsing the market':
						cls
						cla
						gs 'stat'
						minut += 15
						'<center><img src="images/locations/pavlovsk/market/market_boy01.jpg"></center>'
						'You continued to wander through the market for another 15 minutes when the boy from the Chinese stall came up to you.'
						'"Hey, you really want that rice wine?" he asked after checking to make sure no one was watching.'
						'"Yes," you answered him, "But it costs too much. I can''t afford it."'
						'"I''ll make a deal with you." he said, "Go to the porn dealer and buy me a porn magazine and I''ll get you the wine."'
						if pornmarkonce = 0: pornmarkonce = 1 & '"Where is the porn dealer?" you asked and he gives you directions.'
						act 'Agree': porndeal = 1 & gt 'Gpornmark'
						act 'Refuse':
							'You declined his offer and he shrugged and walked away.'
							act 'Leave': gt $curloc
						end
					end
				end
			end
		elseif ShowerIvan = 5 and lookwine ! daystart:
			cla
			cls
			gs 'stat'
			lookwine = daystart
			'<center><img src="images/locations/pavlovsk/market/grinok.jpg"></center>'
			'You look around the market, but the Chinese vendors don''t seem to be here today.'
			act 'Leave': gt $curloc
		end

		if porndeal = 1 and porndealday ! daystart:
			cls 
			cla
			gs 'stat'
			porndealday = daystart
			lookboy = 0
			'<center><img src="images/locations/pavlovsk/market/market_boy01.jpg"></center>'
			'The boy from the Chinese stall comes up to you as you entered the market.'
			'"Do you have it?" he asked eagerly.'
			if haveporn = 1: 
				porndeal = 2 & ! to close the ricewine event
				ricewine = 1
				'"Do you have the wine?" you countered.'
				'He pulls the bottle out of his backpack and you trade items.'
				act 'Leave': gt $curloc
			else
				'You told him you didn''t have his magazine yet but you were working on it.'
				act 'Leave': gt $curloc
			end 
		end
		if porndeal = 1 and porndealday = daystart and lookboy = 0:
			cls
			cla
			gs 'stat'
			lookboy = 1
			'<center><img src="images/locations/pavlovsk/market/chinese_stall01.jpg"></center>'
			'You look around the market for the boy from the Chinese stall, but you can''t find him. You decide to look for him another day.'
			act 'Leave': gt $curloc
		end
	else
		'There is no one in the market.'
	end

	act 'Go back to the station':minut += 3 & gt 'vokzalG'
end

if $ARGS[0] = 'events':
	cls
	cla
	'<center><h4>Market</h4></center>'
	minut += 10
	gs 'stat'

	!modify from 1.2.4(force 50%) and old one, set it 30~80% for events, with pcs_intel /2  by illume
	if (30 + pcs_intel/2) > RAND(0,100):
		rinevrand = rand(1,22)
	else
		! no events
		rinevrand = 0
	end

	if rinevrand <= 1:
		$grinok_text[0]='you notice a beggar.'
		$grinok_text[1]='a beggar asks you for some change.'
		$grinok_text[2]='you see a beggar.'
		$grinok_text[3]='you pass a stall is selling children''s clothes.'
		$grinok_text[4]='you see a woman looking for underwear.'
		$grinok_text[5]='you notice a half-naked girl trying on clothes.'
		$grinok_text[6]='you look away as a woman tries on a bra'
		$grinok_text[7]='you don''t see anything unusual, just the usual vegetables and tasteless clothes.'
		$grinok_text[8]='you just see old women in dressing gowns and slippers. Stupid crappy market.'
		$grinok_text[9]='you don''t find anything interesting'
		$grinok_text[10]='you just see cheap Chinese goods and nothing more.'
		$grinok_text[11]='you see all the usual stuff. People with bags scurrying back and forth, children begging for toys from their parents and vociferous traders.'
		$grinok_text[12]='you have to be careful as its so busy. The hustle and bustle makes it hard to find anything worthwhile.'	
		!will killvar after events
	end	
	
	! def action
	act 'Wander further': gt 'Grinok','events'
	act 'Stop wandering': gt $curloc
	
	if rinevrand = 0:
		'<center><img src="images/locations/pavlovsk/market/brodit'+rand(1,8)+'.jpg"></center>'
		'As you are strolling through the market, ' + $grinok_text[rand(3,12)]
		killvar '$grinok_text'
	elseif rinevrand = 1:
		'<center><img src="images/locations/pavlovsk/market/poproshaika'+rand(1,3)+'.jpg"></center>'
		'You are strolling through the market when ' +  $grinok_text[rand(0,2)]
		killvar '$grinok_text'
		act 'Throw a trifle': 
			cla
			*clr
			money -= rand(3,20)
			pcs_willpwr += 5
			pcs_mood += 5
			gs 'stat'
			'<center><img src="images/locations/pavlovsk/market/podaet.jpg"></center>'
			'You gave the beggar a trifle'
			act 'Wander further': gt 'Grinok','events'
		end
	elseif rinevrand = 2:
		'<center><img src="images/locations/pavlovsk/market/dedgitara.jpg"></center>'
		'You see an old man playing the guitar.'
	elseif rinevrand = 3:
		'<center><img src="images/locations/pavlovsk/market/girlvoyeur.jpg"></center>'
		'You see a girl bending over, putting her pussy on display.'
		if girl > 0: pcs_horny += 10
		gs 'stat'
	elseif rinevrand = 4:
		'<center><img src="images/locations/pavlovsk/market/goodass.jpg"></center>'
		'You see a pretty girl and ogle her beautiful ass.'
	elseif rinevrand = 5:
		'<center><img src="images/locations/pavlovsk/market/dedgitara.jpg"></center>'
		'You see an old man playing the guitar.'
	elseif rinevrand = 6:
		'<center><img src="images/locations/pavlovsk/market/nenavizublyadcigan.jpg"></center>'
		'You meet a Gypsy woman. She offers to tell your fortune.'
		act 'Agree':
			cla
			*clr
			minut += 5
			'<center><img src="images/locations/pavlovsk/market/gadaet'+rand(1,2)+'.jpg"></center>'
			'You agree and Gypsy takes your hand before she predicts you will have great happiness and lots of money. Afterwards, she promptly walks away.'
			act 'Something''s wrong':
				cla
				*clr
				money = 0
				pcs_mood -= 50
				gs 'stat'
				'<center><img src="images/locations/pavlovsk/market/pusto.jpg"></center>'
				'With a sneaking suspicion you start looking through your stuff. Your wallet is nowhere to be found!'
				act 'Find the Gypsy':
					cla
					*clr
					'<center><img src="images/locations/pavlovsk/market/okliknut.jpg"></center>'
					'You search the area hoping to find the thief, but she has already disappeared into the crowd.'
					act 'Back': gt 'Grinok'
				end
			end
		end
	elseif rinevrand = 7:
		'<center><img src="images/locations/pavlovsk/market/kosmetic.jpg"></center>'
		tovpay = rand(3, 8) * 100
		'You see some cosmetics on sale for <<tovpay>> <b>₽</b>.'

		if money >= tovpay:
			act 'Buy':
				cls
				minut += 15
				money -= tovpay
				kosmetica += rand(10, 40)
				gs 'stat'
				gt $curloc
			end
		end
	elseif rinevrand = 8:
		'<center><img src="images/locations/pavlovsk/market/vitaminki.jpg"></center>'
		tovpay = rand(2, 5) * 100
		'You see some vitamins on sale for <<tovpay>> <b>₽</b>.'

		if money >= tovpay:
			act 'Buy':
				cls
				minut += 15
				vitamin += 20
				money -= tovpay
				gt $curloc
			end
		end
	elseif rinevrand = 9:
		'<center><img src="images/locations/pavlovsk/market/condoms.jpg"></center>'
		tovpay = rand(2, 8) * 10
		'You see a booth selling condoms for <<tovpay>> <b>₽</b>.'

		if money >= tovpay:
			act 'Buy':
				cls
				minut += 15
				prezik += 5
				i = 5
				:cndmlp
					if rand(0,49-6*(8-tovpay/10)) = 0:badprezik += 1
				if i > 0: i-= 1 & jump 'cndmlp'
				killvar 'i'
				money -= tovpay
				gt $curloc
			end
		end
	elseif rinevrand = 10:
		'<center><img src="images/locations/pavlovsk/market/balzam.jpg"></center>'
		tovpay = rand(3, 9) * 10
		'There is lip balm on sale for <<tovpay>> <b>₽</b>.'

		if money >= tovpay:
			act 'Buy':
				cls
				minut += 15
				lipbalm += 20
				money -= tovpay
				gt $curloc
			end
		end
	elseif rinevrand = 11:
		if pornmarkonce = 0:pornmarkonce = 1
		'Strolling through the market you accidentally wander across a stall selling porn.'

		act 'View the stall sell the porn':gt 'Gpornmark'
	elseif rinevrand = 12:
		if arturKnow = 0:
			'Someone suddenly calls out to you, "Hey pretty girl, do you want a fresh, juicy tangerine?"'
			act 'Look': gt 'arturRinok','start'
		else
			'Someone suddenly calls out to you, "Hey <<$pcs_nickname>>, do you want a fresh, juicy tangerine?"'
			act 'Come': gt 'arturRinok','start'
		end
	elseif rinevrand = 13:
		'<center><img src="images/locations/pavlovsk/market/cheburek.jpg"></center>'
		'Walking through the market, you came across a grandmother selling pies.'
		if pcs_energy <= 80:
			act 'Buy a pie for 20 <b>₽</b>':
				cla
				*clr
				fat+=3
				frost = 0
				pcs_health += 5
				pcs_mood += 5
				water -= 5
				pcs_energy+=5
				money-=20
				minut+=10
				gs 'stat'
				'<center><img src="images/locations/pavlovsk/market/eat.jpg"></center>'
				'You ate a delicious pie with pleasure.'
				act 'Continue walking through the market': gt 'Grinok','events'
			end
		else
			*p ', But you are not hungry right now.'
		end
	elseif rinevrand = 14:
		'<center><img src="images/locations/pavlovsk/market/dropwallet.jpg"></center>'
		'You see woman ahead of you drop her purse.'
		cla
		act 'Tell her':
			cla
			*clr
			minut += 2
			'<center><img src="images/locations/pavlovsk/market/vernutwallet.jpg"></center>'
			'You call to the woman and hold out her wallet. She looks at you and releasing she gratefully takes her wallet back and walks away.'
			act 'Continue walking through the market': gt 'Grinok','events'
		end
		act 'Look through the purse':
			cla
			*clr
			minut += 5
			i = rand(40,200)
			'<center><img src="images/locations/pavlovsk/market/zaglanut.jpg"></center>'
			'In the purse is <<i>> <b>₽</b>'
			act 'Take the money and throw away the wallet':
				cla
				*clr
				money += i
				pcs_mood += 10
				gt $curloc
			end
			act 'Catch up with the woman and return her purse':
				cla
				*clr
				pcs_mood += 5
				'<center><img src="images/locations/pavlovsk/market/proverka.jpg"></center>'
				'You catch up with the woman and telling her she dropped something you hand her the purse. The woman checks her money is still there and seeing that everything is in place, she thanks you and walked away.'
				act 'Continue walking through the market': gt 'Grinok','events'
			end
		end
	elseif rinevrand = 15:
		'<center><img src="images/locations/pavlovsk/market/rebenok.jpg"></center>'
		'You see a young mother and her crying baby.'
	elseif rinevrand = 16:
		'<center><img src="images/locations/pavlovsk/market/rezba.jpg"></center>'
		'You find a stall with beautiful things carved out of wood and metal.'
	elseif rinevrand = 17:
		'<center><img src="images/locations/pavlovsk/market/gruzchik.jpg"></center>'
		'Gazing around, he almost collided with a loader.'
	elseif rinevrand = 18:
		'<center><img src="images/locations/pavlovsk/market/meatlavka.jpg"></center>'
		'You wandered past a the meet seller and see a butcher working with an ax.'
	elseif rinevrand = 19:
		if pcs_apprnc >= 30:
			'<center><img src="images/qwest/alter/grinok/walk'+rand(1,2)+'.jpg"></center>'
			'As you walk, you make an effort to shake your booty. Some guys stare at you, unable to avert their eyes.'
			pcs_mood += 5
		else
			'<center><img src="images/qwest/alter/grinok/walk'+rand(3,4)+'.jpg"></center>'
			'As you walk, you make an effort to shake your booty. But no one gives you slightest attention.'
			pcs_mood -= 5
		end
		gs 'stat'
	elseif rinevrand = 20:
		'<center><img src="images/locations/pavlovsk/market/dirka.jpg"></center>'
		'As you walk past a stall you notice a small hole torn in the canvas.'
		act 'Look through the hole':
			cla
			*clr
			if girl > 1: pcs_horny += 5
			'<center><img src="images/locations/pavlovsk/market/pereodev'+rand(1,15)+'.jpg"></center>'
			'You look inside the tent and see ' + $grinokrandtext[rand(3,7)]
			act 'Leave': gt 'Grinok'
		end
	elseif rinevrand = 21:
		'<center><img src="images/locations/pavlovsk/market/birthcontrolpills.jpg"></center>'
		tovpay = rand(2,8) * 50
		'You see a booth selling birth control pills for <<tovpay>> <b>₽</b>.'

		if money >= tovpay:
			act 'Buy':
				cls
				minut += 15
				tabletki += 1
				if rand(0,49) - (6*(8-tovpay/50)) <= 0: 
					temp = rand(0,3)
					if temp = 0: 
						tabletkiold += 1 & !Pills are expired and less effective
					elseif temp = 1:
						tabletkifake += 1 & !Pills are counterfeit and useless
					elseif temp = 2: 
						tabletkirej += 1 & !Pills are factory rejects with variable strength
					else
						!Pills were tampered with
						if rand(0,100) = 0:
							tabletkifert += 1 & ! pills were replaced with fertility hormones
						else
							tabletkisug += 1 & ! pills were all replaced with sugar pills
						end
					end
				end
				money -= tovpay
				gt 'Grinok'
			end
		end
	else
		tovpay = rand(1, 5) * 1000
		'You see someone selling a handbag for <<tovpay>> <b>₽</b>.'

		if bag = 0:
			if money >= tovpay:
				act 'Buy':
					cls
					minut += 15
					bag = 1
					money -= tovpay
					if menu_option = 1:dynamic $menu_addoldmenu
					if menu_option = 0:gs 'obj_din', 'new'
					gt 'Grinok'
				end
			end
		else
			'But you already have a bag, so you move on.'
		end
	end	
end

--- Grinok ---------------------------------

