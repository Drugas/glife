# Grinok
!{change log
March 31, 2016
Added rice wine quest
April 20, 2016
Fixed Rice wine event
}
$metka = $ARGS[0]
$loc = $CURLOC
CLOSE ALL
if sound = 0:
	if month >=11 and month <= 12 or month >=1 and month <=3:
		if hour >= 8 and hour <= 23: PLAY 'sound/rynok.mp3',30 & PLAY 'sound/zima.mp3',30
		if hour >= 0 and hour <= 7: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/zima.mp3',30
	else
		if hour >= 8 and hour <= 23: PLAY 'sound/rynok.mp3',30 & PLAY 'sound/street_leto.mp3',30
		if hour >= 0 and hour <= 7:PLAY 'sound/street_night.mp3',30
	end
end

clr
gs 'stat'
gs 'time'
'<center><h4>Market</h4></center>'
'<center><img src="images/pic/Grinok.jpg"></center>'
'This small market is location near the station.'

if arturQW = 1 and (week = 6 or week = 7) and hour < 12:
	act 'Wait for Arthur to arrive':hour = 12 & gt 'grinok'
end

if hour >= 8 and hour <= 18:
	'In the market the sellers of goods are beckoning shoppers.'
	if gorslut > 2 and rand(0,9) = 0:
		'An Armenian man comes up to you and in broken Russian says, "You girl who like men, no? Come meet real men have good time."'
		act 'Go with him':gt 'gevent', '3'
		act 'Reject him':
			'You pretend not to understand what he is saying.'
			act 'Return':gt 'Grinok'
		end
		
		exit
	end
	act 'Wander through the market':
		cls
		minut += 15
		gs 'stat'
		'You are strolling through the market looking for good deals.'
		if intel >= 100:
			rinmaxrand = 2
		elseif intel >= 75 and intel < 100:
			rinmaxrand = 4
		elseif intel >= 50 and intel < 75:
			rinmaxrand = 6
		elseif intel >= 25 and intel < 50:
			rinmaxrand = 8
		else
			rinmaxrand = 10
		end

		rinevrand = RAND(0,rinmaxrand)
		if rinevrand = 0:

			if arturKnow = 0:
				temp = rand(0, 6)
			else
				temp = rand(0, 5)
			end

			if temp = 0:
				tovpay = rand(3, 8) * 100
				'You see that cosmetics are for sale for <<tovpay>> rubles.'

				if money >= tovpay:
					act 'Buy':
						cls
						minut += 15
						money -= tovpay
						kosmetica += rand(10, 40)
						gs 'stat'
						gt $curloc
					end
				end
			elseif temp = 1:
				tovpay = rand(2, 5) * 100
				'You see vitamins for sale for <<tovpay>> rubles.'

				if money >= tovpay:
					act 'Buy':
						cls
						minut += 15
						vitamin += 20
						money -= tovpay
						gt $curloc
					end
				end
			elseif temp = 2:
				tovpay = rand(2, 8) * 10
				'You see a booth selling condoms for <<tovpay>> rubles.'

				if money >= tovpay:
					act 'Buy':
						cls
						minut += 15
						prezik += 5
						money -= tovpay
						gt $curloc
					end
				end
			elseif temp = 3:
				tovpay = rand(5, 10) * 100
				'There is lip balm for sale for <<tovpay>> rubles.'

				if money >= tovpay:
					act 'Buy':
						cls
						minut += 15
						lipbalm += 20
						money -= tovpay
						gt $curloc
					end
				end
			elseif temp = 4:
				tovpay = rand(1, 5) * 1000
				'You see someone selling a handbag for <<tovpay>> rubles.'

				if bag = 0:
					if money >= tovpay:
						act 'Buy':
							cls
							minut += 15
							bag = 1
							money -= tovpay
							gt $curloc
						end
					end
				else
					'But you already have a bag, so you move on.'
				end
			elseif temp = 5:
				if pornmarkonce = 0:pornmarkonce = 1
				'Strolling through the market you accidentally wander into the far corner where they sell porn.'

				act 'Go to the area with the porn':gt 'Gpornmark'

			else
				'Someone suddenly called out to you, "Hey pretty girl, do you want a fresh, juicy tangerine?"'

				act 'Look':gt 'arturRinok', 'start'
			end

		else
			'Nothing unusual you do not come across, trash, buckets, granny gowns, sneakers, breeches and waistcoats with pockets.'
		end
		
		act 'Move on':gt $curloc
	end


	if pornmarkonce = 1:
		act 'Go to the area with the porn':gt 'Gpornmark'
	end

	if arturKnow = 1 and arturQW = 0:
		act 'Go to Arthur':gt 'arturRinok', 'start'
	elseif arturQW = 1 and (week = 6 or week = 7):
		if hour = 12 or hour = 13:
			act 'Go to Arthur':gt 'arturRinok', 'work_1'
		end
	elseif arturQW = 2:
		if hour = 16 and minut <= 30:
			act 'Go to Arthur':gt 'arturRinok', 'work_2'
		elseif hour < 16:
			act 'Wait for Arthur to arrive':hour = 16 & minut = 0 & gt 'Grinok'
		end
	end

	act 'Go to the book seller':
		cls
		minut += 15
		gs 'stat'
		'On the shelves are very entertaining fantasy books, which costs only 150 rubles each and there are also women''s romance paperback novels for 300 rubles each.'

		if money >= 150:
			act 'Buy a fantasy book (150 rubles)':
				cls
				minut += 15
				money -= 150
				f_book += 1
				gs 'stat'
				'You bought a fantasy book for 150 rubles.'

				act 'Leave':gt $curloc
			end
		end

		if money >= 300:
			act 'Buy a romance novel (300 rubles)':
				cls
				minut += 15
				money -= 300
				w_book += 1
				gs 'stat'
				'You bought a romance novel for 300 rubles.'

				act 'Leave':gt $curloc
			end
		end

		act 'Escape':gt $curloc
	end

	
	if gobelen > 0 and gobQW = 0:
		act'Sell ​​tapestry':
			cls
			gobQW = 1
			minut += 15
			gs'stat'
			'You stood quietly by the entrance to the market, waiting for customers and displaying your tapestries. Shortly buyers started to gather, but a large, muscular man approached you and said, "It is illegal to trade in the market without a trader''s licence, but I may be interesting in buying your tapestries, 500 rubles apiece. Come and see me at my stall."'
			'Not wanting to get into trouble, you packed your stuff away. The man was already gone, but you saw which direction he went and he should be easy to find.'
			act 'Escape':gt $curloc
		end
	end

	if gobelen > 0 and gobQW >= 1:
		act'Sell ​​tapestry':
			cls
			minut += 15
			gs'stat'
			'You went to the man who offered to buy your tapestries for 500 rubles. You found him by his stall, smiling.'

			if gobQW = 2:
				'Roman welcomed you. Interested and businesslike, he asked what you have for sale.'
	        end

	        if gobQW = 1:
				gobQW = 2
				minute + 5
				'The man greeted you and introduced himself as Roman, you told him that your name is <<$name>>. After a brief chat Roman said that he is interested in seeing what you have for sale.'
	        end

			act'Show tapestry':
				cls
				gobelen -= 1
				money += 500
				gs'stat'
				'You show your tapestry to Roman and he gives you five hundred for it. You take the money and give him the tapestry.'
				act 'Escape':gt $curloc
			end

			if gobelen > 1:
				act'Sell ​​all tapestries':
					cls
					kolgob = gobelen
					gobelen = 0
					moneyGob = kolgob*500
					money += moneyGob
					gs'stat'
					'You show Roman your tapestries and he gives you <<moneyGob>> rubles for them. You take the money and give him the tapestries.'
					act 'Escape':gt $curloc
				end
			end

			act 'Leave':gt $curloc
		end
	end
	if ShowerIvan = 5 and porndeal = 0 and rand(1,100) <= 10 and lookwine ! daystart:
		cls
		cla
		gs 'stat'
		minut += 20
		'<center><img src="images/locations/Pavlovsk/Market/chinese_stall01.jpg"></center>'
		'You were looking through the market when you came upon a stall selling Chinese foods. On a shelf behind the old woman minding the stall there are a number of bottles.'
		act 'Ask about huangjiu':
			'You asked the old woman if she has any rice wine to sell. She smiles at you and points to a basket filled with rice.'
			'"Onry 2 kopec, hundred glam." she said in badly accented Russian, "You buy?"'
			'"No," you replied, "I need rice wine, rice wine."'
			'She looked at you in confusion for a moment then suddenly brightened, "Ah!" she exclaimed, "Come, come!"'
			'The old woman hurried to the other side of the stall and you followed her to where a number of smoked piglets hung in a row.'
			'"Here," the old woman crowed handing you one of r the pigs, "Nice swine, vely nice swine."'
			'"No, not nice swine," you cried in dispair, "Rice. Wine."'
			'"Yes, yes," the woman countered, "Nice swine, only 5 lubles."'
			'Just as you were about to give up, a young Chinese boy about your brother''s age arrived.'
			'"I am sorry, my grandmother does not speak much Russian." he said as he stepped up beside the old woman who started to speak rapidly to him in what you assumed was Chinese.'
			'"My grandmother asks why you don''t like the piglets." he translated as the old woman stopped speaking.'
			'"I don''t want a pig," you replied in relief as his Russian is quite good, "I am looking for rice wine, wang-goo."'
			'"Ah, huangjiu!" he said in understanding then spoke to his grandmother in Chinese.'
			'The old woman glared at you then hung the smoked pig back on its hook. She shuffled over to the shelf with the bottles and peered short-sightedly at the labels. She finally picked up one of them and brought it over to you.'
			'"Why you not say wanted lice wine." she asked in aggrieved tones, "2000 lubles."'
			if money >= 2000:
				act 'Buy the wine':
					money -= 2000
					ricewine = 1
					'You hand the woman the money and she gives you the wine.'
					act 'Leave': gt $curloc
				end
			else
				'You are astonished by the price and realize that you cannot afford the wine. You apologize and turn away.'
				act 'Leave the market': gt $curloc
				act 'Continue browsing the market':
					cls
					cla
					gs 'stat'
					minut += 15
					'<center><img src="images/locations/Pavlovsk/Market/market_boy01.jpg"></center>'
					'You continued to wander through the market for another 15 minutes when the boy from the Chinese stall came up to you.'
					'"Hey, you really want that rice wine?" he asked after checking to make sure no one was watching.'
					'"Yes," you answered him, "But it costs too much. I can''t afford it."'
					'"I''ll make a deal with you." he said, "Go to the porn dealer and buy me a porn magazine and I''ll get you the wine."'
					if pornmarkonce = 0: pornmarkonce = 1 & '"Where is the porn dealer?" you asked and he gives you directions.'
					act 'Agree': porndeal = 1 & gt 'Gpornmark'
					act 'Refuse':
						'You declined his offer and he shrugged and walked away.'
						act 'Leave': gt $curloc
					end
				end
			end
		end
	elseif lookwine ! daystart:
		cla
		cls
		gs 'stat'
		lookwine = daystart
		'<center><img src="images/pic/Grinok.jpg"></center>'
		'You look around the market, but the Chinese vendors don't seem to be here today.'
		act 'Leave': gt $curloc
	end

	if porndeal = 1 and porndealday ! daystart:
		cls 
		cla
		gs 'stat'
		porndealday = daystart
		lookboy = 0
		'<center><img src="images/locations/Pavlovsk/Market/market_boy01.jpg"></center>'
		'The boy from the Chinese stall comes up to you as you entered the market.'
		'"Do you have it?" he asked eagerly.'
		if haveporn = 1: 
			porndeal = 2 & ! to close the ricewine event
			ricewine = 1
			'"Do you have the wine?" you countered.'
			'He pulls the bottle out of his backpack and you trade items.'
			act 'Leave': gt $curloc
		else
			'You told him you didn''t have his magazine yet but you were working on it.'
			act 'Leave': gt $curloc
		end 
	end
	if porndeal = 1 and porndealday = daystart and lookboy = 0:
		cls
		cla
		gs 'stat'
		lookboy = 1
		'<center><img src="images/locations/Pavlovsk/Market/chinese_stall01.jpg"></center>'
		'You look around the market for the boy from the Chinese stall, but you can't find him. You decide to look for him another day.'
	end
else
	'There is no one in the market.'
end

act 'Go back to the station':minut += 3 & gt 'vokzalG'

--- Grinok ---------------------------------

