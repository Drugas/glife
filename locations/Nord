# Nord
$metka = ''
$loc = 'Nord'
$metkaM = ''
$locM = 'Nord'
$metkaStreet = ''
$locStreet = 'Nord'
$location_type = 'public_outdoors'
CLOSE ALL
if sound = 0:
	if month >=11 and month <= 12 or month >=1 and month <=3:
		if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/street7.mp3',30 & PLAY 'sound/zima.mp3',30
		if hour >= 0 and hour <= 7: PLAY 'sound/street_night.mp3',30 & PLAY 'sound/zima.mp3',30
	else
		if hour >= 8 and hour <= 23: PLAY 'sound/street2.mp3',30 & PLAY 'sound/street7.mp3',30 & PLAY 'sound/street_leto.mp3',30
		if hour >= 0 and hour <= 7: PLAY 'sound/street_night_leto.mp3' & PLAY 'sound/street_night.mp3',30
	end
end

gs 'dinam'
clr
gs 'stat'

'<center><b>Northern region</b></center>'
'<center><img src="images/etogame/nord.jpg"></center>'
!call random events from [street_events], and display them before show location.
gs'street_event','Nord' & if streetrand=-1:exit

act 'Walk to the main road to Pavlovsk (15 min)':minut += 15 & nroad = 0 & gt'road'
act 'Wait':
	cla
	waiting = input ("How long would you like to wait? (Not more than 120 minutes)")

	if waiting <= 0 or waiting > 120:
		minut += 15
	elseif waiting > 0 and waiting <= 120:
		minut += waiting
	end

	act 'Take a look at the clock':gt curloc
end

gs 'taxi'

if car > 0:
	if cardrive = 3 or cardrive >= 6 and cardrive <= 9:'In the parking stands your <a href="exec:GS ''carF'', ''start''"><<$car>></a>.'
end

'The towns <a href="exec:hour += 1 & gt ''park'', ''start''">central park</a> is an hours walk from here.'

if hour >= 8 and hour <= 22:'You can see the <a href="exec:$locM = ''Nord'' & gt ''kiosk'', ''start''">kiosk</a> selling cigarettes and magazines.'

'<a href="exec:minut += 15 & gt ''vokzal''">Train Station</a> to get to the small Town.'

if slavaQW >= 1:'<a href="exec:gt ''qwBarPolet''">Bar "Flight"</a>.'

litltown = 0
'There is a <a href="exec:gt ''zaprF'', ''start''">Gas</a> Station near the street'

if hour >= 5 and hour <= 23:'The <a href="exec:gt ''metro'', ''start''">Metro</a> Station is within a 5 minute walk from here.'
if hour >= 9 and hour <= 17 and week > 1:'<a href="exec:gt ''autosalonF'', ''start''">Car Dealership</a>'
if hour >= 9 and hour <= 20 and week < 7:'<a href="exec:gt ''autoservisF'', ''start''">Repair Shop</a>'
if hour >= 9 and hour <= 17 and week > 1:'Used <a href="exec:gt ''autotraidF'', ''start''">Car Dealership</a>'
if hour >= 9 and hour <= 17 and week < 6:'<a href="exec:gt ''dk'', ''start''">Community centre</a>'
if hour >= 9 and hour <= 17 and week < 6 and bumtolik > 3:'<a href="exec:gt ''buklinik'', ''start''">Mercy clinic</a>'
if hour >= 8 and hour <= 20:'The parking lot, which is engulfed in bright lights, belongs to a <a href="exec:gt ''shop'', ''start''">Supermarket</a>'
if hour >= 12 and hour <= 19:'Above the door to a basement room is a bright neon sign for the <a href="exec:gt ''pirsingsalon'', ''start''">Tattoo & Piercing parlor</a>'
if tobiQW >= 1:'The old mansion, home to <a href="exec:gt ''kakuzu'', ''start''">Kakuzu</a>'

'There is a huge building, with bright lights, which houses a <a href="exec:gt ''casino''">Casino</a> and a hotel.'

if hour >= 14 or hour < 4:'Located far away from the buildings <a href="exec:gt ''lakecafe'', ''main''">barbecue</a>.'
if jouryResult = 10:'You can go to one of the apartment blocks and visit <a href="exec:gt ''youry'',''start''">Yuri''s</a> flat'
if IvanKvartira > 0:'In one of these building is <a href="exec:gt ''ivanRoom''">Manager Ivans</a> apartment'

'A long and high fence, which is build around the <a href="exec:gt ''Terminal''">storage terminal</a>'


--- Nord ---------------------------------

