# gadforest

!MiraLoc tells us where Mira is Located
!----------------------------------------------
!MiraLoc = 0 (Mira at home)
!MiraLoc = 1 (Mira at river)
!MiraLoc = 2 (Mira at meadow)
!MiraLoc = 3 (Mira at forest)
!MiraLoc = 4 (Mira at field)
!MiraLoc = 5 (Mira with gop)
!---------------------------------------------
	
if $ARGS[0] = '1':
	! figure out if Sveta came from Mira code or not. events play out differently whether with Mira or not
	PC_arrive = iif($loc ! 'Miroslava', 1 , 0)

	! edge of forest
	$loc = 'gadforest'	
	$metka = '1'
	$location_type = 'secluded'	
	$menu_loc = 'gadforest'
	$menu_arg = '1'
	menu_off = 0
	clothesAtLocation = FUNC('lost_clothes_here',$CURLOC)
	gs 'gadukino_event', 'sound'
	gs'stat'
	
	*clr & cla
	
	! Add to knowledge of forest
	if goforest < 20:goforest += rand(0,1)
	! chance to get lost
	lostrand = rand(1,20)
	! Determine where Sveta got lost in the forest
	lost = 1
	! chance to rip clothes
	riprand = rand(1,10)
	foresteventrand = rand(1,20)
	new_boletus = 0
	new_bilberry = 0

	'<center><H4>Edge of the Forest</H4></center>'
	if month >= 4 and month <= 10:
		if hour >= 6 and hour < 22:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest1.jpg"></center>'
			'Your are at the edge of the forest. Mushrooms and berries grow here and you can often find other people picking them.'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_night1.jpg"></center>'
			'You are at the edge of the forest. Night in the woods is scary and uncomfortable.'
		end
	else
		if hour >= 8 and hour < 18:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_winter1.jpg"></center>'
			'You are at the edge of the forest. The forest in winter is very beautiful.'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_winter_night1.jpg"></center>'
			'You are at the edge of the forest. Night in the forest during wintertime is very scary and uncomfortable.'
		end
	end
	
	if forestpicnic = 1:'On the edge of the forest you see people. Apparently they are having a family <a href="exec:gt ''gadforest_event'',''picnic''">picnic</a>.'
	if MiraLoc = 3:'Nearby is <a href="exec: gt ''Miroslava'' ">Mira</a>.'
	if clothesAtLocation = 1:'Your clothes are nearby on the ground.'
	if findvel = 2:'Leaning up against a tree is your old <a href="exec:gt ''bicycle'',''forest''">bike</a>.'
	if goforest < 20:'You don''t know this part of the forest very well, it''s best if you don''t go any deeper into the woods.'
	if riprand >= 10: gs 'gadforest', 'torncloth'
	
	! Sveta arrive without Mira events
	if PC_arrive = 1:
		! Mira nude event can be done alone as well
		if temper >= 10 and hour >= 6 and hour <= 20 and miratalkforest = 1 and miraforestday ! daystart and MiraLoc ! 3:act 'Try walking naked in the forest': gt 'gadforest_event', 'nude_event'
		if temper >= 10 and hour >= 6 and hour <= 20 and miratalkforest = 1 and miraforestday ! daystart and MiraLoc = 3:act 'Mira suggests walking naked in the forest': gt 'gadforest_event', 'nude_event'
		! Mushroom and berry events
		if foresteventrand <= 2 and month >= 6 and month <= 9 and mushroom_pickersday ! daystart:mushroom_pickers = 1 & gt'gadforest_event', 'mushroom_pickers1'
		if foresteventrand <= 4 and month >= 6 and month <= 9 and mushroom_pickersday ! daystart: gt 'gadforest_event', 'mushroom_pickers2'
		! Picnic event
		if foresteventrand = 5 and month >= 5 and month <= 9 and temper >= 30 and sunWeather = 1 and week > 5 and forestpicnic = 0: forestpicnic = 1 & gt 'gadforest_event', 'picnic'
		! Mitka, Kolyamba, Vasyan events
		if hour > 8 and hour < 20 and month >= 4 and month <= 10:
			if foresteventrand <= 8:
				if mitkaday ! daystart: 
					gt 'gadforest_event', 'gopforest_mitka'
				elseif kolyambaday ! daystart:
					gt 'gadforest_event', 'gopforest_kolyamba'
				elseif vasyanday ! daystart:
					gt 'gadforest_event', 'gopforest_vasyan'
				end
			elseif foresteventrand <=9:
				if mitkaday ! daystart and kolyambaday ! daystart:
					gt 'gadforest_event', 'gopforest_2boys', '4'
				elseif mitkaday ! daystart and vasyanday ! daystart:
					gt 'gadforest_event', 'gopforest_2boys', '5'
				elseif kolyambaday ! daystart and vasyanday ! daystart:
					gt 'gadforest_event', 'gopforest_2boys', '6'
				elseif mitkaday ! daystart:
					gt 'gadforest_event', 'gopforest_mitka'
				elseif kolyambaday ! daystart:
					gt 'gadforest_event', 'gopforest_kolyamba'
				elseif vasyanday ! daystart:
					gt 'gadforest_event', 'gopforest_vasyan'
				end
			elseif foresteventrand <= 10:
				if mitkaday ! daystart and kolyambaday ! daystart and vasyanday ! daystart:
					gt 'gadforest_event', 'gopforest_3boys'
				elseif mitkaday ! daystart and kolyambaday ! daystart:
					gt 'gadforest_event', 'gopforest_2boys', '4'
				elseif mitkaday ! daystart and vasyanday ! daystart:
					gt 'gadforest_event', 'gopforest_2boys', '5'
				elseif kolyambaday ! daystart and vasyanday ! daystart:
					gt 'gadforest_event', 'gopforest_2boys', '6'
				elseif mitkaday ! daystart:
					gt 'gadforest_event', 'gopforest_mitka'
				elseif kolyambaday ! daystart:
					gt 'gadforest_event', 'gopforest_kolyamba'
				elseif vasyanday ! daystart:
					gt 'gadforest_event', 'gopforest_vasyan'
				end
			end
		end
	end

	! can only be naked if successfully complete Mira gadforest_event, nude_event
	if $clothingworntype ! 'nude' and clothesAtLocation = 0 and temper >= 10 and hour >= 6 and hour <= 20 and miratalkforest > 1 and MiraLoc ! 3 and (pcs_inhib > 50 or exhibitionist_lvl > 1):
		act'Strip naked':
			if forestpicnic = 0:
				cla
				*clr
				if pcs_inhib < 50:inhib_exp += rand(1,3)
				minut += 5
				if CloSkirtShortness = 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/nudeforest.jpg"></center>'
				if CloSkirtShortness > 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/nudeforest1.'+rand(1,2)+'.jpg"></center>'
				gs 'underwear', 'remove'
				gs 'clothing', 'strip', 'gadforest'
				gs 'exhibitionism', 3
				gs 'stat'
				
				'You don''t hesitate and quickly get undressed, your clothes are left laying in a pile.'

				act'Continue':gt'gadforest','1'
			else
				cla
				*clr
				'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/noundress.jpg"></center>'
				'You see a number of people nearby and decide not to undress right now.'

				act'Continue':gt'gadforest','1'
			end
		end
	end
	
	if clothesAtLocation = 1 and $clothingworntype = 'nude':
		act 'Get dressed':
			cla
			*clr
			minut += 5
			gs 'clothing', 'swamp_clothes', 0
			gs 'underwear', 'wear'
			gs 'clothing', 'recover_lost_clothes', 'gadforest', 1
			gs 'stat'
			
			gt 'gadforest','1'
		end
	end
	
	! if Sveta left the forest without retrieving her clothes
	if clothesAtLocation = 1 and $clothingworntype ! 'nude':
		if $clothingworntype = 'cheap' and clothingwornnumber = 414:
			act 'Pick up the abandoned clothing':
				cla
				minut += 2
				gs 'clothing', 'swamp_clothes', 0
				gs 'underwear', 'wear'
				gs 'clothing', 'recover_lost_clothes', 'gadforest', 1
				gs 'stat'
				*nl
				'You pick up your abandoned clothing and change out of the hessian sack the hunters gave you.'
				gt 'gadforest','1'
			end
		else
			act 'Pick up the abandoned clothing':
				cla
				minut += 5
				gs 'clothing', 'swamp_clothes', 0
				gs 'underwear', 'wear'
				gs 'clothing', 'recover_lost_clothes', 'gadforest'
				gs 'stat'
				*nl
				'You pick up your abandoned clothing.'
				gt 'gadforest','1'
			end
		end
	end
	
	if hour >= 6 and hour <= 20:
		act 'Go for a walk in the woods':
			cla
			*clr
			minut += 60
			pcs_mood += 20
			pcs_willpwr += willpowermax/5
			if $clothingworntype = 'nude':
				pcs_health += healthmax/5 & pcs_horny += 5
				inhib_exp += rand(1,5)
			end
			gs 'stat'

			if month >= 4 and month <= 10:
				if MiraLoc ! 3:
					if ($clothingworntype ! 'nude' and $clothingworntype ! 'swimwear') and CloSkirtShortness = 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gulforest0.'+rand(1,2)+'.jpg"></center>'
					if ($clothingworntype ! 'nude' and $clothingworntype ! 'swimwear') and $pantyworntype ! 'none' and CloSkirtShortness > 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gulforest1.'+rand(1,3)+'.jpg"></center>'
					if ($clothingworntype ! 'nude' and $clothingworntype ! 'swimwear') and $pantyworntype = 'none' and CloSkirtShortness > 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gulforest_tanga0.'+rand(1,2)+'.jpg"></center>'
					if $clothingworntype = 'nude':'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gulforest_nude0.'+rand(1,4)+'.jpg"></center>'
					'You are walk through the woods, enjoying the sounds of the forest and the fresh air.'
				else
					if $clothingworntype ! 'nude':'<center><img <<$set_imgh>> src="images/characters/village/mira/miraforest'+rand(1,3)+'.jpg"></center>'
					if $clothingworntype = 'nude':'<center><img <<$set_imgh>> src="images/characters/village/mira/miraforest_nude'+rand(1,3)+'.jpg"></center>'
					'You walk with Mira through the woods, enjoying the sounds of the forest and the fresh air.'
				end
			else
				if MiraLoc ! 3:
					'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gulforestwinter0.'+rand(1,2)+'.jpg"></center>'
					'You walk through the snowy woods, enjoying the sounds of the forest and the fresh air.'
				else
					'<center><img <<$set_imgh>> src="images/characters/village/mira/miraforestwinter.jpg"></center>'
					'You walk with Mira through the snowy woods, enjoying the sounds of the forest and the fresh air.'
				end
			end
			
			act 'Continue':gt 'gadforest','1'
		end
	end
	
	if hour >= 6 and hour <= 20 and month >= 6 and month <= 9 and boletus + bilberry < 10 and edgeforestday ! daystart:
		act 'Look for mushrooms and berries':
			cla
			*clr
			minut += 30
			edgeforestday = daystart
			gs 'stat'
			
			! no mushroom pickers or mushroom pickers moved deeper into the forest
			if mushroom_pickers = 0 or mushroom_pickers = 2:
				new_boletus += rand(1,2)
				new_bilberry += rand(1,2)
				boletus += new_boletus
				bilberry += new_bilberry
				pcs_mood += 10
				gs 'stat'

				gs 'gadforest', 'clothes1'
				
				if new_boletus > 0 and new_bilberry > 0:'After searching for mushrooms and berries for half an hour you found <<new_boletus>> kg of mushrooms and <<new_bilberry>> kg of berries.'
			! mushroom pickers are at the edge of the forest
			elseif mushroom_pickers = 1:
				temp_gf = rand(1,2)
				if temp_gf = 1:
					new_boletus += rand(0,1)
					new_bilberry += rand(0,1)
					boletus += new_boletus
					bilberry += new_bilberry
					if new_boletus > 0 or new_bilberry > 0:pcs_mood += 5
					if new_boletus = 0 and new_bilberry = 0: pcs_mood -= 5
					gs 'stat'

					if new_boletus > 0 or new_bilberry > 0: gs 'gadforest', 'clothes1'
					if new_boletus and new_bilberry = 0: gs 'gadforest', 'clothes2'
					
					if new_boletus > 0 and new_bilberry = 0:'After searching for mushrooms and berries for half an hour you found <<new_boletus>> kg of mushrooms.'
					if new_boletus = 0 and new_bilberry > 0:'After searching for mushrooms and berries for half an hour you found <<new_bilberry>> kg of berries.'
					if new_boletus > 0 and new_bilberry > 0:'After searching for mushrooms and berries for half an hour you found <<new_boletus>> kg of mushrooms and <<new_bilberry>> kg of berries.'
					if new_boletus = 0 and new_bilberry = 0:'After searching for mushrooms and berries for half an hour you found nothing.'
				elseif temp_gf = 2:
					pcs_mood -= 5
					gs 'stat'

					gs 'gadforest', 'clothes2'
					
					'After searching for mushrooms and berries for half an hour you found nothing.'
				end
			end
			act 'Continue':gt 'gadforest','1'
		end
 	end
 	
	if hour >= 6 and hour <= 20 and month >= 6 and month <= 9 and MiraLoc ! 3:
		act 'Go further into the forest':
			if goforest >= lostrand:minut += 30 & gt 'gadforest','2'
			if goforest < lostrand:gt 'gadforest', 'gadforestloststart'
	 	end
	end
	
	if goswamp = 1 and MiraLoc ! 3:
		! day time chance to get stuck in swamp. ps. use bike to get there 100% of the time by illume
		if hour >= 6 and hour < 20:
			act 'Go to the swamp':
				minut += 75
				if goforest >= lostrand:
					gt 'swamp'
				else
					gt 'gadforestswamp','1'
				end
			end
		! night time chased by wolves
		else
			act 'Go to the swamp':gt 'gadforest_event', 'swamp_road'
		end
	end
	
	act 'Leave the woods':
		dirtyclothes = 0
		gs 'clothing', 'swamp_clothes', 0
		gs 'stat'
		if $clothingworntype = 'nude':
			if $CloLosTyp[$CURLOC] = '' or gadforest_clothestorn = 1:
				! no clothes here, they must be ripped apart enough times in the woods or swamp to get destroyed.
				! This may be a good exhibition or rape event entry point
				inhib_exp += rand(1,3)
				gadforest_clothestorn = 0
				'You sneak out the woods, hiding your naked body in the shadows and moving toward grandparents house.'
				'You successfully reach home without anyone noticing.'
				cla
				act 'Go home':gt'Gadhouse'
			else
				'<B>You should get dressed before leaving the woods.</B>'
			end
		else
			minut += 30 & gt'gadroad'
		end
	end
end

if $ARGS[0] = '2':
	! slightly inside forest
	$loc = 'gadforest'	
	$metka = '2'
	$location_type = 'secluded'
	$menu_loc = 'gadforest'
	$menu_arg = '2'
	menu_off = 0
	gs 'gadukino_event', 'sound'
	gs'stat'
	
	*clr & cla
	
	! Add to knowledge of forest
	if goforest < 40:goforest += rand(0,2)
	! chance to get lost
	lostrand = rand(1,40)
	! Determine where Sveta got lost in the forest
	lost = 2
	! chance to rip clothes
	riprand = rand(1,10)
	foresteventrand = rand(1,10)
	new_boletus = 0
	new_bilberry = 0
	
	'<center><H4>Forest</H4></center>'
	if month >= 4 and month <= 10:
		if hour >= 6 and hour < 22:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest2.jpg"></center>'
			'You are in the forest. The fresh air and singing birds light up your soul with joy.'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_night2.jpg"></center>'
			'You are in the forest. Night in the woods is scary and uncomfortable.'
		end
	else
		if hour >= 8 and hour < 18:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_winter2.jpg"></center>'
			'You are in the forest. The forest in the winter is very beautiful, the fresh air makes you happy.'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_winter_night2.jpg"></center>'
			'You are in the forest. Night in the forest during wintertime is very scary and uncomfortable.'
		end
	end
	
	if goforest < 40:'You don''t know this part of the forest very well, it''s best if you don''t go any deeper into the woods.'
	if riprand >= 8: gs 'gadforest', 'torncloth'
	
	! mushroom and berry events
	if foresteventrand <= 1 and month >= 6 and month <= 9 and mushroom_pickersday ! daystart:mushroom_pickers = 2 & gt 'gadforest_event', 'mushroom_pickers1'
	if foresteventrand <= 2 and month >= 6 and month <= 9 and mushroom_pickersday ! daystart:mushroom_pickers = 2 & gt 'gadforest_event', 'mushroom_pickers3'
	! hunters events
	if foresteventrand <= 5 and hour > 8 and hour < 20 and foresteventday ! daystart:gt 'gadforest_event', 'forest_hanters'

	act 'Go back to the edge of the forest':
		lostrand = rand(1,40)
		if goforest >= lostrand:minut += 30 & gt 'gadforest','1'
		if goforest < lostrand:gt 'gadforest', 'gadforestloststart'
	end
		
	if hour >= 6 and hour <= 20 and month >= 6 and month <= 9 and boletus + bilberry < 10 and forestday ! daystart:
		act 'Look for mushrooms and berries':
			cla
			*clr
			minut += 30
			forestday = daystart
			gs 'stat'
			
			! mushroom_pickers either not seen today or still at the edge of the forest
			if mushroom_pickers < 2:
				new_boletus += rand(2,3)
				new_bilberry += rand(2,3)
				boletus += new_boletus
				bilberry += new_bilberry
				pcs_mood += 10
				gs 'stat'

				gs 'gadforest', 'clothes1'
				
				if new_boletus > 0 and new_bilberry > 0:'After searching for mushrooms and berries for half an hour you found <<new_boletus>> kg of mushrooms and <<new_bilberry>> kg of berries.'
			! mushroom pickers are in the forest
			elseif mushroom_pickers = 2:
				temp_gf = rand(1,2)
				if temp_gf = 1:
					new_boletus += rand(0,1)
					new_bilberry += rand(0,1)
					boletus += new_boletus
					bilberry += new_bilberry
					if new_boletus > 0 or new_bilberry > 0:pcs_mood += 5
					if new_boletus = 0 and new_bilberry = 0: pcs_mood -= 5
					gs 'stat'

					if new_boletus > 0 or new_bilberry > 0: gs 'gadforest', 'clothes1'
					if new_boletus and new_bilberry = 0: gs 'gadforest', 'clothes2'
					
					if new_boletus > 0 and new_bilberry = 0:'After searching for mushrooms and berries for half an hour you found <<new_boletus>> kg of mushrooms.'
					if new_boletus = 0 and new_bilberry > 0:'After searching for mushrooms and berries for half an hour you found <<new_bilberry>> kg of berries.'
					if new_boletus > 0 and new_bilberry > 0:'After searching for mushrooms and berries for half an hour you found <<new_boletus>> kg of mushrooms and <<new_bilberry>> kg of berries.'
					if new_boletus = 0 and new_bilberry = 0:'After searching for mushrooms and berries for half an hour you found nothing.'
				elseif temp_gf = 2:
					pcs_mood -= 5
					gs 'stat'

					gs 'gadforest', 'clothes2'
					
					'After searching for mushrooms and berries for half an hour you found nothing.'
				end
			end
			act 'Continue':gt 'gadforest','2'
		end
 	end
	
	if hour >= 6 and hour <= 20 and month >= 6 and month <= 9:
		act 'Go further into the forest':
			if goforest >= lostrand:minut += 30 & gt 'gadforest','3'
			if goforest < lostrand:gt 'gadforest', 'gadforestloststart'
	 	end
	end
end

if $ARGS[0] = '3':
	! deep inside forest
	$loc = 'gadforest'	
	$metka = '3'
	$location_type = 'secluded'
	$menu_loc = 'gadforest'
	$menu_arg = '3'
	menu_off = 0
	gs 'gadukino_event', 'sound'
	gs'stat'
	
	*clr & cla
	
	! Add to knowledge of forest
	if goforest < 60:goforest += rand(0,3)
	! chance to get lost
	lostrand = rand(1,60)
	! Determine where Sveta got lost in the forest
	lost = 3
	! chance to rip clothes
	riprand = rand(1,10)
	foresteventrand = rand(1,12)
	new_boletus = 0
	new_bilberry = 0
	
	'<center><H4>Deep Forest</H4></center>'
	if month >= 4 and month <= 10:
		if hour >= 6 and hour < 22:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest3.jpg"></center>'
			'You are deep in the forest. The fresh air and singing birds light up your soul with joy.'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_night3.jpg"></center>'
			'You are deep in the forest. Night in the woods is scary and uncomfortable.'
		end
	else
		if hour >= 8 and hour < 18:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_winter3.jpg"></center>'
			'You are deep in the forest. The forest in the winter is very beautiful, the fresh air makes you happy.'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_winter_night3.jpg"></center>'
			'You are deep in the forest. Night in the forest during wintertime is very scary and uncomfortable.'
		end
	end
	
	if goforest < 60:'You don''t know this part of the forest very well, it''s best if you don''t go any deeper into the woods.'
	if riprand >= 6: gs 'gadforest', 'torncloth'
	
	! mushroom and berry events
	if foresteventrand = 1 and month >= 6 and month <= 9 and foresteventday ! daystart: gt 'gadforest_event', 'basket'
	if foresteventrand = 2 and month >= 6 and month <= 9 and foresteventday ! daystart: gt 'gadforest_event', 'bilberry'
	if foresteventrand = 3 and month >= 6 and month <= 9 and foresteventday ! daystart: gt 'gadforest_event', 'mushroom'
	! hunters events
	if foresteventrand <= 6 and hour > 8 and hour < 20 and foresteventday ! daystart:gt 'gadforest_event', 'forest_hanters'

	act 'Go back to the forest':
		lostrand = rand(1,60)
		if goforest >= lostrand:minut += 30 & gt 'gadforest','2'
		if goforest < lostrand:gt 'gadforest', 'gadforestloststart'
	end
	
	if hour >= 6 and hour <= 20 and month >= 6 and month <= 9 and boletus + bilberry < 10 and thicketday ! daystart:
		cla
		*clr
		minut += 30
		thicketday = daystart
		gs 'stat'
		
		act 'Look for mushrooms and berries':
			new_boletus += rand(3,4)
			new_bilberry += rand(3,4)
			boletus += new_boletus
			bilberry += new_bilberry
			pcs_mood += 10
			gs 'stat'

			gs 'gadforest', 'clothes1'
			
			if new_boletus > 0 and new_bilberry > 0:'After searching for mushrooms and berries for half an hour you found <<new_boletus>> kg of mushrooms and <<new_bilberry>> kg of berries.'
			
			act 'Continue':gt 'gadforest','3'
		end
	end
	
	if hour >= 6 and hour <= 20 and month >= 6 and month <= 9:
		act 'Go further into the forest':
			if goforest >= lostrand:minut += 30 & gt 'gadforest','4'
			if goforest < lostrand:gt 'gadforest', 'gadforestloststart'
	 	end
	end
end

if $ARGS[0] = '4':
	! forest near swamp
	$loc = 'gadforest'	
	$metka = '4'
	$location_type = 'secluded'
	$menu_loc = 'gadforest'
	$menu_arg = '4'
	menu_off = 0
	gs 'gadukino_event', 'sound'
	gs'stat'
	
	*clr & cla
	
	! Add to knowledge of forest
	if goforest < 80:goforest += rand(0,4)
	! chance to get lost
	lostrand = rand(1,80)
	! Determine where Sveta got lost in the forest
	lost = 4
	! chance to rip clothes
	riprand = rand(1,10)
	! dirty
	dirty = 0
	foresteventrand = rand(1,8)
	new_boletus = 0
	new_bilberry = 0
	
	'<center><H4>Forest near the Swamp</H4></center>'
	if month >= 4 and month <= 10:
		if hour >= 6 and hour < 22:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest4.jpg"></center>'
			'You are in the forest near the swamp. The fresh air and singing birds light up your soul with joy.'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_night4.jpg"></center>'
			'You are in the forest near the swamp. Night in the woods is scary and uncomfortable.'
		end
	else
		if hour >= 8 and hour < 18:
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_winter4.jpg"></center>'
			'You are in the forest near the swamp. The forest in the winter is very beautiful, the fresh air makes you happy.'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadforest_winter_night4.jpg"></center>'
			'You are in the forest near the swamp. Night in the forest during wintertime is very scary and uncomfortable.'
		end
	end
	
	if goforest < 80:'You don''t know this part of the forest very well, it''s best if you don''t go any deeper into the woods.'
	if riprand >= 4: gs 'gadforest', 'torncloth'
	
	! mushroom and berry events
	if foresteventrand = 1 and month >= 6 and month <= 9 and foresteventday ! daystart: gt 'gadforest_event', 'bilberry'
	if foresteventrand = 2 and month >= 6 and month <= 9 and foresteventday ! daystart: gt 'gadforest_event', 'mushroom'
	! hunter events
	if foresteventrand <= 4 and hour > 8 and hour < 20 and foresteventday ! daystart:gt 'gadforest_event', 'forest_hanters'

	act 'Go back to the deep woods':
		lostrand = rand(1,80)
		if goforest >= lostrand:minut += 30 & gt 'gadforest','3'
		if goforest < lostrand:gt 'gadforest', 'gadforestloststart'
	end
	
	if hour >= 6 and hour <= 20 and month >= 6 and month <= 9 and boletus + bilberry < 10 and swampforestday ! daystart:
		cla
		*clr
		minut += 30
		swampforestday = daystart
		gs 'stat'
		
		act 'Look for mushrooms and berries':
			new_boletus += rand(4,5)
			new_bilberry += rand(4,5)
			boletus += new_boletus
			bilberry += new_bilberry
			pcs_mood += 10
			gs 'stat'

			gs 'gadforest', 'clothes1'
			
			if new_boletus > 0 and new_bilberry > 0:'After searching for mushrooms and berries for half an hour you found <<new_boletus>> kg of mushrooms and <<new_bilberry>> kg of berries.'
			
			act 'Continue':gt 'gadforest','4'
		end
	end
	
	act 'Go further into the swamp':
		if goforest >= lostrand and goswamp = 1:minut += 30 & gt 'swamp'
		if goforest >= lostrand and goswamp = 0:minut += 30 & gt 'gadforestswamp','1'
		if goforest < lostrand:gt 'gadforest', 'gadforestloststart'
	end
end

!dynamics to ARGS by illume
if $ARGS[0] = 'clothes1':
	if MiraLoc ! 3:
		if $clothingworntype ! 'nude' and CloSkirtShortness = 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/mushroom1.jpg"></center>'
		if $clothingworntype ! 'nude' and $pantyworntype ! 'none' and CloSkirtShortness > 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/mushroom2.jpg"></center>'
		if $clothingworntype ! 'nude' and $pantyworntype = 'none' and CloSkirtShortness > 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/mushroom3.jpg"></center>'
		if $clothingworntype = 'nude':'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/mushroom4.'+rand(1,3)+'.jpg"></center>'

	else
		if $clothingworntype ! 'nude':'<center><img <<$set_imgh>> src="images/characters/village/mira/miramushroom'+rand(1,2)+'.jpg"></center>'
		if $clothingworntype = 'nude':'<center><img <<$set_imgh>> src="images/characters/village/mira/miramushroom_nude'+rand(1,2)+'.jpg"></center>'
	end
end

if $ARGS[0] = 'clothes2':
	if MiraLoc ! 3:
		if $clothingworntype ! 'nude' and CloSkirtShortness = 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gulforest2.jpg"></center>'
		if $clothingworntype ! 'nude' and $pantyworntype ! 'none' and CloSkirtShortness > 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gulforest3.jpg"></center>'
		if $clothingworntype ! 'nude' and $pantyworntype = 'none' and CloSkirtShortness > 0:'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gulforest_tanga1.'+rand(1,2)+'.jpg"></center>'
		if $clothingworntype = 'nude':'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gulforest_nude1.'+rand(1,4)+'.jpg"></center>'
	else
		if $clothingworntype ! 'nude':'<center><img <<$set_imgh>> src="images/characters/village/mira/miramushroom3.jpg"></center>'
		if $clothingworntype = 'nude':'<center><img <<$set_imgh>> src="images/characters/village/mira/miramushroom_nude3.jpg"></center>'
	end
end

if $ARGS[0] = 'gadforestloststart':
	cla
	*clr
	gs 'stat'
	
	! add for rescue in forest by illume
	forest_lost = 1
	if StoryLine = 1: forest_lostday = daystart + rand(1,3)
	
	'<center><H4>Somewhere in the woods</H4></center>'
	if month >= 4 and month <= 10:
		if $clothingworntype ! 'nude':'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadfear1.jpg"></center>'
		if $clothingworntype = 'nude':'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadfear2.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/gadukino/forest/gadfear3.jpg"></center>'
	end
	'You suddenly realize in horror that you are not familiar with the area you are in... you are lost!.'

	act 'Try to find your way out of the forest':
		if lost = 1: gt 'gadforestlost','1'
		if lost = 2: gt 'gadforestlost','2'
		if lost = 3: gt 'gadforestlost','3'
		if lost = 4: gt 'gadforestlost','4'
	end
end

if $ARGS[0] = 'torncloth':
	!the withdrawal strength of the garment, if not jeans. moved from gadforest 1 into args, and call in all forest by illume
	if $clothingworntype ! 'nude':
		if (PCloThinness = 1 or PCloSkirt > 0) and PCloQuality < 5:
			!skirts, not fit, and not high-class enough by illume
			'Oops, your clothes get snagged and tear slightly.'
			dynamic $clothingworntype + 'H[<<clothingwornnumber>>] -= 1'
			!add remove clothes if worn out
			if dyneval('RESULT = <<$clothingworntype>>H[<<clothingwornnumber>>]') <= 0:
				'Your ripped clothes were barely holding together, but now they have finally fell apart.'
				'You are naked now.'
				gadforest_clothestorn = 1
				gs 'clothing', 'strip', 'gadforest', 2
			end
		end
	end
end

--- gadforest ---------------------------------
