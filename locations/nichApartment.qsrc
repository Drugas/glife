# nichApartment
CLOSE ALL

$metka = ''
$loc = 'nichApartment'
$menu_loc = 'nichApartment'
$menu_arg = ''
menu_off = 0

!! A list of used variables is included in nichUtil

if $ARGS[0] = '' or $ARGS[0] = 'start':
	*clr & cla
	tanhouse1 = 1
	$location_type = 'private'
	killvar '$locclass'
	
	if tanwork > 0:
		gs 'nichUtil', 'convert'
	end
	
	gs 'stat'
	'<center><b>Nicholas'' Apartment</b></center>'
	'<center><img <<$set_imgh>> src="images/characters/city/nicholas/apartment/hallway.jpg"></center>'
	'This is the apartment of Tanya''s stepfather Nicholas - it''s a luxurious and spacious city center home.'
	if nichWork = 1:
		'You work and live here together with him, Tanya and her mother, Gala.'
	elseif nichWork > 1:
		'You used to work here.'
	end
	'The corridor''s floor and walls are a tasteful mix of marble and wood. Besides several paintings and small statues for decoration, the only thing of note in it is are a coat and shoe rack for guests.'
	
	if nichWork = 1:
		if nichLastWorkDay ! daystart:
			gs 'nichUtil', 'startWorkday'
		end
		gs 'nichUtil', 'checkOutfit'
	end
	
	
	!'if nichEvaluationLast ! daystart and nichEvaluationLastReq = daystart and hour >= 20 and hour < 23:
		''
		'Your contract states that, around this time, you should go to the living room and talk to Nicholas, so he can evaluate your day''s work.'
	end'!

	act '<b>Leave and go to the city center</b>':
		if $clothingworntype ! 'nude':
			minut += 5
			gt'down'
		else
			msg'<b><font color = red>You can''t walk around the city like that! You need to get dressed first.</font></b>'
			gt $CURLOC
		end
	end
	
	act 'Go to the guest cloakroom':
		minut += 1
		gt 'nichBathGuest'
	end
	
	act 'Go to the kitchen':
		minut += 1
		gt 'nichKitchen'
	end

	if nichWork = 1:
		act 'Go to your bedroom':
			minut += 1
			gt 'nichBedroomServant'
		end
	else
		act 'Go to the servant bedroom':
			minut += 1
			gt 'nichBedroomServant'
		end
	end

	act 'Go to the master bedroom':
		minut += 1
		gt 'nichBedroomMaster'
	end

	act 'Go to the living room':
		minut += 1
		gt 'nichLivingroom'
	end

	act 'Go to Tanya''s room':
		minut += 1
		gt 'nichBedroomTanja'
	end
	
	act 'Go to the study':
		minut += 1
		gt 'nichStudy'
	end

	if nichWork = 1:
		if nichLastInspepection ! daystart :
			if nichOutfitState = 1:
				act '(0:20) Inspect Apartment':
					*clr & cla
					minut += 20
					nichLastInspepection = daystart
					'<center><img <<$set_imgh>> src="images/characters/city/nicholas/maid/inspect_001.jpg"></center>'
					'You carefully inspect each room of the apartment. Whenever you see something you are supposed to clean or fix you take a note.'
					act 'Finish':
						gt $CURLOC
					end
				end
			else
				'You could create a list of your chores for today, but you should wear an appropriate outfit to do so.'
			end
		else
			''
			'You have created a list of your remaining chores for today:'
			if nichMaidHallway >= 4:
				'-the hallway must be cleaned'
			elseif nichMaidHallway >= 2:
				'-the hallway should be cleaned'
			end
		end
		
		gs 'nichUtil', 'renderJob', 'hallwaySweep'
	end


end

--- nichApartment ---------------------------------

