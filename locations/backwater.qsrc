# backwater

$location_type = 'secluded'
$metka = ''
$loc = 'backwater'
gs 'gadukino_event', 'sound'
cls
gs'stat'
clothesAtLocation = FUNC('lost_clothes_here',$CURLOC)

act 'Go back to the hut':minut += 5 & gt'swamp_yard'

'<center><B>Backwater</B></center>'

if month >= 4 and month <= 10:
	if hour >= 6 and hour < 22:
		'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/backwater.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/backwater_night.jpg"></center>'
	end
else
	if hour >= 8 and hour < 18:
		'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/backwater_winter.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/backwater_winter_night.jpg"></center>'
	end
end

'A small creek with relatively clean water. If the weather is warm you can clean yourself up. Also, if your clothes are dirty you can wash them here.'
if clothesAtLocation = 1:'Your clothes lie on the shore.'

if $clothingworntype = 'nude':
	act 'Wash self':
		cla
		*clr
		dynamic $showerdin
		face_write = 0
		body_write = 0
		minut += 30
		pcs_mood += 10
		pcs_willpwr += 5
		inhib_exp += rand(1,3)
		if hantersRape = 1: hantersRape = 2

		if $pantyworntype = 'none':
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/backwaterswim2.jpg"></center>'
		else
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/backwaterswim1.jpg"></center>'
		end
		'You washed yourself in the creek.'

		act 'Further':gt 'backwater'
	end
	if arrsize('sparrvol') > 0:
		act 'Wash off sperm':
			cla
			*clr
			dynamic $showerdin
			lubonus = 0
			pcs_sweat -= 1
			minut += 10
			if hantersRape = 1: hantersRape = 2
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/backwaterswim1.jpg"></center>'
			'You carefully wash off the sperm in the creek.'
			act 'Further':gt 'backwater'
		end
	end
else
	if (cumloc[6] = 1 or cumloc[7] = 1):
		act 'Clean the sperm off your clothes':
			cla
			cumspclnt = 6 & gs 'cum_cleanup'
			minut += 5
			if hantersRape = 1: hantersRape = 2
			gs'stat'
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/cumwash.jpg"></center>'
			'You clean the cum stains off your clothes.'
				act 'Finish':gt 'backwater'
		end
	end
end

if temper >= 10:
	act 'Wash your face':
		cla
		*clr
		minut += 5
		pcs_makeup = 1
		cumspclnt = 9
		face_write = 0
		gs 'cum_cleanup'
		cumspclnt = 11
		gs 'cum_cleanup'
		cumspclnt = 13
		gs 'cum_cleanup'
		if pcs_sweat > 19 : pcs_sweat -= 5
		gs'stat'
		'<center><img <<$set_imgh>> src="images/shared/home/bathroom/facesp.jpg"></center>'
		*nl
		'You carefully wash your face, removing any impurities.'
		act 'Further':gt 'backwater'
		if hantersRape = 1: hantersRape = 2
	end
end

if clothesAtLocation = 0:
	if $clothingworntype ! 'nude' and temper >= 15:
		act'Undress':
			if hantersKnowSlut > 0 or hanters = 0 or hour < 8 or hour >= 20 or pcs_inhib > 35:
				cla
				*clr
				minut+=5
				gs 'underwear', 'remove'
				if hantersRape = 1: hantersRape = 2

				if CloSkirtShortness > 0:
					'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/backwaterundress_ski.jpg"></center>'
				else
					'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/backwaterundress.jpg"></center>'
				end
				gs 'clothing', 'strip', 'backwater'
				'You do not hesitate getting undressed, not caring if anyone is watching.'
				act'Back':gt'backwater'
			else
				cla
				*clr
				'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/noundress.jpg"></center>'
				'You hesitate getting undressed, what if someone suddenly sees you?'
				act'Back':gt'backwater'
			end
		end
	end
else
	if $clothingworntype ! 'nude':
		act 'Pick up your clothes':
			cla
			*clr
			gs 'clothing', 'recover_lost_clothes', 'backwater'
			'You pick up your clothes laying on the ground.'
			gt $loc, $metka
		end
	else
		act 'Get dressed':
			cla
			*clr
			gs 'clothing', 'recover_lost_clothes', 'backwater', 1
			gs 'underwear', 'wear'
			'You pick up your clothes laying on the ground and get dressed.'
			gt $loc, $metka
		end

	end
end

if $clothingworntype = 'nude' and hantersKnowSlut = 0 and hanters = 1 and hour > 8 and hour < 20 and hanterknowday < day:
	cla
	*clr
	minut += 5
	gs 'stat'
	'<center><B>Backwater</B></center>'
	'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/hunter_looks.jpg"></center>'
	'While cleaning yourself in the backwater, you suddenly get the feeling that you are not alone here. Turning around, you see one of the hunters. He''s just standing there watching you, his camouflage is almost indistinguishable compared to the bushes and trees.'

	if clothesAtLocation = 1:
		act'Get dressed':
			cls
			minut += 5
			gs 'underwear', 'wear'
			gs 'clothing', 'recover_lost_clothes', 'backwater', 1

			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/swampbackwaternude1.jpg"></center>'
			'Noticing the hunter, you rush over to your clothes and begin dressing yourself in panic. As you get fully dressed, you look over your shoulder, expecting the hunter standing there, but there is no one there.'
			act'Further':gt 'backwater'
		end
	else
		act'Hide':
			cls
			minut += 1
			'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/swampbackwaternude2.jpg"></center>'
			'Covering your breasts and crotch you jump into the nearest bush. In your state of fear and confusion, you start scanning the area but you don''t see anyone there. You quickly run back towards the cabin.'
			act'Go back to the hut':gt 'swamp_yard'
		end
	end
	act'Stay naked':
		cls
		hanterslut += 1
		hanterknowday = day
		gs'stat'
		'<center><img <<$set_imgh>> src="images/locations/gadukino/hunters/swampbackwaternude3.jpg"></center>'
		'You decide that you have nothing to be afraid of, and continue on completely nude. The hunter looks at you for a minute or so, all of a sudden you hear a "Hmph", him turning around and going away.'
		act'Further':gt 'backwater'
	end
	exit
end

if hantersRape = 2:
	cla
	*clr
	minut += 5
	hantersRape = 3
	gs 'stat'

	'<center><img <<$set_imgh>> src="images/locations/gadukino/sex/hunter/backwaterrelax.jpg"></center>'
	'You sit down by the shore, resting.'

	act 'Further':gt 'hanters', 'start'
end

--- backwater ---------------------------------

