# portmirror

cla
clr
gs 'stat'
*pl '<center><img src="<<FUNC(''$face_image'')>>"></center>'
*nl
if daystart < 10 and StoryLine = 0:'In the mirror, you see a strange woman''s face looking back at you that exactly copies all your actions. You can hardly accept the idea that this is now you.'
if daystart >= 10 and daystart < 30 and StoryLine = 0:'In the mirror you see a woman''s face to which you have become accustomed to. But sometimes, you get the feel that this is not your face.'
*pl '<<$hair>>'
*pl '<<$mop>>. <<$lip>>'
*pl '<<$skin>>'
*pl '<<$vnesh>>'

if curly > 0: '<br>Your curly hair will last for another <<curly>> days.'
if hbraids > 0: '<br>Your braided hair will last for another <<hbraids>> days.'
if hcol ! nathcol:
	if dyefade > 0 and dyefade < 7: '<br>Your hair dye is beginning to fade and your roots are showing.'
	if dyefade = 0: '<br>Your hair dye has faded and looks terrible.'
end

set $makeUpString = '<br>'

if kosmetica > 0: $makeUpString += 'Cosmetics: <<kosmetica>>'
if kosmetica = 0: $makeUpString += 'You have no makeup'

if lipbalm > 0: $makeUpString += ' | Lip Balm: <<lipbalm>>'
if lipbalm = 0: $makeUpString += ' | You have no lip balm left'

*pl $makeUpString
*nl

set $makeupLight = 'You subtly apply some neutral tones, with a light application of makeup to bring out your natural beauty.'
set $makeupNatural = 'You apply some mildly vibrant tones and colors of makeup to cover your minor imperfections and enhance your best features.'
set $makeupHeavy = 'You apply some deeper, richer shades of makeup, thick enough to cover most imperfections, while drawing attention to your eyes and lips.'
set $brushHair = 'You brush your hair.'
set $applyLipbalm = 'You apply some moisturizing balm on your lips.'

! WD: Cheat Unused ~ 'AutoHapri'
if hapri = 0 and AutoHapri = 1:
	minut += 3
	hapri = 1
	*pl $brushHair
end

! WD: Cheat Unused ~ 'AutoMakeup'
if mop = 1 and kosmetica > 0 and AutoMakeup = 1:
	mop = AutoMakeupType
	if kosmetica < 3 and mop = 4:mop = 3
	if kosmetica < 2 and mop = 3:mop = 2
	if kosmetica < 1 and mop ! 1:
		mop = 1
		'You have no make-up left.'
	else
		if mop = 2:
			*pl $makeupLight
			minut -= 3
			kosmetica -= 1
		end

		if mop = 3:
			*pl $makeupNatural
			minut -= 5
			kosmetica -= 2
		end

		if mop = 4:
			*pl $makeupHeavy
			minut -= 10
			vidageday -= 1
			kosmetica -= 3
		end
	end
end

! WD: Cheat Unused ~ 'AutoLipBalm'
if lipbalm > 0 and lipbalmstat <= 0 and AutoLipBalm = 1:
	minut -= 10
	lipbalm -= 1
	lipkoef += rand(0, 1)
	if lipkoef > 50:
		lipkoef = 0
		lip += 1
	end

	lipbalmstat += 8
	*pl $applyLipbalm
end

if hapri = 0:
	act 'Brush your hair':
		cla
		minut += 3
		hapri = 1
		*pl $brushHair
		act 'View results':gt 'portmirror'
	end
end

if mop = 1 and kosmetica > 0:
	act 'Apply makeup':
		cla
		*clr
		'<center><img src="images/etogame/mop.jpg"></center>'
		*nl
		*pl 'You consider for a moment which look you want to gor for.'
		*nl
		act 'Light make-up':
			cla
			minut += 3
			kosmetica -= 1
			mop = 2
			recmop = mop
			*pl $makeupLight
			act 'View results':gt 'portmirror'
		end

		if kosmetica >= 2:
			act 'Natural makeup':
				cla
				minut += 5
				kosmetica -= 2
				mop = 3
				recmop = mop
				*pl $makeupNatural
				act 'View results':gt 'portmirror'
			end
		end

		if kosmetica >= 3:
			act 'Heavy makeup':
				cla
				minut += 10
				kosmetica -= 3
				vidageday -= 1
				mop = 4
				recmop = mop
				*pl $makeupHeavy
				act 'View results':gt 'portmirror'
			end
		end
	end
end

if lipbalm > 0 and lipbalmstat <= 0:
	act 'Apply lip balm':
		cla
		minut += 10
		lipbalm -= 1
		lipkoef += rand(0, 1)
		if lipkoef > 50:
			lipkoef = 0
			lip += 1
		end

		lipbalmstat += 8
		*pl $applyLipbalm
		act 'View results':gt 'portmirror'
	end
end

act 'Move away from the mirror':gt '<<$gt1>>', '<<$gt2>>'

--- portmirror ---------------------------------

