# mirror
if $ARGS[0] = 'start':
	*clr & cla
	gs 'stat' &! It should be here, because some of the strigs have to be initialized
	if player_avatar = 1:
		'<center><img <<$set_imgh>> src="images/avatar.jpg"></center>'
	else
		'<center><img <<$set_imgh>> src="<<FUNC(''$face_image'')>>"></center>'
	end

	if mirror_steam ! hour: mirror_steam = 100
	if $loc = 'vanrPar' and mirror_steam = hour or $loc = 'vanr' and mirror_steam = hour or $loc = 'yvan'and mirror_steam = hour: gt'mirror','steam'

	if pcs_hairbsh = 0:
		act 'Brush your hair': xgt 'mirror', 'brush'
	end

	*nl
	if daystart  < 10 and StoryLine = 0:'In the mirror, you see a strange woman''s face looking back at you that exactly copies all your actions. You can hardly accept the idea that this is now you.'
	if daystart >= 10 and daystart < 30 and StoryLine = 0:'In the mirror you see a woman''s face to which you have become accustomed to. But sometimes, you get the feel that this is not your face.'
	'<<$hair>>'
	'<<$pcs_makeup>>. <<$lip>>'
	'<<$skin>>'
	'<<$pcs_apprnc>>'

	if curly > 0 and defcurly = 0: '<br>Your curly hair will last for another <<curly>> days.'
	if straight > 0 and defcurly = 1: '<br>Your straightened hair will last for another <<straight>> days.'
	if hbraids > 0: '<br>Your braided hair will last for another <<hbraids>> days.'
	if pcs_haircol ! nathcol:
		if dyefade > 0 and dyefade < 7: '<br>Your hair dye is beginning to fade and your roots are showing.'
		if dyefade = 0: '<br>Your hair dye has faded and looks terrible.'
	end


	if kosmetica > 0: *nl 'Cosmetics: <<kosmetica>>' else *nl 'You have no makeup'
	if lipbalm > 0: *pl ' | Lip Balm: <<lipbalm>>' else *pl ' | You have no lip balm left'
	*nl

	$makeupLight =   'You subtly apply some neutral tones, with a light application of makeup to bring out your natural beauty.'
	$makeupNatural = 'You apply some mildly vibrant tones and colors of makeup to cover your minor imperfections and enhance your best features.'
	$makeupHeavy =   'You apply some deeper, richer shades of makeup, thick enough to cover most imperfections, while drawing attention to your eyes and lips.'

	! WD: Cheat Unused ~ 'AutoHapri'
	if pcs_hairbsh = 0 and AutoHapri = 1:
		minut += 3
		pcs_hairbsh = 1
		'You brush your hair.'
	end

	!! WD: Cheat Unused ~ 'AutoMakeup'
	if pcs_makeup = 1 and kosmetica > 0 and AutoMakeup = 1:
		pcs_makeup = AutoMakeupType
		makupskl_exp += rand(1,3)
		if kosmetica < 3 and pcs_makeup = 4:pcs_makeup = 3
		if kosmetica < 2 and pcs_makeup = 3:pcs_makeup = 2
		if kosmetica < 1 and pcs_makeup ! 1:
			pcs_makeup = 1
			'You have no make-up left.'
		else
			if pcs_makeup = 2:
				*pl $makeupLight
				minut -= 3
				kosmetica -= 1
			end

			if pcs_makeup = 3:
				*pl $makeupNatural
				minut -= 5
				kosmetica -= 2
			end

			if pcs_makeup = 4:
				*pl $makeupHeavy
				minut -= 10
				vidageday -= 1
				kosmetica -= 3
			end
		end
	end

	! WD: Cheat Unused ~ 'AutoLipBalm'

	!! Added the makeup setting.

	if pcs_makeup = 1 and kosmetica >= makeupSetting:
		act 'Apply makeup':
			*clr & cla
			'<center><img <<$set_imgh>> src="images/pc/mop.jpg"></center>'
			*nl
			'You consider for a moment which look you want to go for.'
			*nl
			if makeupSetting = 1:
				cla
				'Without thinking, you instantly apply a sutle amount of makeup to your face.'
				minut += 3
				makupskl_exp += rand(1,3)
				kosmetica -= 1
				pcs_makeup = 2
				'<<$makeupLight>>'
				act 'View results':gt 'mirror', 'start'
			elseif makeupSetting = 2:
				cla
				'Without thinking, you instantly apply a moderate amount of makeup to your face.'
				minut += 5
				makupskl_exp += rand(1,3)
				kosmetica -= 2
				pcs_makeup = 3
				'<<$makeupNatural>>'
				act 'View results':gt 'mirror', 'start'
			elseif makeupSetting = 3:
				cla
				'Without thinking, you instantly apply a heavy amount of makeup to your face.'
				minut += 10
				makupskl_exp += rand(1,3)
				kosmetica -= 3
				vidageday -= 1
				pcs_makeup = 4
				'<<$makeupHeavy>>'
				act 'View results':gt 'mirror', 'start'
			else
				if kosmetica > 0:
					act 'Light make-up':
						cla
						minut += 3
						makupskl_exp += rand(1,3)
						kosmetica -= 1
						pcs_makeup = 2
						'<<$makeupLight>>'
						act 'View results':gt 'mirror', 'start'
					end
				end

				if kosmetica > 1:
					act 'Natural makeup':
						cla
						minut += 5
						makupskl_exp += rand(1,3)
						kosmetica -= 2
						pcs_makeup = 3
						'<<$makeupNatural>>'
						act 'View results':gt 'mirror', 'start'
					end
				end

				if kosmetica > 2:
					act 'Heavy makeup':
						cla
						minut += 10
						makupskl_exp += rand(1,3)
						kosmetica -= 3
						vidageday -= 1
						pcs_makeup = 4
						'<<$makeupHeavy>>'
						act 'View results':gt 'mirror', 'start'
					end
				end

				act 'No makeup':
					cla
					minut += 2
					'After looking in the mirror, you decide that you look fine and don''t need to put on any makeup, especially since you''ll likely be washing it off in a few hours.'
					act 'View results':gt 'mirror', 'start'
				end
			end
		end
	elseif kosmetica < makeupSetting:
		'You don''t have enough makeup to apply your chosen option.'
	end

	if AutoLipBalm = 1:	gs 'mirror','applyLipbalm'
	if lipbalm > 0 and pcs_lipbalm <= 0:
		act 'Apply lip balm':
			cla
			gs 'mirror','applyLipbalm'
			act 'View results':gt 'mirror', 'start'
		end
	end

	if (pcs_makeup <= 1 and kosmetica > 0) or (lipbalm > 0 and pcs_lipbalm <= 0 and pcs_hairbsh = 0):
		act 'Do the works': gt 'mirror', 'works'
	end

	!!New makeup setting
	!!This imforms the person what makeup they have pre selected
	!! 0 - none, 1 - light, 2 - moderate, 3 - heavy

	'Select level for auto make-up: <a href="exec:makeupSetting = 0 & gt ''mirror'', ''start''">none</a>, <a href="exec:makeupSetting = 1 & gt ''mirror'', ''start''">light</a>, <a href="exec:makeupSetting = 2 & gt ''mirror'', ''start''">vibrant</a>, <a href="exec:makeupSetting = 3 & gt ''mirror'', ''start''">thick</a>.'

	if makeupSetting = 0:
		'Currently you have no make-up set.'
	elseif makeupSetting = 1:
		'Currently the level you have set is for light make-up.'
	elseif makeupSetting = 2:
		'Currently the level you have set is for vibrant make-up.'
	elseif makeupSetting = 3:
		'Currently the level you have set is for thick make-up.'
	end

	gs 'stat'

	act 'Move away from the mirror':gt 'mirror','fin'
	
end --- start ---

if $ARGS[0] = 'steam':
	'<center><img <<$set_imgh>> src="images/shared/home/bathroom/mirror1.jpg"></center>'
	'<center>Mirror heavily misted. It must wipe or leave to cool.</center>'
	gs 'stat'
	act 'Wipe the mirror':
		*clr & cla
		minut+=1
		mirror_steam = 100
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/shared/home/bathroom/mirror2.jpg"></center>'
		'<center>You wiped the mirror with a towel.</center>'
		act 'Look in the mirror':gt 'mirror','start'
	end
	act '<b>Move away from the mirror</b>':gt 'mirror','fin'
end &! --- steam ----

if $ARGS[0] = 'brush':
	minut += 3
	pcs_hairbsh = 1
	'You brush your hair.'
	wait 500
	gt 'mirror', 'start'
end &! --- brush ----

if $ARGS[0] = 'works':
	cla
	if kosmetica <= 0:
		'You don''t have any makeup.'
	elseif pcs_makeup = 1 and kosmetica >= makeupSetting:
		*clr
		'<center><img <<$set_imgh>> src="images/pc/mop.jpg"></center>'
		*nl
		'You consider for a moment which look you want to go for.'
		*nl
		if makeupSetting = 1:
			'Without thinking, you instantly apply a sutle amount of makeup to your face.'
			minut += 3
			makupskl_exp += rand(1,3)
			kosmetica -= 1
			pcs_makeup = 2
			'<<$makeupLight>>'
		elseif makeupSetting = 2:
			'Without thinking, you instantly apply a moderate amount of makeup to your face.'
			minut += 5
			makupskl_exp += rand(1,3)
			kosmetica -= 2
			pcs_makeup = 3
			'<<$makeupNatural>>'
		elseif makeupSetting = 3:
			'Without thinking, you instantly apply a heavy amount of makeup to your face.'
			minut += 10
			makupskl_exp += rand(1,3)
			kosmetica -= 3
			vidageday -= 1
			pcs_makeup = 4
			'<<$makeupHeavy>>'
		end
	elseif kosmetica < makeupSetting:
		'You don''t have enough makeup to apply your chosen option.'
	end
	if pcs_hairbsh = 0:
		minut += 3
		pcs_hairbsh = 1
		'You brush your hair.'
	end
	gs 'mirror','applyLipbalm'
	
	act 'View results':gt 'mirror', 'start'
end &! --- works ----

if $ARGS[0] = 'applyLipbalm':
	if lipbalm > 0 and pcs_lipbalm <= 0:
		minut += 10
		lipbalm -= 1
		lipkoef += rand(0, 1)
		if lipkoef > 50:
			lipkoef = 0
			pcs_lip += 1
		end
		pcs_lipbalm += 8
		'You apply some moisturizing balm on your lips.'
	end
end &! --- applyLipbalm ---

if $ARGS[0] = 'fin':
	killvar '$makeupLight'
	killvar '$makeupNatural'
	killvar '$makeupHeavy'
	gt $locM, $metkaM
end &! --- fin ----


--- mirror ---------------------------------

