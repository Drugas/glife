# Gshveyfab
$metka = $ARGS[0]
$loc = $CURLOC
$location_type = 'public_outdoors'

if $ARGS[0] = 'start':
	cla
	*clr
	$menu_loc = 'Gshveyfab'
	$menu_arg = 'start'
	menu_off = 0
	gs'stat'

	'<center><B>TEXTILE FACTORY, PARIS COMMUNE</B></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/factory/gshveyfab.jpg"></center>'
	'You''re standing in front one biggest employers in Pavlovsk, the local textile factory. It''s mostly known for making clothing for the G&M Store, which also opened up a store within the compound.'
	'Opening hours are Monday to Friday, between 08:00 to 20:00.'
	if hour >= 8 and hour < 20 and week >= 1 and week <= 5:
		'Behind the security checkpoint you can see the <a href="exec:minut += 5 & GT ''Gshveyfab'', ''enter''">entrance</a> that every employee passes through.'
	elseif hour < 8 or hour > 20 or week > 5:
		'The factory is currently closed.'
		'Opening hours are Monday through Friday:'
		'First shift - 08:00 to 16:00'
		'Second shift - 16:00 to 20:00'
	end
	if week < 6 and hour >= 8 and hour < 20:
		*pl 'The local <a href="exec:torg = 0 & GT ''budgetclothes'',''start''">G&M Store</a> is currently open.'
	else
		*pl 'The local G&M Store is currently closed. Opening hours are Monday to Friday, between 08:00 to 20:00.'
	end

	if week < 7 and hour >= 8 and hour < 15:
		'In a smaller building right next to the factory is the local tailor shop, a service offered by the factory. Opening hours are Monday to Saturday, between 08:00 to 15:00.'
		act 'Visit the tailor':
			cla
			*clr
			menu_off = 1
			minut += 5
			gs'stat'
			'An elderly man is inspecting some repaired clothes, as you walk in, he stops and smiles gently, greeting you.'
			'"Hello there young lady, do you want to adjust some of your clothing? It''ll cost you 500 <b>₽</b> per item."'

			if money >= 500:
				act 'Resize the clothes you''re wearing (500 <b>₽</b>)':
					cla
					*clr
					minut += 15
					if CloStyle = 5 or $clothingworntype = 'exercise':
						'The tailor is looking irritatingly at you, "Why are you wasting my time with exercise clothing? That type of clothing does not need to be resized..."'
					else
						money -= 500
						dynamic '<<$clothingworntype>>B[<<clothingwornnumber>>] = pcs_hips'
						'You are handed a t-shirt and shorts as you undress and give the clothes to the tailor for adjustment. The tailor is fast and only takes fifteen minutes until your clothes are ready.'
						'You are handed your clothes back and quickly get dressed. You''re pleased with the job that the tailor has done.'
					end

					act 'Leave': minut += 5 & gt 'Gshveyfab', 'start'
				end

				act 'Resize your other clothes':gt 'clothing', 'view_clothing_list', 'resize'
				act 'Resize all your clothes':gt 'portnoi2'
			end
			act 'Leave': minut += 5 & gt 'Gshveyfab', 'start'

		end
	end

	act 'Head to the residential area': minut += 15 & gt 'pavResidential'
	act 'Head to the high street': minut += 15 & gt 'pavCommercial'
end

if $ARGS[0] = 'enter':
CLOSE ALL
	cla
	*clr
	$menu_loc = 'Gshveyfab'
	$menu_arg = 'enter'
	menu_off = 0
	gs'stat'
	'<center><B>TEXTILE FACTORY, PARIS COMMUNE</B></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/factory/gshveyfab.jpg" ></center>'
	'You''re standing inside the building that is accommodating the biggest employer in Pavlovsk, the local textile factory.'
	'There is a work schedule hanging on the wall:'
	'First shift - 08:00 to 16:00.'
	'Second shift - 16:00 to 20:00.'
	*nl
	if workFabrika = 0 and age >= 16 and age < 18:'You can apply for a <a href="exec:minut += 15 & GT ''Gshveyfab'', ''job_start''">part-time job</a> here.'
	if work = 0 and workFabrika = 0 and age >= 18:'You can apply for a <a href="exec:minut += 15 & GT ''Gshveyfab'', ''job_start''">full-time job</a> here.'

	if workFabrika = 1 and week >= 1 and week <= 5 and hour >= 8 and hour < 9 and age >= 18:
		'Your shift is starting now.'
		act 'Start shift': gt'Gshveyfab','job'
	end
	if workFabrika = 1 and week >= 1 and week <= 5 and hour >= 16 and hour < 17 and age >= 16 and age < 18:
		'Your shift is starting now.'
		act 'Start shift': gt'Gshveyfab','job'
	end
	if workFabrika = 1:
		'You can quit your job, but quitting will lead to a deducted wage.'
		act 'Quit job':
			menu_off = 1
			if age >= 16 and age < 18:
				smenaFabrika -= 1
			elseif age >= 18:
				smenaFabrika -= 2
			end
			if smenaFabrika < 0:
				smenaFabrika = 0
			end
			pay = smenaFabrika * 420
			karta += pay
			smenaFabrika = 0
			workFabrika = 0
			work = 0
			minut += 5
			gt 'Gshveyfab','enter'
		end
	end
	act 'Leave': minut += 5 & gt 'Gshveyfab', 'start'
end

if $ARGS[0] = 'job_start':
	cla
	*clr
	$menu_loc = 'Gshveyfab'
	$menu_arg = 'job_start'
	menu_off = 0
	gs'stat'
	'<center><B>FOREMAN''S OFFICE</B></center>'
	'<center><img <<$set_imgh>> src="images/characters/pavlovsk/resident/stepan/nachalnik.jpg" ></center>'
	'You head into the factory foreman, Stepan Sergeyevich office, and ask him if there''s an available job.'
	if age >= 16 and age < 18 and workFabrika = 0:
		'"Sure, have a seat," he says. "Now, the work is difficult and meticulous, but doesn''t require any special knowledge. You should catch on pretty quick. Since you are not an adult yet, you can only work half-time, and the pay is 105 <b>₽</b> per hour. Once you become an adult, we can transfer you to full-time. Naturally, as this is a state-recognized company, we do require all the appropriate documentation. The main shift is on weekdays from 08:00 to 16:00, but you will be working the second shift, from 16:00 to 20:00.'
		'But beware, if you miss more than three days of work in a single month, you will be fired. You will be paid at the end of the month and getting fired before then will forfeit any pay not yet received. So, is that okay?"'

		act 'Accept the job':
			cla
			menu_off = 1
			minut += 10
			work = 1
			workFabrika = 1
			smenaFabrika = 0
			FabrikaProgul = 0
			'You hand him over your documents, which he takes and then reminds you to come in on every weekday, at 16:00.'

			act 'Leave':gt'Gshveyfab', 'enter'
		end

	elseif age >= 18 and workFabrika = 0:
		'"Sure, have a seat," he says. "Now, the work is difficult and meticulous, but doesn''t require any special knowledge. You should catch on pretty quick. Naturally, as this is a state-recognized company, we do require all the appropriate documentation. The main shift is on weekdays from 08:00 to 16:00, and the pay is 105 <b>₽</b> per hour.'
		'But beware, if you miss more than three days of work in a single month, you will be fired. You will be paid at the end of the month, and getting fired before then will forfeit any pay not yet received. So, is that okay?"'

		act 'Accept the job':
			cla
			menu_off = 1
			minut += 10
			work = 1
			workFabrika = 1
			smenaFabrika = 0
			FabrikaProgul = 0
			'You hand him your documents, which he takes and then reminds you to come in on every weekday, at 08:00.'

			act 'Leave':gt'Gshveyfab', 'enter'
		end
	end

	act 'Leave':gt'Gshveyfab', 'enter'
end

if $ARGS[0] = 'job':
	cla
	*clr
	menu_off = 1
	FabrikaProgul -= 1
	if FabrikaProgul < 0: FabrikaProgul = 0
	if age >= 16 and age < 18:
		minut += 240
		smenaFabrika += 1
	end
	if age >= 18:
		minut += 480
		smenaFabrika += 2
	end
	gs'stat'
	'<center><B>TEXTILE FACTORY, PARIS COMMUNE</B></center>'
	'<center><img <<$set_imgh>> src="images/locations/pavlovsk/factory/gshveyfab.jpg" ></center>'
	'You work hard throughout your shift at the factory. The foreman has you running all over the factory performing all type of tasks. You barely even have time to rest during the shift.'

	act 'Leave': minut += 5 & gt 'Gshveyfab', 'start'
end

--- Gshveyfab ---------------------------------

