# saunawork

$metka = ''
$loc = 'saunawork'
$metkaBroom = ''
$locBroom = 'saunawork'
$metkaM = ''
$locM = 'saunawork'
$location_type = 'public_indoors'
saunaYouRoom = 1

if paymamka = 0:paymamka = 300
if workDolg > 0:paymamka = 300
if workDolg <= 0:paymamka = 500

gs 'stat'
'<center><b><font color="maroon">Sauna</font></b></center>'
'<center><img <<$set_imgh>> src="images/locations/city/residential/sauna/sauna1.jpg"></center>'
'All the walls are covered in <a href="exec:gt ''mirror'', ''start''">mirrors</a>.'

if saunaWorkNow = 1:
	saunaWorkNow = 0
	money -= paymamka
	'Nurse cames to you and takes from you <<paymamka>> per customer.'
end

if money > 0 and workDolg > 0:workDolg -= money & money = 0
if workDolg > 0:'Your debt is <<workDolg>> <b>â‚½</b>'

gs 'stat'

if workDolg <= 0:act 'Leave':gt 'sauna'
if workDolg > 0:act 'Go to your room':gt 'saunaroom'

if workDolg > 0 and pcs_mood <= 10 or workDolg > 0 and SLomka > 0:
	cls
	workDolg += 1500
	gs 'stat'
	'You are beckoned by the nurse. "Hey, come here."'
	'You go up to her and she demands your bared arm. You present your arm as required and she takes out a syringe filled with drugs. "Sit still, I''ll give you a shot now and it will make you feel like normal again."'

	act 'Leave':StrongNarkota += 25 & SNarkTimes += 1 & minut += 10 & gt 'saunawork'

	exit
end

act 'Sit on the couch (1:00)':
	if pcs_makeup > 1 and pcs_hairbsh = 1 and pcs_sweat < 26 and pcs_leghair <= 3 and pcs_pubes <= 3:
		cls
		minut += 60
		gs 'stat'
		'<center><img <<$set_imgh>> src="images/locations/city/residential/sauna/saunawork.jpg"></center>'
		'You spend an hour sitting on the couch waiting for customers.'

		saunaWorkKlient = 0

		if hour >= 21:
			saunaWorkKlient = 1
		elseif hour >= 16:
			if rand(0, 100) >= 10:saunaWorkKlient = 1
		elseif hour >= 12:
			if rand(0, 100) >= 50:saunaWorkKlient = 1
		elseif hour > 8:
			if rand(0, 100) >= 80:saunaWorkKlient = 1
		end

		if saunaWorkKlient = 1:
			cls
			gs 'stat'
			'A client has arrived at the sauna, and the girls line up so that the customer can choose his favorite.'
			'<center><img <<$set_imgh>> src="images/locations/city/residential/sauna/saunawhore.jpg"></center>'

			if rand(0, 120) < pcs_apprnc:
				'Client jabs a finger at you, "This one."'
				act 'Go to the sauna with client':
					saunaWorkKlient = 1
					guy += 1
					slutty += 1
					SaunaWhore += 1
					gt 'saunawhore', 'clientGate'
				end
			else
				'The client examines you closely but turns away to chose another girl.'

				act 'Continue':gt 'saunawork'
			end
		else
			'The client chooses another girl and she leads him to one of the back rooms.'

			act 'Continue':gt 'saunawork'
		end

	else
		cla
		'The brothel madam comes over to you and says, "Our customers expect you to be clean, shaven, made-up and with your hair looking nice. Go and sort yourself out, now!"'
		act 'Leave':gt 'saunawork'
	end
	
	exit
end

if clenerSauna ! daystart:
	act 'Clean toilets (1:00)':
		cla
		*clr
		minut += 60
		clenerSauna = daystart
		pcs_mood -= 25
		workDolg -= 100
		pcs_sweat += 30
		'<center><img <<$set_imgh>> src="images/locations/city/residential/office/clener2.jpg"></center>'
		'You scrub the sauna toilets for an hour until they shine.'

		act 'Leave':gt 'saunawork'
	end
end

if clenerSauna2 ! daystart:
	act 'Scrub floors (1:00)':
		cla
		*clr
		minut += 60
		clenerSauna2 = daystart
		pcs_mood -= 25
		workDolg -= 100
		pcs_sweat += 30
		'<center><img <<$set_imgh>> src="images/locations/city/residential/office/clener1.jpg"></center>'
		'You spend an hour mopping and scrubbing all the floors.'

		act 'Leave':gt 'saunawork'
	end
end


act 'Take a shower (0:15)':
	cla
	*clr
	minut += 15
	pcs_horny += 1
	pcs_hairbsh = 0
	pcs_makeup = 1
	dynamic $showerdin

	if frost > 0:frost = 0

	'<center><img <<$set_imgh>> src="images/shared/home/bathroom/dush.gif"></center>'
	'You go into the bathroom and turn on the shower. Lather your body and wash it off in the shower.'

	if pcs_leghair > 3 or pcs_pubes > 3:
		act 'Get razor':dynamic $saunabrit
	end

	if pcs_horny >= 90:
		act 'Point shower at pussy':
			cla
			*clr
			if pcs_exhibition < 30:exhibition_exp += rand(1,3)
			pcs_mood += 15
			pcs_willpwr += 15
			'<center><img <<$set_imgh>> src="images/shared/home/bathroom/wash2.jpg"></center>'
			'You turn the shower head on your pussy and let the jet of warm water caress you. Soon you feel like a warm wave covers your entire body and you begin to shudder in orgasm.'

			$orgasm_or = 'yes'
			gs 'arousal', 'masturbate', 30
			gs 'arousal', 'end'
			gs 'stat'
			
			act 'Get out and get dressed':gt 'saunawork'
		end
	elseif pcs_horny >= 40 and pcs_horny < 90:
		act 'Point shower at pussy':
			cla
			*clr
			pcs_mood += 10
			pcs_willpwr += 10
			'<center><img <<$set_imgh>> src="images/shared/home/bathroom/wash2.jpg"></center>'
			'You turn the shower head on your pussy and let the jet of warm water caress you. First you find this very nice, but after a while, it gets a little painful. You realise this is pointless and stop masturbating.'

			gs 'arousal', 'masturbate', 30
			gs 'arousal', 'end'
			gs 'stat'
			
			act 'Get out and get dressed':gt 'saunawork'
		end
	end

	act 'Get out and get dressed':gt 'saunawork'
end

$saunabrit = {
	cls
	if workDolg > 0:
		'There are a handful of cheap razors you can use. At least they don''t make you pay for them, but only because they don''t know who has used which blades.'
	else
		'You rinse the razor blade it will be good for another <<stanok>> uses.'
	end

	if pcs_pubes > 3 or pcs_leghair > 3:
		act 'Shave legs and pubes (0:20)':
			cla
			*clr
			minut += 20
			pcs_leghair = 0
			pcs_pubes = 0
			pcs_horny += 10
			if workDolg = 0:stanok -= 1
			'<center><img <<$set_imgh>> src="images/shared/home/bathroom/shave3.jpg"></center>'
			'You lathered your pubis and legs, then shave all the hair off them. You run your hands over your lower body to make sure its nice and smooth which makes you feel a bit horny.'

			act 'Get out and get dressed':gt 'saunawork'
		end
	end

	if pcs_leghair > 3:
		act 'Shave your legs (0:15)':
			cla
			*clr
			minut += 15
			pcs_leghair = 0
			pcs_horny += 5
			if workDolg = 0:stanok -= 1
			'<center><img <<$set_imgh>> src="images/shared/home/bathroom/shave.jpg"></center>'
			'You lathered up and shaved the hair off them.'

			act 'Rinse razor':dynamic $saunabrit
		end
	end

	if pcs_pubes > 3:
		act 'Shave pubes (0:15)':
			cla
			*clr
			minut += 15
			pcs_pubes = 0
			pcs_horny += 5
			if workDolg = 0:stanok -= 1
			'<center><img <<$set_imgh>> src="images/shared/home/bathroom/shave3.jpg"></center>'
			'You lathered your pubis, then shaved all the hair from it.'

			act 'Rinse razor':dynamic $saunabrit
		end
	end

	act 'Get out and get dressed':gt 'saunawork'
}

if mesec > 0 and isprok = 0 and workdolg > 0:
	act 'Use a tampon (0:05)':
		cla
		*clr
		isprok = 1
		minut += 5
		pcs_mood -= 5
		workDolg += 10

		'<center><img <<$set_imgh>> src="images/shared/home/bathroom/wash3.jpg"></center>'
		'You use a tampon, it will add another 10rub to your debt, but the alternative is unpleasant.'

		act 'Finish':gt $locM, $metkaM
	end
end

--- saunawork ---------------------------------

