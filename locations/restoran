# restoran
if $ARGS[0] = 'start':
	cla
	clr
	*clr
	minut += 5
	gs 'stat'
	'<center><b><font color="maroon">Restaurant</font></b></center>'
	'<center><img src="images/etogame/restoran.jpg"></center>'
	fcolor = rgb(0, 0, 0)
	bcolor = rgb(255, 255, 255)
	lcolor = rgb(106, 90, 205)

	!!eto there_were glamur > 1 and vnesh > 50
	if $clothingworntype = 'formal' and mop > 1 and hapri = 1 and sweat <= 0:
		'A well-dressed man at the entrance politely asks you whether you prefer the VIP section or the common room'

		if money >= 5000:
			act 'VIP room (5,000 <b>₽</b>)':
				cla
				money -= 5000
				gt 'restoran', 'vip'
			end
		end

		act 'Common room':gt 'restoran', 'zal'
	else
		'A man in a suit politely informs you that customers must look presentable and wear formal clothing if they wish to dine here.'
	end

	act 'Leave the restaurant':
		cla
		minut += 5
		gt 'down'
	end

	if vladimirday = daystart and vladimirQW = 21 and hour = 20:gt 'qwmeet'
end

if $ARGS[0] = 'zal':
	cla
	clr
	*clr
	minut += 5
	gs 'stat'
	fcolor = rgb(0, 0, 0)
	bcolor = rgb(255, 255, 255)
	lcolor = rgb(106, 90, 205)
	'<center><b><font color="maroon">restaurant, common room</font></b></center>'
	'<center><img src="images/etogame/restoran2.jpg"></center>'
	'The room emits a good atmosphere with its soft lighting and light furniture. You hear the sounds of music, the clinks of glasses and female laughter. It seems you can finally relax a little.'
	!!etogame
	nastjarand = rand(0, 100)

	if nastjarand > 80 and nastjaqw = 0 and energy > 15 and nastjaday ! daystart:
		cls
		nastjaday = daystart
		gs 'stat'
		'<center><img src="images/etogame/nastja1.jpg"></center>'
		'In the hall you notice a girl, sitting alone. She looks pretty attractive. Your eyes linger on her for a bit and she seems to feel your gaze as she suddenly looks at you.'

		act 'Look down':
			cla
			'She turns away'

			act 'Leave':gt 'restoran', 'zal'
		end

		act 'Smile at her':gt 'nastja', 'pos1'
	end

	if money >= 5000:
		act 'Eat (5,000 <b>₽</b>)':
			cla
			minut += 60
			energy += 60
			fat += 2
			water = 100
			willpower += 30
			manna += 80
			health += 20
			money -= 5000
			'You order a wonderful meal, recommended by the chef, and eat it with pleasure in the noisy restaurant hall.'

			act 'Leave':gt 'restoran', 'zal'
		end
	end

	act 'Leave':minut += 5 & gt 'restoran', 'start'
end

if $ARGS[0] = 'vip':
	cla
	clr
	*clr
	minut += 5
	gs 'stat'
	fcolor = rgb(0, 0, 0)
	bcolor = rgb(255, 255, 255)
	lcolor = rgb(106, 90, 205)
	'<center><b><font color="maroon">Restaurant, VIP room</font></b></center>'
	'<center><img src="images/etogame/restoran3.jpg"></center>'
	'You go into the VIP room and can''t help but smile involuntarily. Soft lighting and empire style furniture provide a calm and friendly atmosphere. You hear soft music and finally feel safe enough to let your guard down a little.'

	if money >= 5000:
		act 'Eat (5,000 <b>₽</b>)':
			cla
			minut += 60
			energy += 60
			fat += 2
			water = 100
			willpower += 30
			manna += 80
			health += 20
			money -= 5000
			gs 'stat'
			'You order a wonderful meal, recommended by the chef, and eat it with pleasure in the VIP room of the restaurant.'

			act 'Leave':gt 'restoran', 'vip'
		end
	end
	act 'Leave':
		cla
		minut += 5
		gt 'restoran', 'start'
	end
end
--- restoran ---------------------------------

