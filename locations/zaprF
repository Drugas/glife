# zaprF

if $ARGS[0] = 'start':

	$metka = $ARGS[0]
	$loc = $CURLOC
	cla
	clr
	*clr
	minut += 5
	gs 'stat'
	'<center><b>Gas Station</b></center>'
	'<center><img <<$set_imgh>> src="images/locations/shared/gas/zapr.jpg"></center>'
	'The local gas station, the way the cheap pumps work they will only let you fill the tank.'
	'When the weather is nice sometimes girls will offer a car washing service for some tips, there''s no structure to it so you could always try it.'
	'Cost of 1 liter of petrol 30 <b>₽</b>.'
	*nl

	if hour < 9:
		'It''s too early to wash cars.'
	elseif hour > 19:
		'It''s too late to wash cars.'
	else
		act 'Wash car windows (1:00)':gt 'zaprFWash', 'start'
	end
	act 'Go to the toilet':gt 'zaprF', 'toilet'

	if car > 0:
		if cardrive = 6 or cardrive = 21:'On refueling costs your <a href="exec:GS ''carF'', ''start''"><<$car>></a>.'
	end

	zprbenz = 40 - benz

	if benz < 40 and car > 0 and cardrive = 6 or benz < 40 and car > 0 and cardrive = 21:
		zprbenz = 40 - benz

		if zprbenz * 30 <= money:
			act 'Fill petrol':
				cla
				*clr
				benz = 40
				zprpay = zprbenz * 30
				money -= zprpay

				! WD: Uninitialised ~ 'pol'
				! if pol > 0:'<center><img <<$set_imgh>> src="images/locations/shared/gas/zapr1.jpg"></center>'

				'<center><img <<$set_imgh>> src="images/locations/shared/gas/zapr1.jpg"></center>'

				'You have filled a full tank and paid <<zprpay>> <b>₽</b>.'

				act 'Remove gun':gt 'zaprF', 'start'
			end
		else
			'You do not have enough money at the gas station.'
		end
	end

	if car > 0:
		act 'Buy a canister of gasoline with up to 5 liters of 150 <b>₽</b>':
			cla
			kanistra += 1
			money -= 150
			'You bought a canister of gasoline. (It is automatically added to the trunk)'

			act 'Remove gun':gt 'zaprF', 'start'
		end
	end



	act 'Leave':
		if cardrive = 6:
			gt 'nord'
		elseif cardrive = 21:
			gt 'pavCommercial'
		elseif litltown = 0:
			gt 'nord'
		elseif litltown = 1:
			gt 'pavCommercial'
		end
	end
end

if $ARGS[0] = 'toilet':
	CLOSE ALL
	cla
	*clr
	$locM = 'zaprF'
	$metka = 'toilet'
	$metkaM = 'toilet'
	$loc = 'zaprF'
	$location_type = 'bathroom'
	$gt1 = 'zaprF'
	$gt2 = 'toilet'
	minut += 2

	'<center><h4>Gas station toilet</h4></center>'
	'<center><img <<$set_imgh>> src="images/locations/shared/bathroom/toilet.jpg"></center>'
	'You are in a dirty gas station toilet. At least it provides some privacy.'

	act 'Look in the mirror':
		cla
		gt 'portmirror', 'start'
	end

	if pcs_hairbsh = 0:
		act 'Brush your hair': gt 'portmirror', 'brush'
	end

	if spdirtyc > 0:
		act 'Wipe the cum off your clothes (0:10)':
			cla
			*clr
			cumspclnt = 6
			gs 'cum_cleanup'
			minut += 10
			'<center><img <<$set_imgh>> src="images/shared/sex/cum/frotsp.jpg"></center>'
			'You spend ten minutes wiping and rinsing the cum stains out of your clothes. By the end, all that remain of them are some damp spots that should dry up in a few minutes.'

			act 'Continue':gt 'zaprF', 'toilet'
		end
	end

	dynamic $tampon
	dynamic $quickwash
	dynamic $basin

	act 'Leave the restroom':gt 'zaprF', 'start'
end

--- zaprF ---------------------------------

